//===== rAthena Script =======================================
//= Milk Trader
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Trades bottles for milk
//===== Additional Comments: =================================
//= Fully working
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Raised the price to close zeny exploit [Lupus]
//= 1.2a Switched to Lupus's "loopless" technique.[kobra_k88]
//= 1.3 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4 Implemented checkweight. [L0ne_W0lf]
//= 1.5 Fixed missed variable. (bugreport:1523) [L0ne_W0lf]
//============================================================

prontera,73,140,0	script	Milk Vendor	86,{
	if (checkweight(1201,1) == 0) {
		mes "^FF0000น้ำหนักของท่านเกินกำหนด^000000";
		close;
	}
	mes "[Milk Vendor]";
	mes "ถ้าท่านนำ Empty Bottle และเงิน 15z มาให้ ฉันจะเปลี่ยนเป็น Milk ให้ท่าน 1ขวด!";
	next;
	if (select("~ ซื้อ นมกล่อง:~ เปล่า, แค่แวะมาเฉยๆ") == 1) {
		if (countitem(713) <= 0) {
			mes "[Milk Vendor]";
			mes "โปรดนำ Empty Bottles มาให้ฉัน Empty Bottles ที่แปลว่าขวดเปล่าน้ำ";
			mes "...ไหนล่ะขวด ...?";
			close;
		}
		set .@bottles,countitem(713);
		set .@total_weight,.@bottles * 50;
		set .@total_cost,.@bottles * 15;
		if (Zeny < .@Total_cost) {
			mes "[Milk Vendor]";
			mes "โอ้ว ว๊าาา~!";
			mes "ท่านมีเงินไม่พอ ที่จะแลกเปลี่ยนกับฉัน";
			mes "ค่าบริการทั้งหมด " + .@total_cost + "z";
			close;
		}
		if (MaxWeight - Weight < .@total_weight) {
			mes "[Milk Vendor]";
			mes "ฮืมๆ...";
			mes "น้ำหนักของท่านมีมากเกินไป";
			mes "โปรดกลับไปตรวจสอบสัมภาระในตัวท่านแล้วกลับมาหาข้าใหม่อีกครั้ง";
			close;
		}
		set Zeny, Zeny-.@total_cost;
		delitem 713,.@bottles; //Empty Bottles
		getitem 519,.@bottles; //Milk
		close;
	}
	close;
}
