//===== rAthena Script ======================================= 
//= Episode 12 Socket Enchant NPC
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.1a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================ 

-	script	Leablem#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- รอสักครู่ !! -";
		mes "- ขณะนี้คุณกำลังแบก -";
		mes "- ไอเท็มมากเกินไป -";
		mes "- โปรดนำไอเท็มไปฝากคลัง -";
		mes "- แล้วกลับมาลองอีกครั้ง -";
		close;
	}
	mes "[Leablem]";
	mes "สวัสดี ฉันชื่อ Leablem";
	mes "และฉันก็เป็นผู้เชี่ยวชาญด้าน";
	mes "การเพิ่ม Solt Item";
	mes "ให้กับอาวุธแลกเครื่องป้องกัน";
	mes "แต่ฉันชอบในสิ่งที่ฉันทำและก็ภูมิใจกับมัน";
	next;
	mes "[Leablem]";
	mes "ฉันได้เรียนรู้มากที่สุดในสิ่งที่ได้จากพี่ชายของฉัน Seyablem";
	mes "แต่ฉันคิดว่าฉันยังคงมีจำนวนมากที่จะเรียนรู้";
	mes "ดังนั้นโปรดเข้าใจด้วยว่าการเจาะรูไอเท็ม";
	mes "นั้นยังมีข้อจำกัดไม่เหมือนกับพี่ชาย Seyablem";
	next;
	mes "[Leablem]";
	mes "พี่ฉันไม่ค่อยชอบใจนักเรื่องสอน";
	mes "ความสามารถของเขาให้ฉันเพราะเขา";
	mes "กังวลว่าเขาจะไม่ใช่คนเดียวที่ทำได้";
	mes "อีกต่อไป แต่อย่างไรก็ตาม";
	mes "เขาก็สอนฉันอย่างเต็มที่";
	next;
	mes "[Leablem]";
	mes "ขอบคุณเขามาที่สอนให้ฉันเรียนรู้หลายอย่าง";
	mes "ฉันไม่ค่อยมั่นใจมากเท่าไหร่";
	mes "ในการทดลองทำหลายๆ อย่าง";
	mes "แต่ฉันก็จะทำเท่าที่ฉันจะทำได้ละกัน";
	next;
	mes "[Leablem]";
	mes "จริงๆ แล้วมีอยู่อย่างหนึ่ง";
	mes "ที่พี่ฉันไม่อยากให้ทำ";
	mes "ฉันไม่อยากให้เขาเสียใจ..ดังนั้นฉันมเรื่องนั้นไปดีกว่า";
	mes "ฉันจะไม่พูดถึงมันอีกต่อไป";
	next;
	mes "[Leablem]";
	mes "ช่างเหอะ แล้วท่านมี";
	mes "อะไรมาให้ฉันเจาะรูล่ะ ?";
	mes "ค่าบริการ วัสดุ";
	mes "และ โอกาสสำเร็จนั้น";
	mes "ขึ้นอยู่กับไอเท็มที่จะทำนะ";
	next;
	switch(select("~ อาวุธ:~ เครื่องป้องกัน:~ สิ่งที่ท่านไม่ยอมพูดถึง:~ ข้อมูลอื่นๆ:~ ยกเลิกสนทนา"))
	{
		case 1:
			mes "[Leablem]";
			mes "อาวุธใช่ไหม? ได้เลยไม่มีปัญหา";
			mes "อาวุธแบ่งออกเป็น 4 ประเภท";
			mes "ตามระดับความยากของงาน";
			mes "ประเภท C ง่ายสุดและประเภท S นั้นยากที่สุด";
			mes "ท่านจะลองประเภทไหนล่ะ ?";
			next;
			switch(select("~ ระดับ C:~ ระดับ B:~ ระดับ A:~ ระดับ S"))
			{
				case 1:
					mes "[Leablem]";
					mes "ประเภท C นะ";
					mes "แล้วจะเจาะรูอาวุธอะไรล่ะ";
					next;
					switch(select("~ Book of Mother Earth:~ Book of Billows:~ Book of Gust of Wind:~ Book of the Blazing Sun"))
					{
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10;
					}
				case 2:
					mes "[Leablem]";
					mes "ท่านเลือกประเภท B นะ";
					mes "แล้วจะเจาะรูอาวุธอะไรล่ะ";
					next;
					switch(select("~ Orcish Axe:~ Scimiter:~ Spike"))
					{
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1;
						case 3: callfunc "Func_Socket2",1523,1538,40,61,300,984,1;
					}
				case 3:
					mes "[Leablem]";
					mes "ประเภท A ดูเหมือนท่านจะอยากได้ของดี แต่ไม่มีปัญหา";
					mes "แล้วจะเจาะรูอาวุธอะไรล่ะ ?";
					next;
					switch(select("~ Dragon Killer:~ Katar of Quaking:~ Katar of Raging Blaze:~ Katar of Frozen Icicle:~ Katar of Piercing Wind:~ Golden Mace:~ Oriental Lute:~ Queen's Whip:~ Spectral Spear:~ Gae Bolg:~ Schweizersabel"))
					{
						case 1:  callfunc "Func_Socket2",13001,13030,40,61,500,984,2;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,61,500,984,2;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,61,500,984,2;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,61,500,984,2;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,56,500,984,2;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,56,500,984,2;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,61,500,984,2;
					}
				case 4:
					mes "[Leablem]";
					mes "หืม ประเภท S เลยหรอ ท่านพูดจริงป่าว *โอ้ว...*";
					mes "อันไหนที่ท่านต้องการเจาะรู ?";
					next;
					switch(select("~ Zephyrus:~ Mailbreaker:~ Dragon Slayer:~ Swordbreaker:~ Assasin Dagger:~ Grand Cross:~ Executioner"))
					{
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10;
					}
			}
		case 2:
			mes "[Leablem]";
			mes "เครื่องป้องกันหรือ ? ได้เลยไม่มีปัญหา";
			mes "อาวุธจะแบ่งออกเป็น 4 ระดับที่แตกต่างกัน";
			mes "ขึ้นอยู่กับความยากลำบากในการทำงาน";
			mes "ประเภท C ง่ายสุดและประเภท S นั้นยากที่สุด";
			mes "ท่านจะลองประเภทไหนล่ะ ?";
			next;
			switch(select("~ ระดับ C:~ ระดับ B:~ ระดับ A:~ ระดับ S"))
			{
				case 1:
					mes "[Leablem]";
					mes "ประเภท C นะ";
					mes "แล้วจะเจาะรูอาวุธอะไรล่ะ";
					next;
					switch(select("~ Sunflower:~ Ph.D Hat:~ Big Ribbon:~ Boys Cap"))
					{
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5;
					}
				case 2:
					mes "[Leablem]";
					mes "ท่านเลือกประเภท B";
					mes "แล้วจะเจาะรูอาวุธอะไรล่ะ ?";
					next;
					switch(select("~ Skull Ring:~ High Heels"))
					{
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5;
					}
				case 3:
					mes "[Leablem]";
					mes "ประเภท A ?";
					mes "ดูเหมือนท่านจะอยากได้ของดีแต่ไม่มีปัญหา";
					mes "แล้วจะเจาะรูอาวุธอะไรล่ะ ?";
					next;
					switch(select("~ Pirate Bandana:~ Black Leather Boots"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1;
					}
				case 4:
					mes "[Leablem]";
					mes "หืม ประเภท S !! พูดจริงป่าว? *โอ้ว....*";
					mes "อันไหนที่ท่านต้องการเจาะรู !!";
					next;
					switch(select("~ Mage Coat:~ Holy Robe:~ Sacred Mission:~ Undershirt:~ Pantie"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1;
						case 4: callfunc "Func_Socket2",2522,2523,45,51,1000,985,1;
						case 5: callfunc "Func_Socket2",2339,2371,45,51,1000,985,1;
					}
			}
		case 3:
			mes "[Leablem]";
			mes "โอ้ มันไม่ได้พิเศษอะไรหรอก";
			mes "แต่พี่ฉันคิดว่ามันไม่ใช่สิ่งที่มนุษย์ควรจะไปหลงมัน";
			mes "ฉันไม่อยากทำให้พี่ฉันเสียใจ";
			mes "ฉันควรลืมมันไปเสียดีกว่า";
			next;
			mes "[Leablem]";
			mes "..โอกาสสำเร็จนั้นมีมากอยู่";
			mes "แต่ใช้วัสดุและเงินเยอะมาก";
			mes "ฉันก็ไม่คิดว่าใครจะบ้าทำ";
			mes "ถึงแม้จะเปิดให้บริการเอาไว้";
			next;
			mes "[Leablem]";
			mes "...อืมมๆ...";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes "ดูเหมือนท่านจะสนใจหรือ";
			mes "ถ้าเจ้ะต้องการฉันบอกท่านได้";
			mes "แต่ท่านต้องสัญญาว่าจะเก็บเป็นความลับ";
			mes "ถ้าพี่ฉันรู้ว่าบอกท่านละก็";
			mes "เขาเอาฉันตายแน่";
			next;
			mes "[Leablem]";
			mes "บริการนี้มีโอกาสสำเร็จถึง 90%";
			mes "แต่ต้องใช้ Gold 2ea , เงิน 200,000,000Zeny";
			mes "ฉันสามารถเจาะรู้ให้กับ Hat Of The Sun God";
			next;
			mes "[Leablem]";
			mes "พี่ฉันเคยบอกว่า ตั้งแต่โบราณมีกฏห้ามสิ่งนี้เอาไว้";
			mes "แต่ฉันเชื่อว่ามนุษย์มีความอยากรู้อยากเห็น";
			mes "และชอบทดลอง โดยเฉพาะสิ่งที่ถูกห้าม";
			mes "ฉันก็แค่ไม่อยากให้พี่มาด่าฉันแค่นั้นแหล่ะ";
			next;
			switch(select("~ ใช้บริการ","~ ยกเลิกสนทนา"))
			{
				case 1:
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "...อะ... อะไรน่ะ? ไม่ๆ!";
					mes "นี่จะให้ฉันเป็นคนไม่ดีหรือ";
					mes "อย่าทำกับฉันแบบนี้เลย!";
					mes "ไม่ ฉันไม่ทำ ไม่คือ ไม่ ไม่โว้ย";
					next;
					mes "[Leablem]";
					mes "ไม่ ฉันจะไม่ ไม่โว้ย";
					next;
					mes "[Leablem]";
					mes "ฉันไม่สามารถทำมันได้";
					next;
					mes "[Leablem]";
					mes "ฉันไม่สามารถทำ...ได้..";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "อืมมม...";
					next;
					mes "[Leablem]";
					mes "ท่านนี้ดื้อจริงๆ";
					mes "ก็ได้ฉันจะยกเว้นให้ท่านสักคนอย่าบอกใครล่ะ";
					mes "โดยเฉพาะพี่ชายฉัน สัญญาได้ไหม?";
					next;
					mes "[Leablem]";
					mes "งั้นก็ น้ำสิ่งเหล่านี้มา";
					mes "^FF00002 Gold, เงิน 200,000,000Zeny ค่าบริการ";
					mes "^FF0000และก็ Hat of the Sun God.";
					mes "^FF0000โอกาสสำเร็จมี 90% ท่านรู้ใช่ไหม?^000000";
					mes "เวลามันมีค่า";
					mes "รีบๆ ลงมือเลยดีกว่า";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[Leablem]";
						mes "ฉันจะเริ่มต้นตอนนี้?";
						mes "ภาวนาขอให้ของท่านโชคดี";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[Leablem]";
							mes "ยอดเยี่ยม! ว้าวฉันคิดว่ามีโอกาสประสบความสำเร็จ 90% เป็นความจริง!";
							mes "ดูช่องที่ถูกเพิ่มเข้ามาในสถานที่ที่เหมาะ";
							mes "ขอแสดงความยินดี.";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							getitem 5353,1; //Helm_Of_Sun_
							next;
							mes "[Leablem]";
							mes "ครั้งนี้เป็นครั้งแรกและครั้งสุดท้ายที่ฉันจะทำเพื่อท่าน";
							mes "ไม่เคยบอกใคร";
							mes "เกี่ยวกับเรื่องนี้. เอาล่ะ?";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[Leablem]";
							mes "ฮืม! โอ้พระเจ้าเราล้มเหลว!";
							mes "พระเจ้าฉันคิดว่าเรามีชนิดที่เลวร้ายที่สุดของโชค ...";
							mes "สิ่งที่เราควรทำอย่างไร? ...นี่คือเหตุผล";
							mes "ฉันไม่ได้ต้องการที่จะทำในสถานที่แรก!";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							next;
							mes "[Leablem]";
							mes "ไม่ไม่เคยถามฉันจะทำนั้นเป็นสิ่งที่มีความเสี่ยงอีกครั้ง!";
							mes "เฮ้อ~";
							close;
						}
					}
					else
					{
						mes "[Leablem]";
						mes "อืมมม...";
						next;
						mes "[Leablem]";
						mes "ท่านมีวัสดุดิบไม่พอ";
						mes "แล้วจะให้ฉันลองอะไร?";
						mes "ฉันจะไม่ทำให้ท่านอีกแล้ว";
						mes "ถึงแม้เจ้าจะมีวัตถุดิบครบก็ตามเฮ้อ!~";
						close;
					}
				case 2:
					mes "[Leablem]";
					mes "ไม่เคยบอกใคร";
					mes "เกี่ยวกับสิ่งที่ข้าบอกท่าน เอาล่ะ?";
					close;
			}
		case 4:
			mes "[Leablem]";
			mes "อยากรู้รายละเอียดเพิ่มหรือ?";
			mes "อืมม ฉันไม่คิดว่าจะมีอะไร";
			mes "ที่ต้องรู้เป็นพิเศษนะ";
			mes "แต่ฉันแนะนำเจ้าได้";
			next;
			mes "[Leablem]";
			mes "การเจาะรูไอเท็มในแต่ละเมือง";
			mes "โอกาสในการเจาะสำเร็จ";
			mes "ขึ้นอยู่กับระดับของอุปกรณ์";
			mes "ฉันไม่รู้ว่าจริงไหม";
			mes "แต่ข้าเห็นมันแตกต่างกันทุกเมือง";
			next;
			mes "[Leablem]";
			mes "อย่างเช่น ช่องเจาะรูเมืองหนึ่ง";
			mes "มีโอกาสเจาะรูสำเร็จ";
			mes "กับอาวุธที่อัพเกรดสูง";
			mes "ในขณะที่ช่องเจาะรูอีกเมืองหนึ่ง";
			mes "เจาะสำเร็จกับอาวุธที่อัพเกรดต่ำ";
			next;
			mes "[Leablem]";
			mes "เมื่อท่านล้มเหลวในการเจาะรูไอเท็ม";
			mes "ไอเท็มชิ้นนั้นก็จะหายไป";
			mes "ฉันก็พยายามเต็มที่นะ";
			mes "แต่งานเจาะรูไอเท็มมันเป็นอะไรที่ซับซ้อน";
			next;
			mes "[Leablem]";
			mes "โอ้.มีสิ่งสำคัญท่านต้องระวังเอาไว้";
			mes "^FF0000อย่าถือไอเท็มชนิดเดียวกันเกินกว่า1ชิ้น";
			mes "ในขณะที่ทำการเจาะรูไอเท็ม";
			mes "ไม่งั้นอาวุธที่ท่านไม่ได้ตั้งใจจะเจาะอาจจะกลายเป็นถูกเจาะรู";
			mes "^FF0000และแย่ยิ่งกว่าหากมันล้มเหลวไอเท็มนั้นก็จะหายไป^000000";
			next;
			mes "[Leablem]";
			mes "ฉันก็หวังว่าข้อมูลพวกนี้จะช่วยท่านได้";
			mes "ขอบใจมาก";
			close;
		case 5:
			mes "[Leablem]";
			mes "ไม่เป็นไรแล้ว !";
			close;
	}
}

payon,30,112,6	duplicate(SocketEnchant2)	Leablem#pay	86
//lighthalzen,96,137,3	duplicate(SocketEnchant2)	Leablem#lhz	86
prontera,245,169,3	duplicate(SocketEnchant2)	Leablem#prt	86
morocc,49,39,5	duplicate(SocketEnchant2)	Leablem#moc	86

function	script	Func_Socket2	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[Leablem]";
	if (.@item_req[1] != 0 && .@req_amount[1] != 0)
	{
		mes getitemname(.@item_id) +"...และนี่คือสิ่งของที่ต้องนำมาให้ฉัน";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny สำหรับค่าบริการ,";
		mes "^FF0000"+ .@req_amount[0] +"ea "+ getitemname(.@item_req[0]) +"ea และ "+ .@req_amount[1] +"ea "+ getitemname(.@item_req[1]) +".";
		mes "^FF0000และแน่นอนจงอย่าลืม "+ getitemname(.@item_id) +".^000000";
	}
	else
	{
		mes getitemname(.@item_id) +"? และนี่คือสิ่งของที่ต้องนำมาให้ฉัน";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req/1000 +",000") : .@zeny_req) +",000 zeny ค่าบริการ และ "+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +" สำหรับวัตถุดิบ";
		mes "^FF0000และแน่นอนจงอย่าลืม "+ getitemname(.@item_id) +".";
		mes "^FF0000คุณนำไอเท็มมาให้ครบน่ะ..^000000";
	}
	next;
	mes "[Leablem]";
	mes "ตอนนี้มีไอเท็มครบทั้งหมดแล้ว?";
	mes "และหากท่านล้มเหลวในการเจาะรู";
	mes "ท่านจะต้องสูเสียเสียไอเท็ม "+ ((getiteminfo(.@item_id,5)&EQP_HAND_R) ? "weapon" : "armor") +".";
	mes "ถ้าท่านยังจำได้ "+ ((getiteminfo(.@item_id,5)&EQP_HAND_R) ? "weapon" : "armor") +" ได้รับการอัพเกรดไอเท็มและการ์ด";
	mes "การ์ดที่อยู่ภายในไอเท็มก็จะหายไปด้วย";
	next;
	switch(select("~ ทำการเจาะรู:~ เดี๋ยวก่อน,ขอเวลาแป๊ปนึ่ง"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no .@req_amount[0] and .@req_amount[1] provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Leablem]";
				mes "เอาล่ะ ฉันจะเริ่มงานเดี๋ยวนี้!";
				mes "คุณควรจะภาวนาให้มันสำเร็จ";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect EF_LORD;
					mes "[Leablem]";
					mes "ยอดเยี่ยม.. !!";
					mes "ดูเหมือนว่าฉันจะทำมันได้สำเร็จ ยินดีด้วย!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[Leablem]";
					mes "แล้วเจอกันใหม่..นายท่าน!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Leablem]";
					mes "อ๊า... ฉันเสียใจด้วยมันล้มเหลว";
					mes "คุณอย่าเสียใจไป..จงพยามใหม่ในครั้งหน้า";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Leablem]";
					mes "หวังว่าคุณคงจะโชคดีในครั้งต่อไป!";
					close;
				}
			}
			else
			{
				mes "[Leablem]";
				mes "ท่านงงอะไรอีกนี่?";
				mes "ท่านไม่ได้นำของเหล่านั้นมาเหรอ! ";
				mes "ไปเอามันมาไวๆ เลย";
				close;
			}
		case 2:
			mes "[Leablem]";
			mes "ไว้เจอกันคราวหน้า.";
			close;
	}
}
