//===== rAthena Script ======================================= 
//= Gemstone Trader
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Trade various colors of gemstones for other color gemstones.
//===== Additional Comments: ================================= 
//= 1.0 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Any notes pertaining to the prior trader may be found
//=     in the cities/payon.txt
//= 1.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.2 Updated input with min/max values. [L0ne_W0lf]
//=     Added a checkweight. 
//= 1.3 Fixed checks. [Euphy]
//============================================================ 

payon,60,121,5	script	Jade#pay	754,{
	if (checkweight(1201,1) == 0) {
		mes "^3355FFรอสักครู่! ตอนนี้คุณบรรทุกสิ่งของติดตัวมากเกินไป โปรดกลับมาอีกครั้งหลังจากเก็บสิ่งของของคุณไว้ในคลังคาฟราแล้ว^000000";
		close;
	}
	mes "[Jade]";
	mes "ต้องการ 2 Gemstones";
	mes "ที่มีสีเดียวกัน";
	mes "แล้วฉันจะให้";
	mes "1 Gemstones สีที่แตกต่าง";
	next;
	switch(select("Blue Gemstones แลก Red Gemstones:Red Gemstones แลก Yellow Gemstones:Yellow Gemstones แลก Blue Gemstones")) {
	case 1: callsub S_TradeGems,717,716;
	case 2: callsub S_TradeGems,716,715;
	case 3: callsub S_TradeGems,715,717;
	}

S_TradeGems:
	.@item_req = getarg(0);
	.@item_id = getarg(1);

	if (countitem(.@item_req) < 2) {
		mes "[Jade]";
		mes "ฮะ...!";
		mes "คุณล้อเล่นใช่ไหม?";
		mes "ฉันไม่สามารถให้สิ่งนี้แก่คุณได้";
		mes "ฉันต้องการสิ่งของอย่างน้อย";
		mes "2 "+ getitemname(.@item_req) +"s!";
		close;
	}
	else {
		.@gems = countitem(.@item_req) /2;
		mes "[Jade]";
		mes "ฉันเชื่อว่าฉันสามารถสร้างได้";
		mes "ทั้งหมดเป็น "+ .@gems +" "+ getitemname(.@item_id) +"s";
		mes "โดยใช้ "+ getitemname(.@item_req) +"s";
		mes "ที่คุณมีอยู่ในปัจจุบัน";
		mes "คุณยืนยันไหม?";
		next;
		switch(select("ให้ฉันมากที่สุดเท่าที่จะทำได้:กำหนดจำนวน:ยกเลิก")) {
		case 1:
			delitem .@item_req,.@gems * 2;
			getitem .@item_id,.@gems;
			break;
		case 2:
			mes "[Jade]";
			mes "ระบุจำนวนที่ต้องการแลก";
			mes "จำนวนสูงสุด";
			mes "ที่คุณสามารถใส่ไปได้คือ 100";
			next;
			while(1) {
				input .@input,0,101;
				if (.@input == 0) {
					mes "[Jade]";
					mes "ไม่มีเลย?";
					mes "ฉันเดาว่าคุณ";
					mes "เปลี่ยนใจแล้ว...";
					close;
				}
				else if (.@input > 100) {
					mes "[Jade]";
					mes "เอิร์ม...";
					mes "ฉันขอให้คุณกรอก";
					mes "จำนวนไม่มากไปกว่า 100";
					mes "จำได้ไหม...?";
					next;
				}
				else if (.@gems < .@input) {
					// Custom dialogue
					mes "[Jade]";
					mes "เอิร์ม...";
					mes "คุณไม่มีสิ่งของมากพอที่จะนำมาแลก";
					next;
				}
				else break;
			}
			delitem .@item_req,.@input * 2;
			getitem .@item_id,.@input;
			break;
		case 3:
			mes "[Jade]";
			mes "แน่นอน ไม่มีปัญหา";
			mes "กลับมาได้ทุกเมื่อ";
			close;
		}
		mes "[Jade]";
		mes "เอาล่ะ";
		mes "มาได้เลย";
		mes "กลับมาเมื่อไรก็ได้";
		mes "หืม นั่นมองหาอะไร?";
		mes "มีอะไรอยู่บนหน้าฉันหรือเปล่า?";
		close;
	}
}
