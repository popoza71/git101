//===== rAthena Script ======================================= 
//= Refining NPCs
//===== By: ==================================================
//= Syrus22 (1.1) dafide18 (1.4) Skotlex (1.5)
//===== Current Version: =====================================
//= 3.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Refining NPCs and Metal Salesmen.
//===== Additional Comments: =================================
//= 3.0 Updated several NPC names and locations. [Xantara]
//=     Added WoE map Refiners.
//= 3.1 Added the new refinement & Ore creation NPC's for +11 and above Refinement. [Masao]
//= 3.2 Moved some scripts to Renewal file, other minor changes. [Euphy]
//= 3.2a Added 'disable_items' command. [Euphy]
//= 3.3 Some official script updates. [Euphy]
//= 3.4 Added VIP features. [Euphy]
//= 3.5 Added Refine UI [Atemo, Lemongrass]
//============================================================

// Christopher: Geffen Blacksmith
//============================================================
geffen_in,110,172,0	script	Christopher#1	63,{
	mes "[Christopher Guillenrow]";
	mes "ยินดีต้อนรับสู่ Christopher's Workshop.";
	mes "ท่านสามารถซื้อรายการของทุกสิ่งสำหรับการตีขึ้นรูปได้ที่นี่ ";
	mes "ท่านต้องการอะไรจากข้าล่ะ ?";
	next;
	switch(select("~ ซื้อไอเท็ม Anvil:~ ซื้อไอเท็ม Forging Item:~ ซื้อไอเท็ม Metal:~ หลอมรวมแร่ Rough:~ ยกเลิก")) {
	case 1:
		mes "[Christopher Guillenrow]";
		mes "ทั่งตีเหล็กนั้นจะมีหลายเกรดซึ่งแต่ละเกรดจะแตกต่างกันไป, ท่านรู้หรือเปล่า ? แต่ท่านจะเสียค่าใช้จ่ายมากขึ้น. อย่างที่ข้าจะแนะนำคือเลือกในสิ่งที่ท่านจำเป็นที่สุดก็พอ.";
		next;
		switch(select("~ Anvil ราคา 30,000z:~ Oridecon Anvil ราคา 120,000z:~ Golden Anvil ราคา 300,000z:~ มีรายการทั่งตีเหล็กที่ดีกว่านี้ใหม ?:~ ยกเลิก")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Christopher Guillenrow]";
				mes "ข้าไม่คิดว่าข้าจะให้กับท่านได้หลอก. ข้าต้องการเงินสำหรับการแลกเปลี่ยนเท่านั้น.";
				close;
			}
			getitem 986,1; // Anvil
			Zeny = Zeny-30000;
			mes "[Christopher Guillenrow]";
			mes "นี่คือทั่งตีเหล็กที่ถูกที่สุด, แต่ก็มีประสิทธิภาพพอที่จะสร้างอาวุธขึ้นมา. ขอบท่านสำหรับการช๊อปปิ้งจากร้านของเรา. ท่านสามารถมาที่นี่เมื่อไหร่ก็ได้ตามที่ท่านต้องการ.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Christopher Guillenrow]";
				mes "ข้าไม่คิดว่าข้าจะให้กับท่านได้หลอก. ข้าต้องการเงินสำหรับการแลกเปลี่ยนเท่านั้น.";
				close;
			}
			getitem 987,1; // Oridecon_Anvil
			Zeny = Zeny-120000;
			mes "[Christopher Guillenrow]";
			mes "อาา, ท่านมีตาที่หลักแหลมดีหนิ. สำหรับทั่งตีเหล็กนี้ช่างเหมาะสมสำหรับการสร้างอาวุธขึ้นมา, อา ? ขอบท่านสำหรับการช๊อปปิ้งจากร้านของเรา. ท่านสามารถมาที่นี่เมื่อไหร่ก็ได้ตามที่ท่านต้องการ.";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Christopher Guillenrow]";
				mes "ข้าไม่คิดว่าข้าจะให้กับท่านได้หลอก. ข้าต้องการเงินสำหรับการแลกเปลี่ยนเท่านั้น.";
				close;
			}
			getitem 988,1; // Golden_Anvil
			Zeny = Zeny-300000;
			mes "[Christopher Guillenrow]";
			mes "นี่เป็นทั่งตีเหล็กที่ดีที่สุดในร้านของเรา ! ด้วยสิ่งนี้, ท่านสามารถเป็นเจ้าของช่างตีเหล็กทียอดเยี่ยมที่สุดได้ ! ขอบท่านสำหรับการช๊อปปิ้งจากร้านของเรา. ท่านสามารถมาที่นี่เมื่อไหร่ก็ได้ตามที่ท่านต้องการ.";
			close;
		case 4:
			mes "[Christopher Guillenrow]";
			mes "อืมม, เสียใจด้วย. ข้าไม่ได้มีอะไรที่ดีมากไปกว่า Golden Anvil นี้อีกแล้ว.";
			next;
			mes "[Christopher Guillenrow]";
			mes "ข้าคิดว่า 'Ringgel,' อาจจะมีทั่งตีเหล็กในตำนานอยู่. แต่, ข้าไม่รู้ว่าจะสามารถหาเขาได้จากที่ไหน, เขาอาจจะอยู่ที่ไหนสักแห่งบนโลกนี้.";
			close;
		case 5:
			mes "[Christopher Guillenrow]";
			mes "โอเค, ถ้าหากท่านอยากมาที่นี่, ท่านสามารถมาเมื่อไหร่ก็ได้ตามที่ท่านต้องการ.";
			close;
		}
	case 2:
		mes "[Christopher Guillenrow]";
		mes "ช่างตีเหล็กจำนวนมากจำเป็นที่จะต้องใช้เครื่องมือที่ดีในการหล่อหลอม. ท่านสามารถเลือกรายการที่ท่านต้องการซื้อได้หลังจากนี้ตามที่ท่านต้องการ.";
		next;
		switch(select("~ Mini-Furnace ราคา 150z:~ Iron Hammer ราคา 1,000z:~ Golden Hammer ราคา 3,000z:~ Oridecon Hammer ราคา 5,000z:~ ยกเลิก")) {
		case 1:
			mes "[Christopher Guillenrow]";
			mes "มีนเป็นเครื่องมือที่จำเป็นมากสำหรับการหลอมโลหะ ! ดังนั้น, ท่านต้องการที่จะซื้อจำนวนเท่าไหร่ ? ถ้าหากท่านต้องการยกเลิก, โปรดใส่หมายเลข '0.'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "อา, การแลกเปลี่ยนถูกยกเลิก.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "ท่านสามารถซื้อได้สูงสุดเพียง 500, ต่อครั้งเท่านั้น.";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 150;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "ข้าไม่คิดว่าข้าจะให้กับท่านได้หลอก. ข้าต้องการเงินสำหรับการแลกเปลี่ยนเท่านั้น.";
				close;
			}
			if (checkweight(612,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "ดูเหมือนว่าท่านไม่มีช่องเก็บของที่เพียงพอสำหรับรายการที่ท่านจะซื้อ. โปรดนำรายการของบางอย่างไปฝากไว้กับคาฟราที่อยู่ไกล้ๆนี้เสียก่อน.";
				close;
			}
			getitem 612,.@input; // Portable_Furnace
			Zeny = Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "ขอบท่านสำหรับการซื้อของในร้านของเรา. หากท่านต้องการมาเยี่ยมชมที่นี่อีก, ท่านสามารถมาได้เสมอตามที่ท่านต้องการ.";
			close;
		case 2:
			if (Zeny < 1000) {
				mes "[Christopher Guillenrow]";
				mes "ข้าไม่คิดว่าข้าจะให้กับท่านได้หลอก. ข้าต้องการเงินสำหรับการแลกเปลี่ยนเท่านั้น.";
				close;
			}
			getitem 613,1; // Iron_Hammer
			Zeny = Zeny-1000;
			mes "[Christopher Guillenrow]";
			mes "ขอบท่านสำหรับการซื้อของในร้านของเรา. หากท่านต้องการมาเยี่ยมชมที่นี่อีก, ท่านสามารถมาได้เสมอตามที่ท่านต้องการ.";
			close;
		case 3:
			if (Zeny < 3000) {
				mes "[Christopher Guillenrow]";
				mes "ข้าไม่คิดว่าข้าจะให้กับท่านได้หลอก. ข้าต้องการเงินสำหรับการแลกเปลี่ยนเท่านั้น.";
				close;
			}
			getitem 614,1; // Golden_Hammer
			Zeny = Zeny-3000;
			mes "[Christopher Guillenrow]";
			mes "ขอบท่านสำหรับการซื้อของในร้านของเรา. หากท่านต้องการมาเยี่ยมชมที่นี่อีก, ท่านสามารถมาได้เสมอตามที่ท่านต้องการ.";
			close;
		case 4:
			if (Zeny < 5000) {
				mes "[Christopher Guillenrow]";
				mes "ข้าไม่คิดว่าข้าจะให้กับท่านได้หลอก. ข้าต้องการเงินสำหรับการแลกเปลี่ยนเท่านั้น.";
				close;
			}
			getitem 615,1; // Oridecon_Hammer
			Zeny = Zeny-5000;
			mes "[Christopher Guillenrow]";
			mes "ขอบท่านสำหรับการซื้อของในร้านของเรา. หากท่านต้องการมาเยี่ยมชมที่นี่อีก, ท่านสามารถมาได้เสมอตามที่ท่านต้องการ.";
			close;
		case 5:
			mes "[Christopher Guillenrow]";
			mes "หากท่านต้องการมาเยี่ยมชมที่นี่อีก, ท่านสามารถมาได้เสมอตามที่ท่านต้องการ.";
			close;
		}
	case 3:
		mes "[Christopher Guillenrow]";
		mes "ข้ามีวัสดุเตรียมพร้อมในทุกอย่าง, และ มีข้าเพียงคนเดียวที่มีของท่านภาพเช่นนี้. ในตอนนี้, ท่านต้องการอะไร ?";
		next;
		switch(select("~ Phracon ราคา 200z.:~ Emveretarcon ราคา 1,000z:~ ยกเลิก.")) {
		case 1:
			mes "[Christopher Guillenrow]";
			mes "ดังนั้น, ในตอนนี้ท่านต้องการจำนวนเท่าไหร่ ? ถ้าท่านไม่ต้องการ, กรุณาใส่ '0'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "การแลกเปลี่ยน";
					mes "ถูกยกเลิก.";
					mes "ขอให้โชคดี.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "ท่านสามารถซื้อได้สูงสุดเพียง 500, ต่อครั้งเท่านั้น.";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 200;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "ไหนล่ะเงินของท่าน. ท่านก็น่าจะรู้ว่าข้าไม่สามารถขายได้ในราคาที่ต่ำกว่านี้อีกแล้ว... ท่านต้องรู้ว่าการแลกเปลี่ยนสิ่งของเงินนั้นเป็นสิ่งสำคัญ.";
				close;
			}
			if (checkweight(1010,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "ข้าไม่สามารถแลกเปลี่ยนให้กับท่านได้. ทำไมท่านไม่ลองนำสิ่งของในตัวท่านไปฝากไว้กับคาฟราก่อนล่ะ.";
				close;
			}
			getitem 1010,.@input;
			Zeny = Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "ขอบท่านสำหรับการซื้อของในร้านของเรา. หากท่านต้องการมาเยี่ยมชมที่นี่อีก, ท่านสามารถมาได้เสมอตามที่ท่านต้องการ.";
			close;
		case 2:
			mes "[Christopher Guillenrow]";
			mes "ดังนั้น, ในตอนนี้ท่านต้องการจำนวนเท่าไหร่ ? ถ้าท่านไม่ต้องการ, กรุณาใส่ '0.'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "การแลกเปลี่ยน";
					mes "ถูกยกเลิก.";
					mes "ขอให้โชคดี.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "ท่านสามารถซื้อได้สูงสุดเพียง 500, ต่อครั้งเท่านั้น.";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 1000;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "ไหนล่ะเงินของท่าน. ท่านก็น่าจะรู้ว่าข้าไม่สามารถขายได้ในราคาที่ต่ำกว่านี้อีกแล้ว... ท่านต้องรู้ว่าการแลกเปลี่ยนสิ่งของเงินนั้นเป็นสิ่งสำคัญ.";
				close;
			}
			if (checkweight(1011,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "ข้าไม่สามารถแลกเปลี่ยนให้กับท่านได้. ทำไมท่านไม่ลองนำสิ่งของในตัวท่านไปฝากไว้กับคาฟราก่อนล่ะ.";
				close;
			}
			getitem 1011,.@input;
			Zeny = Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "ขอบท่านสำหรับการซื้อของในร้านของเรา. หากท่านต้องการมาเยี่ยมชมที่นี่อีก, ท่านสามารถมาได้เสมอตามที่ท่านต้องการ.";
			close;
		case 3:
			mes "[Christopher Guillenrow]";
			mes "หากท่านต้องการมาเยี่ยมชมที่นี่อีก, ท่านสามารถมาได้เสมอตามที่ท่านต้องการ.";
			close;
		}
	case 4:
		mes "[Christopher Guillenrow]";
		mes "ข้าสามารถที่จะหลอมรวมแร่ที่บริสุทธิ์อย่าง Oridecon และ Elunium ได้. เพียงแค่ท่านมีแร่บริสุทธิ์เล็กๆจำนวน 5  อัน. อืมม... ท่านต้องการให้ข้าทำอะไรล่ะ ?";
		next;
		switch(select("~ หลอมรวมแร่ Oridecon:~ หลอมรวมแร่ Elunium:~ ยกเลิก")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Christopher Guillenrow]";
				mes "ข้าบอกท่านแล้วไม่ใช่หรือว่า, ข้าต้องการ Rough Oridecons 5ea สำหรับ Oridecon 1ea";
				close;
			}
			else {
				delitem 756,5;
				getitem 984,1;
				mes "[Christopher Guillenrow]";
				mes "นี่แร่บริสุทธิ์ Oridecon ของท่าน. ท่านสามารถมาที่นี่ได้เสมอ, ข้าจะรอพวกท่านอยู่ที่นี่.";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Christopher Guillenrow]";
				mes "ข้าบอกท่านแล้วไม่ใช่หรือว่า, ข้าต้องการ Rough Eluniums 5ea สำหรับ Elunium 1ea";
				close;
			}
			else {
				delitem 757,5;
				getitem 985,1;
				mes "[Christopher Guillenrow]";
				mes "นี่แร่บริสุทธิ์ Elunium ของท่าน. ท่านสามารถมาที่นี่ได้เสมอ, ข้าจะรอพวกท่านอยู่ที่นี่.";
				close;
			}
		case 3:
			mes "[Christopher Guillenrow]";
			mes "หากท่านต้องการมาเยี่ยมชมที่นี่อีก, ท่านสามารถมาได้เสมอตามที่ท่านต้องการ.";
			close;
		}
	case 5:
		mes "[Christopher Guillenrow]";
		mes "หากท่านต้องการมาเยี่ยมชมที่นี่อีก, ท่านสามารถมาได้เสมอตามที่ท่านต้องการ.";
		close;
	}
}

// Paul Spanner: Einbroch Blacksmith Supplier
//============================================================
ein_in01,38,29,0	script	Paul Spanner	63,{
	if (checkweight(1201,1) == 0) {
		mes "- รอสักครู่ !!-";
		mes "- ขณะนี้ท่านกำลังพก -";
		mes "- ไอเท็มมากเกินไป -";
		mes "- โปรดลองอีกครั้ง -";
		mes "- หลังจากที่ท่านลดน้ำหนักลงแล้ว -";
		close;
	}
	mes "[Paul Spanner]";
	mes "ยินดีต้อนรับเพื่อนของข้า";
	mes "ในร้านของข้า, ท่านจะพบทุกอย่างที่ท่านต้องการสำหรับการตีเหล็ก";
	mes "บอกข้าว่าท่านต้องการอะไร";
	next;
	switch(select("~ ซื้อไอเท็ม Anvil:~ ซื้อไอเท็ม Forging Item:~ ซื้อไอเท็ม Metal:~ หลอมรวมแร่ Rough:~ ยกเลิก")) {
	case 1:
		mes "[Paul Spanner]";
		mes "Anvil เป็นไอเท็มที่สำคัญที่สุดสำหรับช่างตีเหล็ก";
		mes "เนื่องจากท่านจะใช้ Anvil หลายๆครั้ง, ท่านควรซื้อ Anvil ที่ดี";
		next;
		switch(select("Anvil - 30,000z.:Oridecon Anvil - 120,000z.:Golden Anvil - 300,000z.:ข้าต้องการ Anvil ที่ดีกว่านี้.:ยกเลิก.")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Paul Spanner]";
				mes "ท่านมีเงินไม่พอ";
				close;
			}
			getitem 986,1; //Anvil
			Zeny = Zeny-30000;
			mes "[Paul Spanner]";
			mes "นี่คือ Anvil ที่ถูกที่สุดและมีความสามารถพื้นฐานที่มากที่สุด";
			mes "ขอบท่านที่ใช้บริการร้านของข้า หากท่านต้องการอะไร, กรุณาแจ้งให้ข้าทราบ";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Paul Spanner]";
				mes "ท่านมีเงินไม่พอ!";
				close;
			}
			getitem 987,1; //Oridecon_Anvil
			Zeny = Zeny-120000;
			mes "[Paul Spanner]";
			mes "อา, ท่านมีความรู้สึกดีต่อ Anvil นะครับ. ช่างตีเหล็กต้องการ Anvil ที่ดีอย่างน้อยเท่านี้";
			mes "ขอบท่านที่ใช้บริการร้านของข้า หากท่านต้องการอะไร, กรุณาแจ้งให้ข้าทราบ";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Paul Spanner]";
				mes "ท่านมีเงินไม่พอ!";
				close;
			}
			getitem 988,1; //Golden_Anvil
			Zeny = Zeny-300000;
			mes "[Paul Spanner]";
			mes "ข้าสามารถรับรู้ถึงความทะเยอทะยานของท่านที่จะเป็นช่างตีเหล็กที่ดีได้จากการท่านเลือกใช้ Golden Anvil นี้!";
			mes "Anvil นี้จะช่วยให้ท่านสร้างอาวุธที่ดีที่สุดแน่นอน";
			close;
		case 4:
			mes "[Paul Spanner]";
			mes "ขอโทษครับ, แต่ข้าไม่ขาย Anvil ที่ดีกว่า Golden Anvil";
			mes "ข้าไม่คิดว่าท่านจะพบ Anvil ที่ดีกว่า Golden Anvil จากที่ไหน";
			close;
		case 5:
			mes "[Paul Spanner]";
			mes "หากท่านต้องการอะไร, กรุณาแจ้งให้ข้าทราบ";
			close;
		}
	case 2:
		mes "[Paul Spanner]";
		mes "ท่านต้องการวัสดุต่าง ๆ เพื่อกระบวนการแปรรูปแร่และตีอาวุธ";
		mes "ข้ามีทุกอย่างที่ท่านต้องการ. ลองดูสิ";
		next;
		switch(select("Mini Furnace - 150z.:Iron Hammer - 1,000z.:Golden Hammer - 3,000z.:Oridecon Hammer - 5,000z.:ยกเลิก.")) {
		case 1:
			.@item = 612;
			.@item_cost = 150;
			.@item_weight = 200;
			mes "[Paul Spanner]";
			mes "ท่านต้องมีเตาหลอมนี้เพื่อแปรรูปแร่";
			next;
			break;
		case 2:
			.@item = 613;
			.@item_cost = 1000;
			.@item_weight = 200;
			break;
		case 3:
			.@item = 614;
			.@item_cost = 3000;
			.@item_weight = 300;
			break;
		case 4:
			.@item = 615;
			.@item_cost = 5000;
			.@item_weight = 400;
			break;
		case 5:
			mes "[Paul Spanner]";
			mes "หากท่านต้องการอะไร, กรุณาแจ้งให้ข้าทราบ";
			close;
		}
		mes "[Paul Spanner]";
		mes "ดังนั้น, ท่านต้องการกี่ชิ้น? หากท่านต้องการยกเลิกการซื้อขาย, กรุณาป้อน '0'";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "การซื้อขายถูกยกเลิกแล้ว. หากท่านต้องการอะไร, กรุณาแจ้งให้ข้าทราบ";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "ท่านสามารถซื้อได้เกิน 500 ต่อครั้ง";
				next;
			}
			else {
				break;
			}
		}
		.@sell = .@input * .@item_cost;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "ท่านมีเงินไม่พอ. ขอโทษนะ, ข้าไม่สามารถขายขาดทุนได้";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "เธอดูสีหน้าซีดนะ. ทำไมไม่ไปลดน้ำหนักก่อนล่ะ?";
			close;
		}
		Zeny = Zeny-.@sell;
		getitem .@item,.@input;
		mes "[Paul Spanner]";
		mes "ขอบท่านที่ใช้บริการร้านของข้า. หากท่านต้องการอะไร, กรุณาแจ้งให้ข้าทราบ";
		close;
	case 3:
		mes "[Paul Spanner]";
		mes "ข้ามีโลหะท่านภาพสูง";
		mes "ดังนั้น, ท่านต้องการซื้อโลหะชนิดไหน?";
		next;
		switch(select("Phracon - 200z.:Emveretarcon - 1,000z.:ยกเลิก.")) {
		case 1:
			.@item = 1010;
			.@item_price = 200;
			break;
		case 2:
			.@item = 1011;
			.@item_price = 1000;
			break;
		case 3:
			mes "[Paul Spanner]";
			mes "หากท่านต้องการอะไร, กรุณาแจ้งให้ข้าทราบ";
			close;
		}
		mes "[Paul Spanner]";
		mes "ดังนั้น, ท่านต้องการกี่ชิ้น? หากท่านต้องการยกเลิกการซื้อขาย, กรุณาป้อน '0'";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "การซื้อขายถูกยกเลิกแล้ว. หากท่านต้องการอะไร, กรุณาแจ้งให้ข้าทราบ";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "ท่านสามารถซื้อได้เกิน 500 ต่อครั้ง";
				next;
			}
			else {
				break;
			}
		}
		.@sell = .@input * .@item_price;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "ท่านมีเงินไม่พอ. ขอโทษนะ, ข้าไม่สามารถขายขาดทุนได้";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "เธอดูสีหน้าซีดนะ. เปลี่ยนไปลดน้ำหนักก่อนมั้ย?";
			close;
		}
		getitem .@item,.@input;
		Zeny = Zeny-.@sell;
		mes "[Paul Spanner]";
		mes "ขอบท่านที่ใช้บริการร้านของข้า. หากท่านต้องการอะไร, กรุณาแจ้งให้ข้าทราบ";
		close;
	case 4:
		mes "[Paul Spanner]";
		mes "ข้าสามารถกระบวนการแปรรูป Oridecon และ Elunium ให้ท่าน";
		mes "ท่านต้องใช้แร่ 5 ชิ้นเพื่อกระบวนการแปรรูปเป็น Oridecon หรือ Elunium 1 ชิ้น";
		mes "ดังนั้น, ท่านต้องการแปรรูป Oridecon หรือ Elunium?";
		switch(select("Oridecon:Elunium:ยกเลิก.")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Paul Spanner]";
				mes "ท่านต้องใช้แร่ 5 ชิ้นเพื่อกระบวนการแปรรูปเป็น Oridecon บริสุทธิ์ 1 ชิ้น";
				close;
			}
			else {
				delitem 756,5; //Oridecon_Stone
				getitem 984,1; //Oridecon
				mes "[Paul Spanner]";
				mes "นี่คือแร่ของท่าน. ขอบท่านที่ใช้บริการ";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Paul Spanner]";
				mes "ท่านต้องการ 5 แร่เพื่อกระบวนการแปรรูปเป็น Elunium ชนิดบริสุทธิ์ 1 ชิ้น";
				close;
			}
			else {
				delitem 757,5; //Elunium_Stone
				getitem 985,1; //Elunium
				mes "[Paul Spanner]";
				mes "นี่คือแร่ของท่าน. ขอบท่านที่ใช้บริการ";
				close;
			}
		case 3:
			mes "[Paul Spanner]";
			mes "หากท่านต้องการอะไร, กรุณาแจ้งให้ข้าทราบ";
			close;
		}
	case 5:
		mes "[Paul Spanner]";
		mes "หากท่านต้องการอะไร, กรุณาแจ้งให้ข้าทราบ";
		close;
	}
}

// Weapon/Armor Refiners
//============================================================
prt_in,63,60,0	script	Hollgrehenn	4_M_03,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Hollgrehenn]";
		mes "ข้าเป็นช่างตีเหล็กที่สามารถอัพเกรดอาวุธและอุปกรณ์ได้";
		mes "ท่านมีไอเท็มใดที่ท่านต้องการให้อัพเกรดหรือไม่?";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Hollgrehenn",0;
		end;
	}
}

morocc_in,73,38,6	script	Aragham	4W_M_03,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Aragham]";
		mes "De~ sert~ Blacksmith~";
		mes "Aragham Ssaleh~ ที่สามารถปรับแต่งทุกอย่าง Ssaleh~";
		mes "ให้ข้าดูว่าท่านต้องการปรับแต่งอะไร~";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Aragham",0;
		end;
	}
}

payon,144,173,5	script	Antonio	4_M_ORIENT01,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Antonio]";
		mes "หยุดจู้จี้จุกจิกและนำอุปกรณ์ที่ท่านต้องการปรับแต่งมาให้ข้า";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Antonio",0;
		end;
	}
}

alberta_in,28,58,0	script	Fredrik	4_M_03,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Fredrik Hermanthorn]";
		mes "ข้าเป็นช่างตีเหล็กที่สามารถปรับแต่งอาวุธหรืออุปกรณ์ของท่านได้";
		mes "ท่านมีอะไรที่ท่านต้องการปรับแต่งหรือไม่?";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Fredrik",0;
		end;
	}
}

yuno_in01,171,21,4	script	Lambert	4_M_ORIENT01,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Lambert]";
		mes "ยินดี...ต้อนรับ...นี่คือ...เหล็กเยี่ยม...ที่สุด...ใน Juno";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Lambert",0;
		end;
	}
}

ein_in01,24,87,5	script	Manthasman	4_M_DWARF,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Manthasman Pruhag]";
		mes "ฮ่าๆๆ, ท่านเคยรู้แล้วนะ. แล้วท่านต้องการอัพเกรดอะไร?";
		mes "วันนี้เราลองบางสิ่งที่เสี่ยงบ้างสิ!";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Manthasman Pruhag",0;
		end;
	}
}

lhz_in02,282,20,7	script	Fulerr	4_M_LGTMAN,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Fulerr]";
		mes "ฮีๆ... ท่านต้องการอัพเกรดใช่ไหม?";
		mes "ฮีๆ.. ถือว่าเสร็จสิ้นแล้ว..";
		mes "ฮีๆ....";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Fulerr",0;
		end;
	}
}

//============================================================
//= Main Refiner Function
//============================================================
//= To allow auto safe refining/multiple refining set the
//= second argument to '1' in the function call.
//= If you enable this function, be sure to edit the value of
//= .@safe to the max safe refine in refine.yml as well.
//============================================================
function	script	refinemain	{
	disable_items;
	.@npc_name$ = getarg(0);
	.@features = getarg(1);
	mes "["+ .@npc_name$ +"]";
	mes "ข้าคือ Armsmith";
	mes "ข้าสามารถอัพเกรดอาวุธ, เกราะ, และอุปกรณ์ทุกชนิดได้, ";
	mes "ดังนั้นให้ข้าทราบว่าท่านต้องการข้าจะอัพเกรดอะไร";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(.@i = 1; .@i<getarraysize(.@indices); ++.@i) {
		if(getequipisequiped(.@indices[.@i])) {
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			.@equipped = 1;
		}
		.@menu$ = .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "["+ .@npc_name$ +"]";
		mes "ข้าไม่คิดว่าข้าสามารถอัพเกรดไอเท็มใดๆที่ท่านมีได้...";
		close;
	}
	.@part = .@indices[select(.@menu$)];

	if(!getequipisequiped(.@part)) { //custom check
		mes "["+ .@npc_name$ +"]";
		mes "ท่านไม่ได้สวมใส่";
		mes "อะไรที่ข้าสามารถอัพเกรดได้";
		emotion ET_FRET;
		close;
	}
	//Check if the item is refinable...
	if(!getequipisenableref(.@part)) {
		mes "["+ .@npc_name$ +"]";
		mes "ข้าไม่คิดว่าข้าสามารถ";
		mes "อัพเกรดไอเท็มนี้ได้เลย...";
		close;
	}
	//Check to see if the items is already +10
	if(getequiprefinerycnt(.@part) >= 10) {
		mes "["+ .@npc_name$ +"]";
		mes "ข้าไม่สามารถอัพเกรดไอเท็มนี้";
		mes "ได้อีก. นี่คือระดับ";
		mes "การอัพเกรดที่สูงสุดแล้ว!";
		close;
	}
	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@price = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_ZENY_COST);
	.@material = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_MATERIAL_ID);
	.@itemtype = getiteminfo( .@refineitemid, ITEMINFO_TYPE );

	if( .@itemtype == IT_ARMOR ){
		.@equip_lv = getequiparmorlv( .@part );

		switch( .@equip_lv ){
			case 1:
				.@safe = 4;
				break;
			default:
				// TODO:
				close;
		}

		// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
		if( VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE) ){
			switch( .@equip_lv ){
				case 1:
					.@price = .@price * 10;
					break;
				default:
					// TODO:
					close;
			}
		}
	}else if( .@itemtype == IT_WEAPON ){
		.@equip_lv = getequipweaponlv( .@part );

		switch( .@equip_lv ){
			case 1:
				.@safe = 7;
				break;
			case 2:
				.@safe = 6;
				break;
			case 3:
				.@safe = 5;
				break;
			case 4:
				.@safe = 4;
				break;
			default:
				// TODO:
				close;
		}

		// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
		if( VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE) ){
			switch( .@equip_lv ){
				case 1:
					.@price = .@price * 40;
					break;
				case 2:
					.@price = .@price * 50;
					break;
				case 3:
					.@price = .@price * 2;
					break;
				case 4:
					.@price = .@price * 2;
					break;
				default:
					// TODO:
					close;
			}
		}
	}else{
		.@safe = 4;
	}

	// TODO: What was this? Maybe shadow?
	// case 5: .@price = .@price * 10; break;

	if(.@features != 1) {
		mes "["+ .@npc_name$ +"]";
		mes "เพื่อที่จะอัพเกรดนี้, ข้าต้องการ";
		mes "หนึ่ง ^003366"+getitemname(.@material)+"^000000 และ";
		mes "ค่าบริการ " + .@price + " Zeny.";
		mes "ท่านต้องการดำเนินการต่อหรือไม่?";
		next;
		if(select("Yes:No") == 2){
			mes "["+ .@npc_name$ +"]";
			mes "ใช่...";
			mes "ไม่ต้องรีบร้อน. ให้ท่านตัดสินใจให้ถูกเวลา";
			close;
		}
		if(getequippercentrefinery(.@part) < 100) {
			mes "["+ .@npc_name$ +"]";
			mes "อ๋อไม่! หากข้าดำเนินการต่อ";
			mes "การอัพเกรดนี้, มีความเสี่ยงที่มัน";
			switch(.@material) {
			case 985:
				mes "อาจถูกทำลาย! นั่นหมายความว่า ^FF0000อุปกรณ์นี้^000000, และ ^FF0000การ์ด^000000 หรือท่านสมบัติพิเศษที่เพิ่มเข้าไปในเกราะนี้, ^FF0000จะหายไป^000000";
				break;
			default:
				mes "ถูกทำลาย, และท่านจะ ^FF0000สูญเสียอาวุธ^000000, ^FF0000การ์ดในอาวุธ^000000,";
				mes "หรือท่านสมบัติพิเศษที่เพิ่มเข้าไป";
				break;
			}
			next;
			mes "["+getarg(0)+"]";
			mes "ข้าไม่สามารถทำให้มันชัดเจนมากขึ้น.";
			mes "เมื่ออาวุธถูกทำลายแล้ว,";
			mes "ไม่มีวิธีในการกู้คืนกลับมา.";
			mes "ท่านจะได้รับโอกาสจริงๆ";
			mes "^FF0000สูญเสียอาวุธนี้^000000 ตลอดกาล.";
			mes "ท่านยังต้องการที่จะอัพเกรดหรือไม่?";
			next;
			if(select("ใช่:ไม่") == 2){
				mes "["+ .@npc_name$ +"]";
				mes "ข้าเห็นด้วยอย่างสมบูรณ์...";
				mes "ข้าอาจเป็นผู้ทำอัพเกรดที่ยอดเยี่ยม, แต่บางครั้งข้าก็ทำผิดได้";
				close;
			}
		}
		if((countitem(.@material) < 1) || (Zeny < .@price)) {
			mes "["+ .@npc_name$ +"]";
			mes "ดูเหมือนท่านจะไม่มี";
			mes "Zeny เพียงพอ หรือ "+getitemname(.@material)+"...";
			mes "ไปหามาเพิ่ม ข้าจะ";
			mes "อยู่ที่นี่ตลอด ถ้าท่านต้องการข้า";
			close;
		}
		Zeny = Zeny-.@price;
		delitem .@material,1;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
		    callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt)) {
			mes "["+ .@npc_name$ +"]";
			emotion ET_FRET;
			mes "เดี๋ยวก่อน...";
			mes "ท่านคิดว่าข้าโง่เหรอ?!";
			mes "ท่านเปลี่ยนไอเทมในขณะที่ข้าไม่ได้มองอยู่! ออกไปจากที่นี่!";
			close;
		}

		if(getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			mes "["+ .@npc_name$ +"]";
			emotion (!rand(5))?ET_MONEY:ET_HUK;
			.@lose = rand(1,3);
			if (.@lose == 1) {
				mes "โอ้! พระเจ้า!";
				mes "ไม่มีอีกแล้ว!";
				mes "ข้าขอโทษจริงๆ แต่ท่านก็รู้ว่าการฝึกฝนทำให้สมบูรณ์แบบได้";
				mes "อืม...ใช่มั้ย? เฮ่ เฮ่...";
			} else if(.@lose == 2) {
				mes "ไม่นะ!";
				mes "มันแตก!";
				mes "ข้า-ข้าขอโทษ!";
			} else {
				mes "อึ!";
				mes "โอ้! พระเจ้า!";
				mes "ขออภัยเกี่ยวกับเรื่องนี้...";
			}
			close;
		}
		mes "["+getarg(0)+"]";
		successrefitem .@part;
		emotion ET_SMILE;
		.@win = rand(1,3);
		if (.@win == 1) {
			mes "สมบูรณ์แบบ!";
			mes "ฮิฮิ!";
			mes "อีกครั้ง,";
			mes "งานที่ไม่มีความบกพร่อง";
			mes "จากมืออาชีพ~";
		} else if(.@win == 2) {
			mes "สำเร็จ...!";
			mes "อีกครั้ง, ทักษะที่น่าทึ่งของข้า";
			mes "เหมือนอาทิตย์ส่องแสงวันนี้";
		} else {
				mes "ฮิฮิ!";
				mes "ข้าเสร็จสิ้นทั้งหมดแล้ว.";
				mes "ไม่มีความสงสัยว่างานของข้า";
				mes "ถูกต้องตามที่ท่านต้องการ";
				mes "ความสมบูรณ์แบบ~";
		}
		close;
	}

// New Refining Functions ========================
	if (.@refinerycnt < .@safe) {
		mes "["+ .@npc_name$ +"]";
		mes "ข้าสามารถปรับแต่งสิ่งนี้ให้ถึงขีดจำกัดที่ปลอดภัยหรือตามจำนวนครั้งที่ต้องการได้ มันเป็นทางเลือกของท่าน";
		next;
		.@menu2 = select("~ ไปถึงขีดจำกัดที่ปลอดภัย","~ ระบุจำนวนครั้ง","~ ข้าเปลี่ยนใจแล้ว...");
	} else
		.@menu2 = 2;
	switch(.@menu2){
	case 1: 
		.@refinecnt = .@safe - .@refinerycnt;
		break;
	case 2:
		next;
		mes "["+ .@npc_name$ +"]";
		mes "ท่านต้องการให้ข้าปรับแต่งรายการของท่านกี่ครั้ง?";
		next;
		input .@refinecnt;
		.@refinecheck = .@refinecnt + .@refinerycnt;
		if (.@refinecnt < 1 || .@refinecheck > 10) {
			mes "["+ .@npc_name$ +"]";
			mes "ข้าไม่สามารถปรับแต่งรายการนี้ได้หลายครั้ง";
			close;
		}
		if(.@refinecheck > .@safe) {
			.@refinecheck = .@refinecheck - .@safe;
			mes "["+ .@npc_name$ +"]";
			mes "โดยจะพยายามปรับแต่งอุปกรณ์" + .@refinecheck + " เกินขีดจำกัดความปลอดภัย อุปกรณ์ของท่านอาจถูกทำลาย... โอเคไหม?";
			next;
			if(select("~ โอเค...","~ ไม่...") == 2){
				mes "["+ .@npc_name$ +"]";
				mes "ท่านว่าอย่างนั้น... เอาเป็นว่า..";
				close;
			}
		}
		break;
	case 3:
		next;
		mes "["+ .@npc_name$ +"]";
		mes "ท่านว่าอย่างนั้น... เอาเป็นว่า..";
		close;
	}
	.@fullprice = .@price * .@refinecnt;
	mes "["+ .@npc_name$ +"]";
	mes "นั่นจะทำให้ท่านเสียค่าใช้จ่าย " + .@refinecnt + " " + getitemname(.@material) + " และ " + .@fullprice + " Zeny. โอเคไหม?";
	next;
	if(select("~ โอเค...","~ ไม่...") == 2){
		mes "["+ .@npc_name$ +"]";
		mes "ท่านว่าอย่างนั้น... เอาเป็นว่า..";
		close;
	}
	if(countitem(.@material) < .@refinecnt || Zeny < .@fullprice) {
		mes "["+ .@npc_name$ +"]";
		mes "นั่นคือทั้งหมดที่ท่านมี? น่าเสียดายที่ข้าไม่สามารถทำงานให้ท่านในราคาที่ต่ำกว่าได้";
		close;
	}
	Zeny = Zeny - .@fullprice;
	delitem .@material,.@refinecnt;
	while(.@refinecnt){
		if (getequipisequiped(.@part) == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "ดูที่นี่... ท่านไม่มีอุปกรณ์ใด ๆ บน...";
			close;
		}
		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
				callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt) || (.@menu2 == 1 && getequippercentrefinery(.@part) < 100)) {
			mes "["+ .@npc_name$ +"]";
			mes "... เธอคิดเหรอว่าข้าจะโง่ขนาดนี้?!";
			mes "เจ้าสลับไอเท็ม";
			mes "ออกไปก่อนที่ข้าจะฟาดด้วยค้อนของข้า!!";
			close;
		} 
		mes "Clang, clang!!!";
		if(.@menu2 == 2 && getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			emotion ET_HUK;
			mes "["+ .@npc_name$ +"]";
			mes "วะฮะฮะ!!! ข้าขอโทษจริงๆ... ข้าเตือนท่านแล้วว่าสิ่งนี้อาจเกิดขึ้นได้...";
			.@refinecnt = .@refinecnt - 1;
			if(.@refinecnt == 0) close;
			mes "นี่คือ Zeny ที่ไม่ได้ใช้และวัสดุกลับคืน...";
			getitem .@material,.@refinecnt;
			.@fullprice = .@refinecnt * .@price;
			Zeny = Zeny + .@fullprice;
			close;
		}
		successrefitem .@part;
		emotion ET_BEST;
		.@refinecnt = .@refinecnt - 1;
		.@refinerycnt = getequiprefinerycnt(.@part);
		next;
	}
	mes "["+ .@npc_name$ +"]";
	mes "เสร็จเรียบร้อย...โปรดกลับมาเมื่อต้องการ";
	close;
}

// Material Salesmen
//============================================================
prt_in,56,68,5	script	Vurewell	86,{
	callfunc "phramain","Vurewell";
	end;
}
payon,145,178,3	script	Begnahd	88,{
	callfunc "phramain","Begnahd";
	end;
}
morocc_in,63,32,6	script	Sade	99,{
	callfunc "phramain","Sade";
	end;
}
alberta_in,13,71,3	script	Kahlamanlith	86,{
	callfunc "phramain","Kahlamanlith";
	end;
}
yuno_in01,171,27,4	script	Dilemma	88,{
	callfunc "phramain","Dilemma";
	end;
}
ein_in01,15,87,3	script	Tirehaus	86,{
	callfunc "phramain","Tirehaus";
	end;
}
lhz_in02,278,24,3	script	Krugg	86,{
	callfunc "phramain","Krugg";
	end;
}

// Material Salesmen Functions
//============================================================
function	script	phramain	{
	if (checkweight(1201,1) == 0) {
		mes "- โปรดรอสักครู่ !! -";
		mes "- ขณะนี้ท่านกำลัง -";
		mes "- แบกน้ำหนักมากเกินไป. -";
		mes "- โปรดลองใหม่อีกครั้ง -";
		mes "- หลังจากท่านจัดการกับน้ำหนักเรียบร้อยแล้ว. -";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "ข้าขายแร่อัพเกรดเพียงสองขนิด";
	mes "สำหรับการอัพเกรดอาวุธเท่านั้น.";
	mes "ข้ามี ^007777Phracon^000000 สำหรับ อาวุธ Level 1";
	mes "และ ^007777Emveretarcon^000000";
	mes "สำหรับ อาวุธ Level 2 Weapons.";
	next;
	switch(select("~ Phracon - 200z:~ Emveretarcon - 1,000z:~ ถามเกี่ยวกับแร่อัพเกรดอื่นๆ")) {
	case 1:
		.@material = 1010;
		.@price = 200;
		break;
	case 2:
		.@material = 1011;
		.@price = 1000;
		break;
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "แร่อื่นๆ ?";
		mes "ดี, ท่านจะต้องใช้แร่พิเศษบางอย่างเพื่อที่จะอัพเกรดอาวุธ หรือ ชุดเกราะ ที่มีระดับสูงขึ้นไป. แต่ท่านรู้ใหมว่า, Oridecon และ Elunium นั้นมัน";
		mes "ยากเกินไปที่จะหา...";
		close;
	}
	mes "["+ .@npc_name$ +"]";
	mes "ดังนั้น, ตอนนี้ท่านต้องการซื้อจำนวนเท่าไหร่ ?";
	mes "ถ้าหากท่านต้องการยกเลิกการแลกเปลี่ยน, กรุณาใส่หมายเลข 0";
	next;
	while(1) {
		input .@input;
		if (.@input == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "การแลกเปลี่ยน";
			mes "ถูกยกเลิก.";
			close;
		}
		else if (.@input < 0 || .@input > 500) {
			mes "["+ .@npc_name$ +"]";
			mes "เอาล่ะ, ท่านสามารถ";
			mes "ที่จะซื้อได้ไม่เกิน 500.";
			mes "รายการต่อครั้ง,";
			mes "ท่านเข้าใจใช่มั้ย ? ดี.";
			next;
		}
		else {
			break;
		}
	}
	.@sell = .@input * .@price;
	if (Zeny < .@sell) {
		mes "["+ .@npc_name$ +"]";
		mes "เอ่อ...";
		mes "ท่านไม่ได้";
		mes "มีจำนวนเงินที่เพียงพอต่อการซื้อ";
		mes ""+ .@input +" รายการ.";
		close;
	}
	if (checkweight(.@material,.@input) == 0) {
		mes "["+ .@npc_name$ +"]";
		mes "อืมม...";
		mes "เฮ้, ดูเหมือน. น้ำหนักในตัวท่านไม่เพียงพอต่อการรับของเหล่านี้เพิ่มเติมนะ.";
		close;
	}
	getitem .@material,.@input;
	Zeny = Zeny-.@sell;
	mes "["+ .@npc_name$ +"]";
	mes "นี่คือของท่าน !";
	mes "ขอบท่านสำหรับ";
	mes "การใช้บริการ.";
	close;
}

// Ori/Elu Refiners
//============================================================
prt_in,63,69,3	script	Dietrich	84,{
	callfunc "orimain","Dietrich";
	end;
}
payon,137,178,5	script	Hakhim	88,{
	callfunc "orimain","Hakhim";
	end;
}
morocc_in,72,32,6	script	Abdula	99,{
	callfunc "orimain","Abdula";
	end;
}
alberta_in,21,63,5	script	Xenophon	84,{
	callfunc "orimain","Xenophon Zolotas";
	end;
}
yuno_in01,164,27,4	script	Delight	88,{
	callfunc "orimain","Delight";
	end;
}
ein_in01,18,82,6	script	Matestein	84,{
	callfunc "orimain","Matestein";
	end;
}
lhz_in02,281,24,5	script	Fruel	84,{
	callfunc "orimain","Fruel";
	end;
}

// Ori/Elu Functions
//============================================================
function	script	orimain	{
	if (checkweight(1201,1) == 0) {
		mes "- โปรดรอสักครู่ !! -";
		mes "- ขณะนี้ท่านกำลัง -";
		mes "- แบกน้ำหนักมากเกินไป. -";
		mes "- โปรดลองใหม่อีกครั้ง -";
		mes "- หลังจากท่านจัดการกับน้ำหนักเรียบร้อยแล้ว. -";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "ข้าสามารถหลอมรวมแร่จาก";
	mes "Rough Oridecons 5ea หรือ";
	mes "Rough Eluniums 5ea";
	mes "สำหรับการหลอมรวมแร่";
	mes "เพื่อแลกกับ Eluniums หรือ Oridecons 1ea แร่จะถูกหลอมขึ้นใหม่";
	next;
	switch(select("~ หลอมรวมแร่ Oridecon:~ หลอมรวมแร่ Elunium:~ สอบถามเกี่ยวกับ Enchanted Stone")) {
	case 1:
		if (countitem(756) > 4) {
			delitem 756,5;  //Oridecon_Stone
			getitem 984,1; // Oridecon
			mes "["+ .@npc_name$ +"]";
			mes "นี่คือแร่บริสุทธิ์ Oridecon ของท่าน.";
			mes "ท่านสามารถมาที่นี่เมื่อไรก็ได้";
			mes "ถ้าหากท่านต้องการ.";
			close;
		}
		else {
			mes "["+ .@npc_name$ +"]";
			mes "ท่านเล่นตลกอยู่หรือยังไง ?";
			mes "ข้าบอกท่านแล้วไม่ใช่หรือว่า ข้าต้องการ Rough Oridecons 5ea เพื่อหลอมรวมแร่บริสุทธิ์ Oridecon 1ea";
			close;
		}
	case 2:
		if (countitem(757) > 4) {
			delitem 757,5;  //Elunium_Stone
			getitem 985,1; // Elunium
			mes "["+ .@npc_name$ +"]";
			mes "นี่คือแร่บริสุทธิ์ Elunium ของท่าน.";
			mes "ท่านสามารถมาที่นี่เมื่อไรก็ได้";
			mes "ถ้าหากท่านต้องการ.";
			close;
		}
		else {
			mes "["+ .@npc_name$ +"]";
			mes "ท่านเล่นตลกอยู่หรือยังไง ?";
			mes "ข้าบอกท่านแล้วไม่ใช่หรือว่า ข้าต้องการ Rough Eluniums 5ea เพื่อหลอมรวมแร่บริสุทธิ์ Elunium 1ea";
			close;
		}
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "Enchanted Stones...?";
		mes "จากที่ข้าเป็นช่างหลอมรวมแร่หินต่างๆมากว่า 20 ปี, ดังนั้นข้าอาจจะเคยได้ยินเรื่องพวกนี้บ้าง,";
		mes "หินเหล่านี้มีชนิดที่แต่ต่างกัน ที่รู้มาคือว่ามีทั้งหมด 4 ชนิด.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "หิน Enchanted Stone ในแต่ละก้อนนั้นจะมีท่านสมบัติที่แตกต่างกันดังต่อไปนี้: ดิน, ลม, น้ำ และ ไฟ.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "ถ้าหากมีใครได้หลอมรวม Enchanted Stone เข้ากับอาวุธ, อาวุธเหล่านั้นจะถูกแปรเปลี่ยนเป็นธาตุนั้นๆตามหินที่ถูกหลอมรวมลงไปด้วย.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "ถ้าพูดถึงนั้น, ท่านจำเป็นที่จะต้องมีทักษะสกิลที่สามารถหลอมรวมธาตุต่างๆเข้ากับอาวุธเหล่านี้ด้วย.";
		close;
	}
}

// Equipment Repairmen
//============================================================
alberta_in,31,65,4	script	Repairman#alb	86,{
	callfunc "repairmain","Repairman";
	end;
}

moc_ruins,107,94,4	script	Repairman#moc	99,{
	callfunc "repairmain","Repairman";
	end;
}

payon,143,165,4	script	Repairman#pay	88,{
	callfunc "repairmain","Repairman";
	end;
}

prt_in,63,54,2	script	Repairman#prt	86,{
	callfunc "repairmain","Grendal";
	end;
}

yuno_in01,175,28,3	script	Repairman#juno	86,{
	callfunc "repairmain","Repairman";
	end;
}

geffen_in,34,166,3	script	Repairman#gef	99,{
	callfunc "repairmain","Repairman";
	end;
}

aldeba_in,38,60,3	script	Repairman#alde	86,{
	callfunc "repairmain","Repairman";
	end;
}

lhz_in02,284,14,3	script	Repairman#lhz	86,{
	callfunc "repairmain","Repairman";
	end;
}

prt_gld,139,117,4	script	Repairman#prt_gld	86,{
	callfunc "repairmain","Repairman";
	end;
}

gef_fild13,263,117,4	script	Repairman#gef_fild	86,{
	callfunc "repairmain","Repairman";
	end;
}

pay_gld,295,183,4	script	Repairman#pay_gld	86,{
	callfunc "repairmain","Repairman";
	end;
}

alde_gld,220,152,4	script	Repairman#alde_gld	86,{
	callfunc "repairmain","Repairman";
	end;
}

aru_gld,189,336,4	script	Repairman#aru_gld	86,{
	callfunc "repairmain","Repairman";
	end;
}

sch_gld,340,80,7	script	Repairman#sch_gld	86,{
	callfunc "repairmain","Repairman";
	end;
}

// Equipment Repair Function
//============================================================
function	script	repairmain	{
	.@repairprice = 5000;
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "สวัสดี !";
	mes "ท่านต้องการให้ข้า";
	mes "ซ่อมแซมอุปกรณ์ที่ถูกทำลาย ?";
	next;
	switch(select("~ ข้ามีอุปกรณ์บางอย่างที่ต้องการให้ซ่อม:~ ไม่มี ยกเลิกการสนทนา")) {
	case 1:
		.@checkitem = 1;
		while (1) {
			if (getbrokenid(.@checkitem) == 0) {
				break;
			}
			.@checkitem = .@checkitem+1;
		}
		.@checkitem = .@checkitem-1;
		if (!.@checkitem) {
			mes "["+ .@npc_name$ +"]";
			mes "โอ้ ว้าว, ดูนี่สิ !";
			mes "ท่านไม่ได้มีอุปกรณ์ใดๆเสียหายเลย. ท่านคงดูแลอุปกรณ์เหล่านี้ดีมากๆ !";
			next;
			mes "["+ .@npc_name$ +"]";
			mes "ถ้าทุกคนเป็นแบบท่าน, ข้าคงจะตกงานแน่ๆ !! ฮ่าฮ่า~!";
			close;
		}
		mes "["+ .@npc_name$ +"]";
		mes "อืม...";
		mes "มาดูกัน...";
		mes "นี่คือรายการไอเท็มของท่านทั้งหมด,";
		mes "" + .@checkitem + " รายการ ที่เสียหาย.";
		mes "ท่านต้องการที่จะให้ข้าซ่อมแซมมันใช่มั้ย ?";
		next;
		.@totalcost = .@repairprice*.@checkitem;
		mes "["+ .@npc_name$ +"]";
		mes "ค่าธรรมเนียมสำหรับการซ่อม " + .@repairprice + " Zeny. ดังนั้นการซ่อมแซมอุปกรณ์ที่เสียหายทั้งหมดของท่านคือ " + .@totalcost + " Zeny! ท่านต้องการที่จะให้ข้าซ่อมแซมมันใช่มั้ย ?";
		next;
		switch(select("~ ใช่:~ ไม่")) {
		case 1:
			if (Zeny < .@totalcost) {
				mes "["+ .@npc_name$ +"]";
				mes "โว้ว โว้ว...";
				mes "ท่านควรเช็คเงินให้เรียบร้อยก่อนที่จะซ่อมแซมอุปกรณ์ ! ข้าไม่สามารถที่ซ่อมแซมอุปกรณ์ใดๆได้หากท่านไม่ได้มีจำนวนเงินที่เพียงพอ.";
				close;
			}
			.@checkitem2 = 1;
			while (1) {
				if (getbrokenid(.@checkitem2) == 0) {
					break;
				}
				.@checkitem2 = .@checkitem2+1;
			}
			.@checkitem2 = .@checkitem2-1;
			if (.@checkitem == .@checkitem2) {
				Zeny = Zeny-.@totalcost;
				while (.@checkitem) {
					repair(.@checkitem);
					.@checkitem = .@checkitem-1;
				}
				mes "["+ .@npc_name$ +"]";
				mes "โอเค ! การซ่อมเสร็จสมบูรณ์. ตอนนี้, ท่านควรระมัดระวังการใช้งานให้มากขึ้น.";
				close;
			}
			else {
				mes "["+ .@npc_name$ +"]";
				mes "อืมม ? มีบางอย่างผิดปกติ. รอสักครู่... ทำไมท่านไม่จัดการอุปกรณ์ของท่านที่ต้องการซ่อมแซมแล้วกลับมาหาข้าใหม่อีกครั้งล่ะ.";
				close;
			}
		case 2:
			mes "["+ .@npc_name$ +"]";
			mes "ดี, แต่... ท่านไม่ควรเก็บอุปกรณ์ที่เสียหายไว้กับตัว. ท่านควรจะซ่อมแซมอุปกณ์หากมันเสียหายให้เร็วที่สุด !";
			close;
		}
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "โฮ่โฮ่โฮ่...";
		mes "ท่านไม่ได้มี";
		mes "อะไรเกี่ยวข้องทางธุรกิจกับข้าหลอก";
		mes "ถ้าหากท่านไม่ได้มี";
		mes "อุปกรณ์ใดๆที่เสียหาย.";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 by A bunch of people!
//=     Syrus22 - Completely redid the script using functions... also
//=     added the option for auto safe refining and multiple refining.
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Added additional reparimen in Morocc and payon. Added
//=     Christopher the blacksmith in Geffen. Edited some dialogue [kobra_k88]
//= 1.3 New Payon Locations [Darkchild]
//=     Corrected zeny subtraction thx to jpnmania77.[kobra_k88]
//= 1.3a Temporary corrected an exploit. Need to check sources
//=     to fully fix bug [Shinigami]
//=     Fixed repairman prices [shadowlady]
//=     Fixed bug that skips requirements thanks to sir_loon [massdriller]
//=     Fixed itemid error thanks to -Vitamin- [massdriller]
//= 1.4 check again item in refining procedure to avoid
//=     hacker that can change item [dafide18]
//= 1.5 Fixed crashing due to badly used callfunc's [Skotlex]
//=     Lupus, don't rollback this important fix again! >.<
//= 1.5a Corrected an unneeded callfunc, fixed the anti-bot 
//=     exploit ruining the safe refine loop. [Skotlex]
//= 1.5b Fixed Spelling mistakes. [Nexon] 
//= 1.6 Replaced all breaks for ends as per the new script engine [Skotlex]
//= 1.7 Added Einbroch Refiners (Custom names ^^;) and a duplicated BS Shop. [Poki#3]
//= 1.8 Added Lighthalzen Refiners (Custom names again ^^;) [Poki#3]
//= 1.8a Fixed wrong indication thanks to NeoSaro [Lupus]
//= 1.9 Rewrote repairman, removed the Steel from repair cost [DracoRPG]
//= 2.0 Fixed missed equppment presence check. Thx2 Coltaro [Lupus]
//= 2.0a Added weight checks thanks to Neouni [Playtester]
//= 2.0b Fixed the names of Lighthalzen and Einbroch refiners thanks to Maud_Dib [Kargha]
//= 2.1 Removed Duplicates [Silent]
//= 2.2 Changed name from "Emvertacon" to "Emveretarcon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.2b Changed name from "Pharacon" to "Phracon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.3 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.4 Updated Refiner function. cleaner, and less dated. [L0ne_w0lf]
//= 2.5 Rather large update to the refiner and merchants. :D [L0ne_W0lf]
//= 2.6 Fixed a few bugs with creating pure stones. [L0ne_W0lf]
//= 2.7 Refiner function accepts additional paramater. [L0ne_W0lf]
//=     0 = No special features; 1 = new refining features
//=     Updated Repairmen and function. No longer shows menu.
//= 2.7a A couple touch-ups to the repairman function. [L0ne_w0lf]
//= 2.8 Changed the nonexistent variable .@matname$ for getitemname(.@material). (bugreport:2340) [Samuray22]
//= 2.8 Added proper Blacksmith Supplier to Einroch. [L0ne_W0lf]
//=     Updated dated features comment to reflect new usage.
//= 2.8a Small bugfix. (bugreport:2418) [Paradox924X]
//= 2.9 Moved Morocc repairman to Morocc Ruins. [L0ne_W0lf]
//============================================================
