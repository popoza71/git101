//===== rAthena Script ======================================= 
//= Bunny Band Quest
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Quest to obtain a Bunny Band headgear.
//===== Additional Comments: ================================= 
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//	Changed @BUNYBAN back to BUNYBND, it should be this way.
//	Removed duplicates. NPC is ALberta-only.
//= 1.6 Cleaning. [Euphy]
//============================================================ 

alberta,26,229,0	script	Kafra Employee#bunny	83,{
	if (BUNYBND == 1) {
		mes "[พนักงานของ Kafra]";
		mes "สวัสดีนะ~!";
		mes "คุณอยากจะ";
		mes "ร่วมกิจกรรมพิเศษ";
		mes "^529DFFBunny Band Event^000000";
		mes "ของบริษัท Kafra";
		next;
		 switch(select("แน่, ฉันนำไอเท็มมาแล้ว.:ข้อมูลกิจกรรม:ยกเลิก")) {
		case 1:
			mes "[พนักงานของ Kafra]";
			mes "โอเคนะ~";
			mes "ฉันจะตรวจสอบ";
			mes "ว่าคุณนำไอเท็มมา";
			mes "ครบทุกชิ้นหรือยัง...";
			next;
			setarray .@Items[0],949,100,706,1,722,1,2213,1;
			for(set .@i,0; .@i<8; set .@i,.@i+2)
				if (countitem(.@Items[.@i]) < .@Items[.@i+1]) {
					mes "[พนักงานของ Kafra]";
					mes "อ๋อ, ขอโทษนะ";
					mes "แต่คุณต้องนำมา";
					mes "อย่างน้อย";
					mes callfunc("F_InsertPlural",.@Items[.@i+1],getitemname(.@Items[.@i]))+".";
					close;
				}
			mes "[พนักงานของ Kafra]";
			mes "ดีมาก, ฉันเห็น";
			mes "ว่าคุณได้รวบรวม";
			mes "ทุกอย่างที่ฉันต้องการ";
			mes "เพื่อทำ Bunny Band แล้ว";
			mes "กรุณารอสักครู่";
			mes "ขณะที่ฉันทำมันให้คุณ...";
			next;
			for(set .@i,0; .@i<8; set .@i,.@i+2)
				if (countitem(.@Items[.@i]) < .@Items[.@i+1]) {
					mes "[พนักงานของ Kafra]";
						mes "เอ้ม? ขอโทษนะ,";
						mes "แต่ฉันไม่สามารถ";
						mes "ทำได้ในขณะนี้. คุณ";
						mes "ต้องมี "+callfunc("F_InsertPlural",.@Items[.@i+1],getitemname(.@Items[.@i]));
						mes "เพื่อให้ฉันสามารถทำ Bunny Band";
					close;
				}
			delitem 949,100; //Feather
			delitem 706,1; //Four_Leaf_Clover
			delitem 722,1; //Scarlet_Jewel
			delitem 2213,1; //Cat_Hairband
			mes "[พนักงานของ Kafra]";
			mes "อ๊าก, นี่คือ~";
			mes "Bunny Band ที่สวยงาม!";
			mes "หวังว่าคุณจะชอบ";
			getitem 2214,1; //Bunny_Band
			set BUNYBND,0;
			next;
			mes "[พนักงานของ Kafra]";
			mes "ขอบคุณที่";
			mes "ร่วมกิจกรรมพิเศษ";
			mes "นี้และคุณคู่ควร";
			mes "ในการให้บริการ Kafra~";
			close;
		case 2:
			goto L_Info;
		case 3:
			close;
		}
	}
	mes "[พนักงานของ Kafra]";
	mes "สวัสดีนะ~! คุณต้องการ";
	mes "ร่วมกิจกรรมพิเศษ ^529DFFBunny";
	mes "Band Event^000000 ที่ถูกจัดขึ้นโดยบริษัท";
	mes "Kafra และได้รับการสนับสนุน";
	mes "จาก Alberta Merchant Guild ด้วย?";
	next;
	switch(select("เข้าร่วมกิจกรรม:ข้อมูลกิจกรรม:ยกเลิก")) {
	case 1:
		set BUNYBND,1;
		mes "[พนักงานของ Kafra]";
		mes "ดีมาก! ขอบคุณที่";
		mes "ร่วมกิจกรรม! หากคุณ";
		mes "ยังไม่ทราบ, คุณต้อง";
		mes "เก็บไอเท็มเหล่านี้";
		mes "ถ้าคุณต้องการให้ฉัน";
		mes "ทำ Bunny Band ให้คุณ...";
		next;
		mes "[พนักงานของ Kafra]";
		mes "100 Feathers,";
		mes "1 Four-Leaf Clover,";
		mes "1 Kitty Band and";
		mes "1 Pearl.";
		mes "โชคดีและฉันจะ";
		mes "รอคุณอยู่ที่นี่~";
		close;
	case 2:
		break;
	case 3:
		close;
	}
L_Info:
	mes "[พนักงานของ Kafra]";
	mes "เพื่อขอบคุณลูกค้า";
	mes "ที่มีค่าของเรา,";
	mes "Kafra Corporation ได้เตรียม";
	mes "กิจกรรมพิเศษที่พนักงาน";
	mes "Kafra จะทำการประกอบ";
	mes "Bunny Bands ให้ผู้ผจญภัย";
	mes "ที่นำเอาไอเท็มที่กำหนดมา.";
	next;
	mes "[พนักงานของ Kafra]";
	mes "สำหรับไอเท็มพิเศษ";
	mes "100 Feathers,";
	mes "1 Four-Leaf Clover,";
	mes "1 Kitty Band and";
	mes "1 Pearl.";
	next;
	mes "[พนักงานของ Kafra]";
	mes "เมื่อคุณพร้อม,";
	mes "กลับมาและนำ";
	mes "ไอเท็มเหล่านั้นมาให้ฉัน.";
	mes "(สนับสนุนโดย";
	mes "Alberta Merchant Guild)";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.1 using duplicate command
//= 1.2 Fixed NPC location, removed NPC dupes [Lupus]
//= 1.3 RE-Fixed exploit V_V, also cleared used VAR [Lupus]
//= 1.4 replaced BUNYBND with @BUNYBND [Lupus]
//============================================================ 
