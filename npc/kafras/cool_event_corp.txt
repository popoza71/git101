//===== rAthena Script ======================================= 
//= Cool Event Corp. Staff
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Cool Event Corp. Staff (Save, Storage & Pushcart Service)
//= in Arunafeltz Region.
//===== Additional Comments: ================================= 
//= 1.0 First version, iRO Sakray X.4 [Gepard]
//= 1.1 Optimized, fixed a conditional bug [Lupus]
//= 1.1a Redo back. It wasn't a bug [Lupus]
//= 1.2 Added Rachel Cool Event Corp. Staff [Playtester]
//= 1.3 Added cutins, closed Kafra Password exploit
//=     until we got a client/packet based password support [Lupus]
//= 1.4 Added Veins Cool Event Corp. Staff. [L0ne_W0lf]
//= 1.5 Cool Event Corp staff is now more iRO like. [L0ne_W0lf]
//= 1.6 Wrote a basic function and added VIP features. [Euphy]
//============================================================ 

//callfunc "F_CoolEventCorp","<menu>","<location>","<save map>",<x>,<y>,"<warp map>";
function	script	F_CoolEventCorp	{
	cutin "zonda_01",2;
	mes "[พนักงาน Cool Event Corp.]";
	mes "ยินดีต้อนรับสู่ Cool Event Corp.";
	mes "พนักงานของเรากำลังทำงานอย่างหนัก";
	mes "เพื่อเติมเต็มความคาดหวังของคุณ";
	mes "สำหรับบริการที่มีคุณภาพ ดังนั้น";
	mes "วันนี้ฉันสามารถช่วยอะไรคุณได้?";
	next;
	switch(select(getarg(0))) {
		case 1:
			mes "[พนักงาน Cool Event Corp.]";
			mes "จุด Respawn ของคุณ";
			mes "ได้รับการบันทึกที่นี่";
			mes getarg(1)+".";
			mes "ขอบคุณที่ใช้บริการของ";
			mes "Cool Event Corp.~";
			savepoint getarg(2),getarg(3),getarg(4),1,1;
			close2;
			break;
		case 2:
			mes "[พนักงาน Cool Event Corp.]";
			if(!callfunc("F_CanOpenStorage")){
				mes "ขออภัย แต่คุณ";
				mes "ต้องมีทักษะพื้นฐานของ Novice";
				mes "เลเวล 6 เพื่อใช้บริการ";
				mes "Storage นี้";
				close2;
				break;
			}
			if(Zeny < 40){
				mes "ขออภัย แต่คุณไม่มี";
				mes "เซนนีเพียงพอในการใช้";
				mes "บริการ Storage ของเรา";
				mes "ค่าบริการ Storage คือ 40 เซนี";
				close2;
				break;
			}
			set Zeny, Zeny - 40;
			mes "ปล่อยให้ฉันเปิด Storage";
			mes "ส่วนตัวของคุณทันที";
			mes "ขอบคุณที่สนับสนุน Cool";
			mes "Event Corp. ด้วยการใช้บริการ";
			mes "ของเรา ขอให้คุณมีวันที่ดี~";
			callfunc("F_CheckKafCode");    //check your storage password, if set
			close2;
			openstorage;
			break;
		case 3:
			mes "โปรดเลือก";
			mes "จุดหมายปลายทางของคุณ";
			next;
			if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE))
				set .@cost,4400;
			else
				set .@cost,2200;
			.@destination$ = getarg(5);
			if (select(.@destination$ +" -> "+ .@cost +" z:ยกเลิก") == 1) {
				if ((countitem(7060) != 0) || (Zeny >= .@cost)) {
					if (countitem(7060) != 0) {
						delitem 7060,1; //Warp_Free_Ticket
					}
					else {
						set Zeny,Zeny-.@cost;
					}
					if (.@destination$ == "Veins") warp "veins",205,101;
					else if (.@destination$ == "Rachel") warp "rachel",115,125;
					cutin "",255;
					end;
				}
				else {
					mes "[พนักงาน Cool Event Corp.]";
					mes "ขออภัย แต่คุณไม่มี";
					mes "เซนนีพอในการใช้บริการ";
					mes "Teleport ค่าบริการเทเลพอร์ต";
					mes "ไปที่ "+ .@destination$ +" คือ "+.@cost+" เซนี";
				}
			}
			close2;
			break;
		case 4:
			mes "[พนักงาน Cool Event Corp.]";
			if(BaseClass != Job_Merchant){
				mes "ขออภัย แต่";
				mes "บริการเช่า Pushcart";
				if (checkre(0))
					mes "สำหรับอาชีพ Merchant เท่านั้น";
				else {
					mes "สำหรับอาชีพ Merchant,";
					mes "Blacksmiths, White Smiths,";
					mes "Alchemists และ Creators เท่านั้น";
				}
				close2;
				break;
			}
			if(getskilllv("MC_PUSHCART") == 0){
				mes "คุณสามารถเช่ารถเข็นได้";
				mes "หลังจากเรียนทักษะ Pushcart";
				close2;
				break;
			}
			if(checkcart() == 1){
				mes "คุณมี";
				mes "Pushcart ติดตั้งอยู่แล้ว";
				mes "นอกจากนี้, เราไม่สามารถ";
				mes "ให้เช่ามากกว่าหนึ่งคัน";
				mes "ให้แก่ลูกค้าแต่ละคนในตัวเดียว";
				close2;
				break;
			}
			if(countitem(7061)){
				delitem 7061,1; //Cart_Free_Ticket
				setcart;
				close2;
				break;
			}
			mes "ค่าบริการเช่า Pushcart";
			mes "คือ 800 เซนี คุณต้องการ";
			mes "เช่า Pushcart หรือไม่?";
			next;
			if(select("เช่า Pushcart:ยกเลิก") == 1){
				if(Zeny < 800){
					mes "[พนักงาน Cool Event Corp.]";
					mes "ขออภัย แต่คุณ";
					mes "ไม่มีเซนนีเพียงพอ";
					mes "ในการจ่ายค่าบริการ";
					mes "เช่า Pushcart 800 เซนี";
				} else {
					set Zeny, Zeny - 800;
					setcart;
				}
			}
			close2;
			break;
		case 5:
			callfunc("F_SetKafCode","[พนักงาน Cool Event Corp.]","Cool Event Corp.");
			//the function closes msg and removes cutins
		case 6:
		default:
			mes "[พนักงาน Cool Event Corp.]";
			mes "Cool Event Corp. กำลัง";
			mes "พยายามที่จะให้บริการที่ดีที่สุด";
			mes "สำหรับลูกค้าของเรา";
			mes "ช่วยเราให้เป็นที่ดีที่สุด";
			mes "โดยการให้ความคิดเห็น";
			mes "และคำติชมของคุณ ขอบคุณครับ";
			close2;
	}
	cutin "", 255;
	end;
}

lighthalzen,94,248,3	script	Cool Event Corp Staff::CoolEventCorpStaffLighthalzen	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Save:Use Storage::Rent a Pushcart:Storage Password Service:Cancel",
		"in the town of Lighthalzen","lighthalzen",95,240;
	end;
}

hugel,88,168,4	script	Cool Event Corp. Staff::CoolEventCorpStaffHugel	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Save:Use Storage::Rent a Pushcart:Storage Password Service:Cancel",
		"in the village of Hugel","hugel",96,155;
	end;
}

rachel,109,138,5	script	Cool Event Corp. Staff::CoolEventCorpStaffRachel	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Save:Use Storage:Teleport Service:Rent a Pushcart:Storage Password Service:Cancel",
		"in the town of Rachel","rachel",113,137,"Veins";
	end;
}

veins,208,128,5	script	Cool Event Corp. Staff::CoolEventCorpStaffVeins	4_M_ZONDAMAN,{
	callfunc "F_CoolEventCorp",
		"Save:Use Storage:Teleport Service:Rent a Pushcart:Storage Password Service:Cancel",
		"in the town of Veins","veins",204,103,"Rachel";
	end;
}
