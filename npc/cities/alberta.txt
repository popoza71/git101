//===== rAthena Script =======================================
//= Alberta Town
//===== By: ==================================================
//= DZeroX
//===== Current Version: =====================================
//= 1.6
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Town-specific Alberta NPCs
//===== Additional Comments: =================================
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Bug fixes and syntax. [Capuche]
//============================================================

alberta,97,51,0	script	Fabian	84,{
	mes "[Fabian]";
	mes "คน... เมื่อคุณท่องทั่วโลก, คุณจะได้ยินเรื่องบ้าบอท่ามกลางไป...";
	next;
	mes "[Fabian]";
	mes "มีครั้งที่ฉันได้ยินว่ามีการ์ดที่มีพลังของมอนสเตอร์อยู่. ถ้ามีคนได้รับการ์ดนั้นมา, พวกเขาจะสามารถใช้พลังของมอนสเตอร์นั้นได้.";
	next;
	mes "[Fabian]";
	mes "ฉันคิดว่ามันน่าจะเป็นรูปแบบหนึ่งของการล่วงลับหรือโกหก, ที่พวกเขาทำให้คุณเก็บการ์ดทั้งหมดหรืออะไรทำนองนั้น. ฉันหมายถึง, การ์ดจริงๆสามารถมีพลังของมอนสเตอร์ได้ไหม?!";
	next;
	mes "[Fabian]";
	mes "จริงจังนะ...";
	close;
}

alberta,53,39,0	script	Steiner	100,{
	mes "[Steiner]";
	mes "โอ้...!";
	mes "ยินดีต้อนรับสู่อัลเบอร์ต้า,";
	mes "นักผจญภัยสาว!";
	next;
	mes "[Steiner]";
	mes "ขอโทษที่ดูเหมือนทำให้เหมือนจะมีความไม่มีความสนใจ. ฉันกำลังวางแผน, ฉันได้ยินว่ามีร้านที่Geffenที่ขายเกราะที่ต้านทานเวทมนตร์.";
	next;
	mes "[Steiner]";
	mes "ถ้าฉันซื้อมันเป็นจำนวนมาก, แล้วขายต่อที่นี่ในราคาที่สูงขึ้น...";
	close;
}

alberta_in,20,183,0	script	Chad	1_M_03,{
	mes "[Chad]";
	mes "มีคนบอกว่า Gungnir, อาวุธตำนาน, ไม่เคยพลาดเป้าหมายเลย. ฉันสงสัยว่ามันเป็นไปได้จริงหรือไม่...";
	next;
	mes "[Chad]";
	mes "คนก็บอกว่าทารกถูกประกอบตัวโดยนกกระสือก่อนการส่งมอบ, สาวๆชอบผู้ชายที่ทำตัวเป็นคนขี้แกล้ง, และซานตาคลอสจริง! แต่เฉพาะที่ Lutie.";
	next;
	mes "[Chad]";
	mes "ฉันสงสัย...";
	mes "ว่ามี";
	mes "ได้จริงหรือไม่...";
	close;
}

alberta,131,139,2	script	Drunken Old Man	1_M_JOBGUIDER,{
	mes "[Deagle]";
	mes "^666666*สะดุ้ง*^000000";
	mes "นายมองไรอยู่? ไปหายไป!!";
	next;
	switch(select("ไม่พูดอะไร.","ปล่อยเขาไป.")) {
	case 1:
		mes "[Deagle]";
		mes "5555555 ^666666*สะดุ้ง~*^000000... เธอกล้าทำแบบนี้. หนูอาจดูน่าสลดตอนนี้ แต่ก่อนหนูเคยเป็นกะลนใน 'Going Mary.'";
		next;
		switch(select("ไม่เคยได้ยินเลย.","จริงหรือ? ไม่มีเล่ห์เหรือ?")) {
		case 1:
			mes "[Deagle]";
			mes "ไม่เคยได้ยินเลย?! ทุกคนรู้จัก 'Going Mary!' สำนักโจรสลัดชื่อดังนี้! ^666666*สะดุ้ง~*^000000";
			next;
			mes "[Deagle]";
			mes "อายุนี้... ของดีได้แค่นี้ ถ้าเราไม่เจอนักสู้...^666666*สะดุ้ง~*^000000";
			next;
			mes "[Deagle]";
			mes "อะ~ วันเก่าเก่า. ถ้ายังมี... ถ้ายังไม่ต้องพบกับพายุนั้น... ^666666*สะดุ้ง~*^000000";
			next;
			mes "[Deagle]";
			mes "อะ~ กัปตัน. คิดถึงกัปตันมากกว่าทุกอย่าง... ไม่มีใครที่รอดเมื่อกัปตันเสร็จส่งดาบของเขา.";
			close;
		case 2:
			mes "[Deagle]";
			mes "ถูกต้อง! ไม่มีใครแทรกแซงกับลูกพวกของ 'Going Mary!' และไม่มีใครที่สามารถทำให้กัปตันแพ้ในการสู้ด้วยดาบ!";
			next;
			mes "[Deagle]";
			mes "กัปตัน~!!! ^666666*สะดุ้ง~*^000000 เขาจะวิ่งดาบของเขาแบบนี้, แล้ว... แล้ว!!";
			next;
			mes "[Deagle]";
			mes "คนไร้เสียงที่กัปตันกำลังต่อสู้กับ, และใครก็ตามที่อยู่ใกล้เคียงถูกล้อมไปด้วยเปลวไฟ!";
			next;
			mes "[Deagle]";
			mes "พวกเขาเคยบอกว่าดาบนั้นต้องมีพลังลึกลับ, หรือกัปตันเคยเก่งขนาดนั้น...!";
			next;
			mes "[Deagle]";
			mes "ฮือ~~ ^666666*ซ็อบ* *ซ็อบ...*^000000 พระเจ้า, คิดถึงทุกคน! ตอนนี้ฉันก็เศร้า! กรุณาไปเถอะ.";
			close;
		}
	case 2:
		mes "[Deagle]";
		mes "ถูกต้อง!";
		mes "ไปหายไป~";
		close;
	}
}


alberta,58,80,2	script	Shakir	99,{
	mes "[Shakir]";
	if (rand(2)) {
		mes "เรานักการค้ามีทักษะการต่อรองเฉพาะของเราเมื่อขายสินค้า. ทักษะนี้สามารถทำให้เราได้รับเงินมากกว่าเมื่อคนอื่นขายสินค้า.";
		next;
		mes "[Shakir]";
		mes "มันไม่ได้แค่การตะโกน 'คุณต้องจ่ายเงินเพิ่มสิ!' คุณต้องมีเสน่ห์และทักษะการโต้เถียง!";
		next;
		mes "[Shakir]";
		mes "เราสามารถได้รับเงินเพิ่มขึ้นได้ถึง 24% ด้วยทักษะที่น่าทึ่งนี้. แต่จำไว้ที่จะฝึกฝนเพื่อให้ได้มัน!!";
	} else {
		mes "เรานักการค้าสามารถ";
		mes "เปิดร้านค้าข้างทาง";
		mes "ทำธุรกิจ.";
		next;
		mes "[Shakir]";
		mes "ด้วยทักษะส่วนลด, เราสามารถซื้อสินค้าในราคาถูกจากร้านในเมืองและนำมาจัดในรถเข็นที่เราเช่า";
		next;
		mes "[Shakir]";
		mes "แล้วต่อมา, เราสามารถเดินทางไปทุกที่และขายสินค้าของเราเพื่อทำกำไร!";
		next;
		mes "[Shakir]";
		mes "ด้วยวิธีนี้, ธุรกิจจะสะดวกและปลอดภัยมาก. อย่าหลับตานะ, ถึงแม้จะง่ายมาก";
	}
	close;
}

alberta,62,156,2	script	Sonya	102,{
	mes "[Sonya]";
	switch(rand(3)){
	case 0:
		mes "ฟังนะ, ครั้งหนึ่งผมกำลังเดินทางผ่านป่าแล้วเห็นลูกกล้วยสีเขียวเล็ก ๆ กำลังเคลื่อนไหว.";
		next;
		mes "[Sonya]";
		mes "ผมไปดูว่ามันคืออะไรแล้วเมื่อผมไปจับมัน. ก็พอดีมันต่อยมือผม!";
		next;
		mes "[Sonya]";
		mes "มันทำให้ผมตกใจ, ผมกระโดดหลบบานแล้วผมตระหนี่ว่ามันไม่ใช่ลูกกล้วยแต่เป็นสัตว์เล็ก ๆ";
		next;
		mes "[Sonya]";
		mes "โชคดีที่ผมไม่ได้ทำให้มันโกรธ. แม้แต่สัตว์ขนาดเล็กก็อาจเป็นอันตรายถ้าโกรธ";
		close;
	case 1:
		mes "คุณรู้มั้ย? หมีที่ดูเหมือนขี้เกียจในป่าทางไป Payon?";
		next;
		mes "[Sonya]";
		mes "เพื่อความสนุก, ผมทิ้งหินที่มันแล้วมันก็อีกทีกับผม! ผมกลัวมากก็วิ่งหลบ. แล้ว BAM!!!";
		next;
		mes "[Sonya]";
		mes "มันวิ่งเข้ามาในกิ่งไม้ต่ำแล้วทำให้มันเป็นลม! ผมสาบานว่า, ผมจะไม่ท้าทายสัตว์เพื่อความสนุกอีก!";
		close;
	case 2:
		mes "ครั้งหนึ่งผมเห็นมีฝูงหมาป่าทำการต่อสู้กับหนึ่งตัวหมีที่ใหญ่มากและขี้เกียจ!";
		next;
		mes "[Sonya]";
		mes "หมาป่ามีความร่วมมือมากกว่าที่คิด. ถ้าหนึ่งในพวกเขาถูกโจมตี, หมาป่าที่อยู่ใกล้จะวิ่งมาร่วมช่วย.";
		next;
		mes "[Sonya]";
		mes "ผมคิดว่าคุณควรประมาณใจอีกครั้งถ้าคุณต้องการต่อสู้กับหนึ่งตัวเมื่อมีอื่น ๆ ในประการ. ระวัง: อย่าให้มีการโจมตีจากหลายทิศทาง!";
	close;
	}
}

alberta,93,174,2	script	Grandmother Alma	103,{
	mes "[ย่าอัลมาแม่]";
	mes "ไม่นานมานี้,";
	mes "เรือที่ถูกทอดทิ้ง";
	mes "drifted into";
	mes "ท่าเรืออัลเบอร์ต้า.";
	next;
	mes "[ย่าอัลมาแม่]";
	mes "หวังว่าจะช่วยชีวิตผู้รอดชีวิตบางคน, บางคนในเมืองพยายามเข้าไปในเรือ. แต่พวกเขาทุกคนวิ่งออกมาหวาดเสียงกล่าวว่าศพกำลังเดินรอบไปภายในเรือ.";
	next;
	mes "[ย่าอัลมาแม่]";
	mes "เรือยังเต็มไปด้วยสิ่งมีชีวิตในทะเลที่อันตราย, และพวกเขาไม่สามารถเข้าไปในเรือได้, แม้แต่ถ้าพวกเขาต้องการ.";
	next;
	mes "[ย่าอัลมาแม่]";
	mes "เราไม่สามารถทำอะไรกับเรือที่ดูน่ากลัวนั้น, และปล่อยไว้ตามที่เคยเป็น. ปัจจุบัน, ทีมสำรวจพยายามเข้าไปในเรือนั้นและกำจัดมอนสเตอร์ภายใน.";
	next;
	mes "[ย่าอัลมาแม่]";
	mes "ดังนั้น, มันอาจเป็นประสบการณ์ที่ดีสำหรับคนที่ยังหน้าตาเด็กเหล่านี้ที่จะเป็นทหารใหม่. แต่ก็ยังไม่คุ้มค่าที่จะเสี่ยงชีวิตของคุณถ้าคุณไม่มีพลังเพียงพอ.";
	close;
}

alberta,189,151,5	script	Fisk	4W_SAILOR,{
	mes "[Fisk]";
	mes "อ่าวเฮ้,";
	mes "คุณจะไปที่ไหน?";
	next;
	switch(select("ไปเรือล่ม -> 250 zeny.","Izlude -> 500 zeny.","ไม่เป็นไร.")) {
	case 1:
		if (Zeny < 250) {
			mes "[Fisk]";
			mes "ไอ้นี่เถอะ, อย่าพยายามหลอกฉัน! ฉันว่า 250 zeny!";
			close;
		}
		Zeny = Zeny - 250;
		warp "alb2trea",43,53;
		end;
	case 2:
		if (Zeny < 500) {
			mes "[Fisk]";
			mes "ไม่มีทางคุณจะได้ไปที่นั่นโดยไม่มี 500 zeny ล่วงหน้า!";
			close;
		}
		Zeny = Zeny - 500;
		if (checkre(0))
			warp "izlude",195,212;
		else
			warp "izlude",176,182;
		end;
	case 3:
		mes "[Fisk]";
		mes "โอเค...";
		mes "เจอกัน.";
		close;
	}
}

alb2trea,39,50,6	script	Fisk#a2t	4W_SAILOR,{
	mes "[Fisk]";
	mes "คุณต้องการกลับไปที่ Alberta เหรอ?";
	next;
	if (select("กลับเลย.","ฉันเปลี่ยนใจ.") == 1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	Paul	4_M_04,{
	mes "[Paul]";
	mes "สวัสดี~คุณต้องการเข้าร่วมทีมสำรวจเรือที่ล่มไหม";
	next;
	mes "[Paul]";
	mes "โอ้! ก่อนที่คุณจะเข้าร่วม, ขอเตือนคุณว่า";
	mes "ถ้าคุณไม่แข็งแกร่งมากพอ, คุณอาจไม่ต้องการไป.";
	next;
	mes "[Paul]";
	mes "คุณต้องการ";
	mes "ที่จะลงชื่อไปหรือไม่?";
	mes "ค่าเข้าร่วมเพียง 200 เซนี่เท่านั้น";
	next;
	switch(select("ไปด้วยสิ!","ไม่ดีกว่า.")) {
	case 1:
		if (Zeny < 200) {
			mes "[Paul]";
			mes "ดูเหมือนว่าคุณไม่มีเงินเพื่อน แต่โปรดกลับมาเมื่อคุณสามารถจ่ายได้";
			close;
		}
		Zeny = Zeny - 200;
		warp "alb2trea",62,69;
		close;
	case 2:
		mes "[Paul]";
		mes "โอเค, ถ้าเปลี่ยนใจ";
		mes "แล้วคุณมาหาฉันได้ทุกเมื่อ.";
		mes "";
		close;
	}
}

alberta,190,173,4	script	Phelix	4_M_03,{
	mes "[ฟีลิกซ์]";
	if ((MaxWeight - Weight) < 10000) {
		mes "รอสักครู่!!";
		mes "คุณเอาของมามากเกินไป!";
		mes "คุณไม่สามารถรับไอเท็มเพิ่มเติมได้!";
		mes "กรุณาลดจำนวนไอเท็ม,";
		mes "แล้วมาหาฉันอีกครั้ง.";
		close;
	}
	if (@event_zelopy == 0) {
		mes "คุณทำไรมาที่นี่?";
		mes "ไม่มีอะไรที่คุณจะได้รับฟรีในเรือนี้, ถ้าคุณต้องการอะไร, ให้ทำงานเพื่อได้มา!!";
		next;
		mes "[ฟีลิกซ์]";
		mes "ฮืม, ดังนั้นทำไมคุณไม่นำมาให้ฉัน 10 Jellopy แล้วฉันจะให้ไอเท็ม 1 อัน. เสียงดีมั้ย?";
		mes "หรือถ้ามันยากมากกับอะไรที่คุณอยากได้, 3 Jellopy สำหรับ 1 แครรอท.";
		next;
		mes "[ฟีลิกซ์]";
		mes "ถ้าคุณสนใจในข้อเสนอของฉัน, เอาไปทำให้ฉันเห็น.";
		@event_zelopy = 1;
		close;
	}
	mes "ฮืม.. คุณต้องการแลกเจลโลพีเพื่อรับ Red Potion หรือแครรอทไหม? คืออะไรหล่ะ?";
	next;
	switch(select("- Red Potion","- แครรอท")) {
	case 1:
		mes "[ฟีลิกซ์]";
		mes "โอเค...ขอดูว่าคุณมีอะไรบ้าง...";
		mes "";
		next;
		mes "[ฟีลิกซ์]";
		if (countitem(909) < 10) {
			mes "เซือ! คุณได้ยินใช่ไหม? ฉันพูดว่า 10 เจลโลพีเพื่อ 1 Red Potion.. คุณหูหนวกเหรอ?";
			close;
		} else {
			.@max = countitem(909)/10;
			mes "ฮืม, ไม่แย่น่ะ...";
			mes "คุณต้องการ";
			mes "รับ Red Potion กี่ขวด?";
			next;
			switch(select("มากที่สุดที่ฉันได้, ครับ.","ฉันต้องการเท่านี้, ครับ.","ละเว้นไป, ฉันชอบเจลโลพี.")) {
			case 1:
				delitem 909,.@max*10;// Jellopy
				getitem 501,.@max;// Red_Potion
				break;
			case 2:
				mes "[ฟีลิกซ์]";
				mes "ฉันไม่ให้คุณมากกว่า 100 ในครั้งเดียว, ดังนั้นอย่าลองกันเยอะ, โอเคหรือไม่? ถ้าคุณไม่ต้องการใด ๆ, ก็บอกว่า '0'.";
				mes "ตอนนี้มากที่สุดที่คุณสามารถได้รับคือ " + .@max + " แต่จำไว้, ไม่เกิน 100 นะ, คุณจะทำให้หลังฉันหักหลอนหรอ?";
				input .@amount;
				next;
				mes "[ฟีลิกซ์]";
				if (.@amount <= 0) {
					mes "ขอบคุณมาก, กลับมาเมื่อไหร่ก็ได้.";
					close;
				}
				if (.@amount > 100) {
					mes "เซือ, ฉันพูดว่าไง? 100 ในครั้งเดียวที่สุด, คุณกำลังพยายามฆ่าฉันใช่มั้ย!";
					close;
				}
				if (countitem(909) < .@amount*10) {
					mes "ฮืม, ดูเหมือนว่าคุณไม่มีเพียงพอ. ไปดึงเจลโลพีมาอีกหากคุณต้องการอะไรมากกว่านี้จากฉัน.";
					close;
				}
				delitem 909,.@amount*10;// Jellopy
				getitem 501,.@amount;// Red_Potion
				break;
			case 3:
				mes "[ฟีลิกซ์]";
				mes "ไม่มีปัญหา,เจอกันคราวหน้า.";
				mes "";
				close;
			}
			mes "[ฟีลิกซ์]";
			mes "นั่นคือทุกอย่างที่คุณต้องการ! ตามที่ฉันสัญญา. อย่าดื่มหลายๆ ขวดในครั้งเดียวนะ.";
			close;
		}
	case 2:
		mes "[ฟีลิกซ์]";
		mes "โอเค, ขอดูว่าคุณมี...";
		next;
		mes "[ฟีลิกซ์]";
		if (countitem(909) < 3) {
			mes "ฮืม, ดูน่ะหล่ะ, ฉันพูดว่า 3 Jellopy สำหรับ 1 Carrot.. คุณเข้าใจมั้ย?";
			close;
		}
		.@max = countitem(909)/3;
		mes "ไม่แย่เลย, น่าจะเป็นเช่นนี้...";
		mes "คุณต้องการเท่าไหร่?";
		next;
		switch(select("มากที่สุดที่ฉันได้, ครับ.","ฉันต้องการเท่านี้, ครับ.","ละเว้นไป, ฉันชอบเจลโลพี.")) {
		case 1:
			delitem 909,.@max*3;// Jellopy
			getitem 515,.@max;// Carrot
			break;
		case 2:
			mes "[ฟีลิกซ์]";
			mes "ถูก, ฉันไม่ให้คุณมากกว่า 100 ในครั้งเดียวนะ, ดังนั้นอย่าพยายามมากนัก, โอเคหรือไม่? ถ้าคุณไม่ต้องการทำอะไร, ก็บอก '0' แล้ว.";
			input .@amount;
			next;
			mes "[ฟีลิกซ์]";
			if (.@amount == 0) {
				mes "โอเคละ, แล้วเจอกันคราวหน้า.";
				close;
			}
			if (.@amount > 100) {
				mes "เซือ, ฉันพูดว่าไง? 100 ในครั้งเดียวที่สุด, ไม่มีมากเกินไป! ฉันไม่ไปทำลายสำนวนของฉันสำหรับคนเฮงซวยเฮงปล่าว!";
				close;
			}
			if (countitem(909) < .@amount*3) {
				mes "ดูเหมือนว่าคุณไม่มีเพียงพอ. ไปดึงเจลโลพีมาเพิ่มถ้าคุณต้องการอะไรมากกว่านี้.";
				close;
			}
			delitem 909,.@amount*3;// Jellopy
			getitem 515,.@amount;// Carrot
			break;
		case 3:
			mes "[ฟีลิกซ์]";
			mes "บ๊ายบาย.";
			close;
		}
		mes "[ฟีลิกซ์]";
		mes "นั่นคือทุกอย่าง! ตามที่ฉันสัญญา. ลองอย่ายวกเจ็บทุกไปหมด.";
		close;
	}
}
