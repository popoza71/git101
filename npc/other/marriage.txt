//===== rAthena Script ======================================= 
//= Player Marriage
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Official Marriage script.
//= Added isloggedin() check, even though Aegis doesn't do it.
//= Currently does not support same-Sex marriages.
//= Old script located in: npc/custom/marriage.txt
//= - Variable in use: wedding_sign (max 1)
//= - Variable in use: $@wedding (max 1) 
//= - Variable in use: $@wed_groom$ $@wed_bride$
//===== Additional Comments: ================================= 
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Fixed a missing ")" [L0ne_W0lf]
//= 1.2 Corrected duration of Wedding status. [L0ne_W0lf]
//= 1.3 Changed permanent global variables to temporary ones. [Euphy]
//=     Added GM NPCs (commented).
//= 1.4 Added GM management function. [Euphy]
//= 1.5 Added Marriage Covenant item and VIP features. [Euphy]
//============================================================

prt_church,97,100,4	script	Wedding Staff#w	71,{
	cutin "wedding_marry01",2;
	if (countitem(6026) == 0) {

		// If the VIP system is enabled:
		// - Non-VIP players must use a Marriage Covenant to get married.
		// - VIP players may either pay the Zeny fee or use a Marriage Covenant.
		if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
			mes "[Marry Happy]";
			mes "ขออภัยคุณไม่สามารถแต่งงานได้ในขณะนี้";
			mes "แต่งงานมีให้เฉพาะสำหรับผู้เล่นทั่วไปเท่านั้น";
			mes "ถ้าคุณมี ^FF0000Marriage Covenant^000000 คุณก็จะสามารถแต่งงานได้!";
			goto L_End;
		}

	} else
		set .@covenant,1;
	if (Upper == 2) {
		mes "[Marry Happy]";
		mes "สวัสดี~";
		mes "ฉันชื่อ Marry Happy";
		mes "และฉันมีข้อกำหนดให้กับคุณ";
		mes "กับข้อมูลที่เกี่ยวข้องกับ";
		mes "การแต่งงาน.";
		mes "คุณมีคำถามหรือไหม?";
		next;
		if (select("~ ฉันต้องการแต่งงาน.:~ ยกเลิก ยกเลิก.") == 1) {
			mes "[Marry Happy]";
			mes "โอ้,ฉันต้องขอโทษ แต่";
			mes "ตัวละครบุญธรรม";
			mes "ไม่ได้รับอนุญาต";
			mes "แต่งงาน ตอนนี้ทำไม";
			mes "คุณไม่สนุกกับการในวัยเด็กล่ะ?";
			goto L_End;
		}
		mes "[Marry Happy]";
		mes "โอ้, แน่นอน คุณ";
		mes "ตอน เด็ก ๆ";
		mes "ไม่สามารถจะแต่งงานได้";
		mes "มีกฏหมายที่ต่อต้านอยู่";
		goto L_End;
	}
	mes "[Marry Happy]";
	mes "การแต่งงานเป็นสิ่งสวยงาม";
	mes "การคบกันที่มีสองจิตใจ";
	mes "เลือกที่จะอยู่ด้วยกันตลอดไป,";
	mes "เพื่อแบ่งปันความสุขและชีวิตของพวกเขา.";
	mes "มีคนพิเศษคนหนึ่ง";
	mes "เหมือนอยู่ในชีวิตของคุณ?";
	next;
	switch(select("~ รายละเอียดพิธีแต่งงาน:~ ขั้นตอนการแต่งงาน:~ สมัครเข้าพิธีแต่งงาน:~ ไม่ล่ะ,ฉันยังไม่มีคู่")) {
	case 1:
		mes "[Marry Happy]";
		mes "พระมหากษัตริย์ Tristram III ใช้";
		mes "การดำเนินพิธีการแต่งงาน,";
		mes "แต่เขาไม่สามารถที่จะทำได้";
		mes "ที่เป็นอย่างนี้ก็เพราะว่าของพระราชกรณียกิจของเขา";
		mes "และระยะเวลาในการทำงานที่รุมเร้า.";
		next;
		mes "[Marry Happy]";
		mes "บาทหลวง";
		mes "เป็นคนแสดงความรักต่อหน้าและเป็นผู้กระทำพิธีให้ในตอนสำคัญ";
		mes "ของพิธีในงานแต่งงาน.";
		mes "เขาจะเป็นคนมีมีเกียริมาก";
		mes "ในราชอาณาจักร Rune-Midgarts .";
		next;
		mes "[Marry Happy]";
		mes "เมื่อคุณแต่งงานกับใครสักคน,";
		mes "มันจะเป็นการใช้ชีวิทั้งชีวิตของคุณ ดังนั้นก่อนที่คุณจะตัดสินใจคุณควรตัดสินใจให้ดี";
		mes "รับข้อเสอในการแต่งงาน.";
		mes "โปรดเลือกชายคนใดคนหนึ่งเท่านั้น";
		mes "ในทางกลับกันชายก็แต่งงานกับผู้หญิง.";
		next;
		mes "[Marry Happy]";
		mes "ถ้าคุณโชคดีพอที่จะ";
		mes "หาคนที่คุณเจอจริงๆ";
		mes "ต้องการที่จะใช้เติมเต็มส่วนที่เหลือ";
		break;
	case 2:
		mes "[Marry Happy]";
		mes "อันดับแรก";
		mes "ขั้นตอนแต่งงานคือ";
		mes "กรอกใบสมัคร.";
		mes "หากอีกฝ่ายเสร็จ";
		mes "คุณจำเป็นต้องสร้างปาร์ตี้เพื่อคุณสองคน.";
		next;
		mes "[Marry Happy]";
		mes "หลังจากที่สร้างปาร์ตี้ขึ้นแล้ว,";
		mes "ทั้งคู่ต้องไปพูดกับ";
		mes "บาทหลวง. เจ้าบ่าว";
		mes "พูดต้องเป็นคนที่พูดคุยกับบาทหลวงเกี่ยวกับ";
		mes "ชื่อของเจ้าสาวให้บาทหลวงรู้ มิฉะนั้นจะหยุดพิธี.";
		next;
		mes "[Marry Happy]";
		mes "หลังจากนั้นเจ้าสาวจะต้อง";
		mes "พูดคุยกับบาทหลวงและบอก";
		mes "ชื่อของเจ้าบ่าวของเธอ.";
		mes "ถ้าชื่อที่บอกนั้นถูกต้องแล้ว";
		mes "บอกให้บาทหลวงทำพิธีต่อ พวกเขาก็จะ";
		mes "ทำการเเลกเปลี่ยนแหวน.";
		next;
		mes "[Marry Happy]";
		mes "เมื่อแหวนแต่งงานมี";
		mes "การเปลี่ยน, ทั้งคู่ก็จะมีข้อผูกมัดกันไว้อย่างแน่นอน,";
		mes "ในจุด เพื่อไม่ให้มีโอกาสที่จะเปลี่ยนความคิดของคุณเพื่อให้ ...";
		next;
		mes "[Marry Happy]";
		mes "มีโอการเลือกสิ่งต่างๆมากมาย";
		mes "ใครบ้างคนก็ต้องการพบกับคู่รัก";
		mes "โปรดแต่งงานในครั้งเดียว";
		next;
		mes "[Marry Happy]";
		mes "สุดท้าย ให้ทั้งคู่แน่ใจว่าแล้ว";
		mes "ให้พูดคุยกับบาทหลวง และหนุ่มสาวทั้งคู่";
		mes "ต้องพูดคุยกับบาทหลวง แต่ ถ้าคุณใช้เวลานานเกินไป,";
		mes "พิธีจะหยุดโดยอัตโนมัติและคุณจะต้องลงทะเบียนใหม่อีกครั้ง.";
		next;
		mes "[Marry Happy]";
		mes "เจ้าสาวจำ มีเวลาเพียง";
		mes "3 นาที ที่จะต้องพูดคุยกับบาทหลวงให้เสร็จ";
		mes "หลังจากที่เจ้าบ่าวได้พูดคุยกับบาทหลวง";
		mes "เสร็จสิ้น.";
		next;
		if (select("~ ขอบคุณ,ช่วยเราได้เยอะมากเลย:~ วิธีที่ง่ายที่สุดที่จะพูดชื่อผู้ร่วมงานของฉัน") == 1) {
			mes "[Marry Happy]";
			mes "ดี,ฉันพร้อมให้ความช่วยเหลือ";
			mes "โปรดดำเนินการต่อไปเพื่อทำให้งานแต่งงาน";
			mes "เป็นไปได้อย่างอย่างราบรื่นที่สุดเท่าที่ทำได้.";
			mes "ถ้ามีบางสิ่งบางอย่าง";
			mes "คุณไม่เข้าใจ,";
			mes "อย่าลังเลที่จะถามฉันอีก.";
			goto L_End;
		}
		mes "[Marry Happy]";
		mes "วิธีที่ง่ายที่สุดในการเขียน";
		mes "ชื่อคู่ของคุณ";
		mes "บาทหลวงก็จะทำการส่ง";
		mes "ข้อความที่เกี่ยวกับส่วนของคุณและ";
		mes "แล้วคลิกซ้ายเพื่อดูรายละเอียดชื่อที่ส่งให้.";
		next;
		mes "[Marry Happy]";
		mes "กด Ctrl+C";
		mes "คีย์ในการคัดลอกชื่อ จากนั้น,";
		mes "คุณสามารถวางชื่อ";
		mes "โดยการกด Shift + Insert มันคือสิ่งที่ง่ายที่สุด";
		next;
		mes "[Marry Happy]";
		mes "เอาล่ะตอนนี้เรามาลอง";
		mes "ในการปฏิบัติเกี่ยวกับ";
		mes "ชื่อของคู่ของคุณ";
		mes "ในสิ่งที่ฉันได้เพิ่งอธิบาย.";
		next;
		input .@partner$;
		mes "[Marry Happy]";
		mes "เอาล่ะหลังจากที่คุณได้";
		mes "ตัดสินใจที่จะแต่งงาน,";
		mes "กลับมาหาฉันและ";
		mes "ส่งใบสมัครของคุณ.";
		break;
	case 3:
		cutin "wedding_marry02",2;
		if (VIP_SCRIPT) {
			mes "[Marry Happy]";
			mes "^FF0000รอก่อน !^000000";
			mes "^FF0000หากบัญชี VIP คุณต้องมีกติกาการแต่งงานที่จะแต่งงาน^000000";
			mes "^FF0000โปรดตรวจสอบสถานะปัจจุบันของบัญชีของคุณและคนที่คุณรักก่อนจะลงทะเบียนแต่งงาน^000000";
			next;
		}
		if (Sex == SEX_MALE) {
			mes "[Marry Happy]";
			mes "คุณอยากจะแต่งงานหรือไม่?";
			mes "ในฐานะเจ้าบ่าวคุณต้องเตรียมตัว";
			if (.@covenant) {
				mes "^3377FF1 Tuxedo^000000 และ ^3377FF1 Marriage Covenant^000000.";
				mes "ต้องจัดเตรียมชุดแต่งงานของตัวเองและจ่าย";
				mes "ค่าของ จำนวน 1,200,000 Zeny.";
			} else {
				mes "^3377FF1 Tuxedo^000000 และจ่าย ^3377FF1,300,000 zeny^000000.";
				mes "ต้องจัดเตรียมชุดแต่งงานของตัวเองและจ่าย";
				mes "ค่าของ จำนวน 1,200,000 Zeny.";
			}
		} else {
			mes "[Marry Happy]";
			mes "คุณอยากจะแต่งงานหรือไม่?";
			mes "ในฐานะเจ้าสาวคุณต้องเตรียมตัว";
			if (.@covenant) {
				mes "^3377FF1 Wedding Dress^000000 และ ^3377FFMarriage Covenant^000000.";
				mes "เจ้าบ่าวต้องนำทักซิโดและจ่ายเงิน";
				mes "1,300,000 zeny ข้อตกลงในการสมรส";
			} else {
				mes "^3377FF1 Wedding Dress^000000 และจ่ายเงินเพิ่ม 1,200,000 Zeny. เจ้าบ่าวต้อง";
				mes "นำชุด Tuxedo และจ่าย 1,300,000 zeny เพื่อแต่งงาน";
			}
		}
		next;
		mes "[Marry Happy]";
		mes "เจ้าบ่าวและเจ้าสาวยังต้อง";
		mes "มีแหวน ^3377FF1 Diamond Ring^000000 อีกด้วย";
		mes "สำหรับแลกเปลี่ยนกันในตอนแต่งงาน.";
		mes "และคุณจะต้องใช้มันหลังจากยื่น";
		mes "ใบสมัครงานแต่งงาน.";
		next;
		mes "[Marry Happy]";
		mes "เจ้าสาวและเจ้าบ่าวจะต้องลงทะเบียนให้เสร็จสิ้น";
		mes "ขั้นตอนการสมัครก่อน";
		mes "งานแต่งงานจะเกิดขึ้น.";
		mes "ถ้าคุณต้องการ";
		mes "แต่งงานในตอนนี้?";
		next;
		if (select("Yes:No") == 1) {
			if (getpartnerid()) {
				cutin "wedding_marry02",2;
				mes "[Marry Happy]";
				mes "ฉันเสียใจ, แต่คุณไม่สามารถ";
				mes "ที่จะขอแต่งงานได้อีก!";
				mes "ฉันไม่สามารถช่วยให้คุณหย่ากับ";
				mes "คู่สมรสของคุณ";
				break;
			}
			else if (wedding_sign == 1) {
				mes "[Marry Happy]";
				mes "คุณไม่ได้";
				mes "กรอกใบสมัครให้เสร็จ?";
				mes "อืม, ตรวจสอบให้แน่ใจว่าของคุณ";
				mes "มีความสัมพันธ์กับผู้อื่นหรือไม่";
				mes "หากต้องการแต่งงาน จงทำตามขั้นตอนการสมัครและ";
				mes "จากนั้นก็พูดคุยกับบาทหลวง.";
				break;
			}
			else if (BaseLevel < 45) {
				mes "[Marry Happy]";
				mes "อืม, คุณจะต้อง";
				mes "แข็งแรงพอที่จะป้องกัน";
				mes "คนที่คุณรักก่อน";
				mes "คุณสามารถพิจารณาการแต่งงาน.";
				goto L_End;
			}
			else if (countitem(2613) < 1) {
				mes "[Marry Happy]";
				mes "คุณลืม";
				mes "นำแหวนเพชร";
				mes "เพื่อแลกเปลี่ยนกับคู่ของคุณ";
				mes "ในระหว่างพิธีแต่งงาน?";
				goto L_End;
			}
			else if (Sex == SEX_FEMALE) {
				if (countitem(6026) == 0 && Zeny < 1200000) {
					mes "[Marry Happy]";
					mes "ฉันขอโทษแต่คุณไม่ได้";
					mes "มี 1,200,000 zeny";
					mes "ที่เจ้าสาวทุกคนจะต้องจ่ายเงิน";
					mes "สำหรับงานแต่งงาน.";
					goto L_End;
				}
				else if (countitem(2338) < 1) {
					mes "[Marry Happy]";
					mes "โอ้คุณลืมไปไหม";
					mes "ชุดแต่งงานของคุณ?";
					mes "รีบไปหาแล้วล่ะ";
					mes "นำมันมาให้ฉัน - คุณ";
					mes "ต้องการมันอย่างแน่นอน";
					mes "สำหรับงานแต่งงาน!";
					goto L_End;
				}
			}
			else if (Sex == SEX_MALE) {
				if (countitem(6026) == 0 && Zeny < 1300000) {
					mes "[Marry Happy]";
					mes "ฉันขอโทษแต่คุณไม่ได้";
					mes "มี 1,300,000 zeny";
					mes "ที่เจ้าบ่าวทุกคนจะต้องจ่ายเงิน";
					mes "สำหรับงานแต่งงาน.";
					goto L_End;
				}
				else if (countitem(7170) < 1) {
					mes "[Marry Happy]";
					mes "Tuxedo ของคุณอยู่ที่ไหน?";
					mes "คุณจะต้องสวมใส่ในระหว่างงานแต่งงาน";
					mes "นำมาสวมใส่ซะ";
					mes "ก่อนที่จะเริ่มแต่งงาน?";
					goto L_End;
				}
			}
			mes "[Marry Happy]";
			mes "ดูเหมือนว่าคุณ";
			mes "มีทุกอย่างพร้อม.";
			mes "ถึงแม้ว่าฉันไม่แน่ใจว่าใคร";
			mes "คือคู่ของคุณ  ขอให้ฉันเป็น";
			mes "คนแรกที่แสดงความยินดีกับคุณ";
			mes "ในวันแต่งงานที่จะเกิดขึ้นของคุณ~";
			next;
			mes "[Marry Happy]";
			mes "ตอนนี้เอาล่ะ";
			mes "กรุณาเขียนใบสมัคร";
			mes "ลงชื่อที่แน่นอนของคุณที่นี่.";
			next;
			while(1) {
				input .@name$;
				if (.@name$ != strcharinfo(0)) {
					mes "[Marry Happy]";
					mes "อืมม, คุณต้องเขียน";
					mes "ให้ตรงตามชื่อของคุณ";
					mes "มันจะปรากฏขึ้น. บางทีคุณอาจจะ";
					mes "ต้องคัดลอกและวาง?";
					next;
				}
				else 
					break;
			}
			mes "[Marry Happy]";
			mes "มันดูเหมือนว่า";
			mes "ใบสมัครของคุณเสร็จแล้ว.";
			mes "โปรดจำไว้ว่าคุณจะต้อง";
			mes "จะบอกบาทหลวง";
			mes "เกี่ยวกับชื่อที่ของคู่สมรส";
			next;
			mes "[Marry Happy]";
			mes "เมื่อคู่ของคุณ";
			mes "พร้อมกับใบสมัคร";
			mes "สิ่งต่อไปคือคุณควร";
			mes "พูดคุยกับบาทหลวงเพื่อเริ่มต้น";
			mes "การฉลองงานแต่งงาน.";
			emotion ET_THROB;
			next;
			mes "[Marry Happy]";
			if (Sex == SEX_FEMALE) {
				mes "เนื่องจากคุณเป็นเจ้าสาว,";
				mes "คุณจำเป็นต้องรอให้";
				mes "เจ้าบ่าวพูดคุยกับบาทหลวงเสียก่อน";
				mes "เมื่อเจ้าบ่าวพูดคุยเสร็จแล้ว จะถึงตาคุณ";
				if (.@covenant)
					delitem 6026,1; //Marriage_Covenant
				else
					set Zeny, Zeny-1200000;
				delitem 2338,1; //Wedding_Dress
			}
			else {
				mes "เนื่องจากคุณเป็นเจ้าบ่าว,";
				mes "คุณจำเป็นต้องพูดคุยกับบาทหลวงก่อน";
				mes "หลังจากนั้นเจ้าสาวต้องพูดคุยกับบาทหลวงเป็นคนต่อไป";
				if (.@covenant)
					delitem 6026,1; //Marriage_Covenant
				else
					set Zeny, Zeny-1300000;
				delitem 7170,1; //Tuxedo
			}
			delitem 2613,1; //Diamond_Ring
			set wedding_sign,1;
			goto L_End;
		}
		mes "[Marry Happy]";
		mes "คุณสามารถกลับมาได้เสมอ";
		mes "เมื่อต้องการแต่งงาน";
		break;
	case 4:
		cutin "wedding_marry02",2;
		donpcevent "Single Army#Prontera::OnEnable";
		donpcevent "Single Army#Geffen::OnEnable";
		donpcevent "Single Army#Morocc::OnEnable";
		donpcevent "Single Army#Payon::OnEnable";
		donpcevent "Single Army#Amatsu::OnEnable";
		donpcevent "Single Army#Kunlun::OnEnable";
		emotion ET_HUK;
		mes "[Single Army]";
		mes "^CC9933ท่านต้องสวมใส่";
		mes "แหวนนั้น";
		mes "ด้วยตัวเอง!^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Prontera");
		next;
		mes "[Single Army]";
		mes "^330099สิ่งที่เสียไป";
		mes "จากคนที่ไม่เคยมีอะไรเลย";
		mes "ฉันสามารถ";
		mes "ทำมันด้วยตัวของฉันเอง!^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Geffen");
		next;
		mes "[Single Army]";
		mes "^666666สวรรค์, ฉันได้รับการฝึกอบรม";
		mes "ทั้งหมดด้วยตัวเองตั้งแต่";
		mes "เกิด, ทั้งหมดทุกอย่าง";
		mes "เปลี่ยนความคิดของฉัน!^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Morocc");
		next;
		mes "[Single Army]";
		mes "^666600ฉันเลือกที่จะใช้จ่าย";
		mes "คริสมาสต์เพียงอย่างเดียว...";
		mes "และการเล่นไพ่คนเดียว^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Payon");
		next;
		mes "[Single Army]";
		mes "^CC9966ผู้หญิงทำอาจร้ายฉัน";
		mes "ฉันกลัวเหลือเกิน";
		mes "ตอนนี้ฉันไม่มีอะไรเหลือ^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Amatsu");
		next;
		mes "[Single Army]";
		mes "^669900...เราเป็นคนของกองทัพ";
		mes "เพื่อปกป้องบ้านเมือง^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Kunlun");
		close2;
		cutin "wedding_marry01",255;
		emotion ET_SWEAT;
		donpcevent "Single Army#Prontera::OnInit";
		donpcevent "Single Army#Geffen::OnInit";
		donpcevent "Single Army#Morocc::OnInit";
		donpcevent "Single Army#Payon::OnInit";
		donpcevent "Single Army#Amatsu::OnInit";
		donpcevent "Single Army#Kunlun::OnInit";
		end;
	}
	close2;
	cutin "wedding_marry01",255;
	end;

L_End:
	close2;
	cutin "",255;
	end;
}

prt_church,97,102,0	script	Single Army#Prontera	105,{
	mes "[Single Army]";
	mes "^CC9933คุณมีการปรับแต่ง";
	mes "ไอเทมในตัวคุณ";
	mes "ทำให้มันยิ่งดีขึ้น!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Prontera";
	end;

OnEnable:
	hideoffnpc "Single Army#Prontera";
	emotion ET_GO;
	end;
}

prt_church,98,102,0	script	Single Army#Geffen	705,{
	mes "[Single Army]";
	mes "^330099มันคือของเสีย";
	mes "จากผู้คนใน";
	mes "ดันเจี้ยน! ฉันสามารถ";
	mes "เอามันไปให้เจ้าของได้!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Geffen";
	end;

OnEnable:
	hideoffnpc "Single Army#Geffen";
	emotion ET_GO;
	end;
}

prt_church,99,102,0	script	Single Army#Morocc	707,{
	mes "[Single Army]";
	mes "^666666เฮ้, ฉันได้รับงานฝึก";
	mes "ทุกๆอย่างในตัวฉันตั้งแต่";
	mes "ฉันเกิด, ทุกวิธีทาง";
	mes "มันคือการเปลี่ยนอาชีพ!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Morocc";
	end;

OnEnable:
	hideoffnpc "Single Army#Morocc";
	emotion ET_GO;
	end;
}

prt_church,100,102,0	script	Single Army#Payon	708,{
	mes "[Single Army]";
	mes "^666600ฉันเลือกที่จะ";
	mes "Christmas อยู่คนเดียว...";
	mes "การเล่นไพ่คนเดียวและ";
	mes "เล่นจิกซอร์คนเดียว!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Payon";
	end;

OnEnable:
	hideoffnpc "Single Army#Payon";
	emotion ET_GO;
	end;
}

prt_church,101,102,0	script	Single Army#Amatsu	767,{
	mes "[Single Army]";
	mes "^CC9966ผู้หญิงอาจจะทำให้";
	mes " วิญาณของฉัน, แต่พวกเขาไม่สามารถ";
	mes "เอามันไปจาก... ความเป็นอิสระของฉัน!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Amatsu";
	end;

OnEnable:
	hideoffnpc "Single Army#Amatsu";
	emotion ET_GO;
	end;
}

prt_church,102,102,0	script	Single Army#Kunlun	780,{
	mes "[Single Army]";
	mes "^669900...พวกเราไม่เสียเงิน! พวกเรา";
	mes "เป็นทหารที่มีความเป็นคงกระพัน!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Kunlun";
	end;

OnEnable:
	hideoffnpc "Single Army#Kunlun";
	emotion ET_GO;
	end;
}

prt_church,100,128,4	script	Bishop#w	60,{
	cutin "wedding_bomars01",2;
	if (Upper == 2) {
		mes "[Vomars]";
		mes "เยี่ยมๆ,เด็กน้อย.";
		mes "ฉันทำมันหาญไหม? อืมมมม.";
		mes "คุณรู้ไหมว่า";
		mes "แม่กับพ่อของคุณอยู่ไหน?";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}

	if (!getpartnerid()) {
		if (!$@wedding) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex == SEX_MALE) {
						set $@wedding,1;
						initnpctimer;
						mes "[Vomars]";
						mes "คนรักวัยเด็ก, โปรด";
						mes "จำช่วงเวลานี้";
						mes "ส่วนที่เหลือของชีวิตคุณในอนาคตของคุณอาจได้รับพรด้วยความสงบ";
						mes "และความสุข คุณอาจได้แบ่งปันความรักร่วมกัน";
						mes "เติบโตไปพร้อมกับวันที่ผ่านแต่ละวัน.";
						next;
						mapannounce "prt_church","เป็นข้อเสนอแต่งงานจากเจ้าบ่าว, คุณ. " + strcharinfo(0) + "...",bc_map;
						mes "[Vomars]";
						mes "จนกว่าจะสิ้นสุด";
						mes "โลก,คุณอาจจะยืนอยู่";
						mes "โดยข้างใดข้างหนึ่ง";
						mes "คนที่คุณรักให้การสนับสนุน";
						mes "สำหรับเธอและเขาอาจปกป้องคุณ";
						mes "ผมทราบชื่อของเจ้าสาวของคุณ?";
						next;
						input $@wed_bride$;
						mes "[Vomars]";
						mes "คุณ "+ strcharinfo (0) + ".";
						mes "คุณสาบานต่อชีวิตของคุณ";
						mes "ชั่วชีวิตที่นิรันดร์ที่คุณจะยึดมั่น";
						mes "การดูแลและสำหรับเจ้าสาวของคุณ,";
						mes "คุณ " + $@wed_bride$ + "?";
						next;
						select("ฉัน ยอมรับ");
						set $@wed_groom$,strcharinfo(0);
						mes "[Vomars]";
						mes "ตอนนี้ก็เป็นเวลา";
						mes "ของเจ้าสาวเพื่อให้";
						mes "รับปากรับคำแต่งงานของเธอ....";
						mes "ถ้าเธอจะมาข้างหน้า...";
						close2;
						mapannounce "prt_church","เจ้าบ่าว, คุณ. " + strcharinfo(0) + ", ได้ทำคำปฏิญาณตนไว้กับนางสาว" + $@wed_bride$ + "...",bc_map;
						cutin "",255;
						end;
					}
					mes "[Vomars]";
					mes "ฉันขอโทษ แต่เจ้าบ่าว";
					mes "ต้องพูดกับฉันครั้งแรกใน";
					mes "เพื่อที่จะเริ่มจัดงานแต่งงาน.";
					mes "มันเป็นโพรโทคอแฟชั่น เก่า,";
					mes "แต่ก็ยอมรับว่ามันไม่";
					mes "เก็บสิ่งที่ทำงานได้อย่างราบรื่น.";
					goto L_End;
				}
				mes "[Vomars]";
				mes "ก่อนที่คุณจะสามารถ";
				mes "แต่งงานแล้วคุณจะต้อง";
				mes "รูปแบบแรกของงาน";
				mes "สองกับคู่ของคุณ.";
				mes "จากนั้นเราจะสามารถดำเนินการต่อไป";
				mes "กับพิธี.";
				goto L_End;
			}
			mes "[Vomars]";
			mes "คุณจะต้องยื่นขอ";
			mes "การแต่งงานให้มีความสุข";
			mes "ก่อนที่คุณจะได้แต่งงาน.";
			mes "Happy Marry จะแจ้งให้คุณทราบ";
			mes "อะไรที่คุณจะต้องทำ";
			mes "เพื่อเตรียมความพร้อมสำหรับการแต่งงาน.";
			goto L_End;
		}
		else if ($@wedding == 1) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex == SEX_FEMALE) {
						if (strcharinfo(0) == $@wed_bride$) {
							mes "[Vomars]";
							mes "คนรักวัยหนุ่มสาว โปรด";
							mes "จำช่วงเวลานี้";
							mes "ส่วนที่เหลือของชีวิตคุณในอนาคตของคุณอาจได้รับพรด้วยความสงบ";
							mes "และความสุข คุณอาจได้แบ่งปันความรักร่วมกัน";
							mes "เติบโตไปพร้อมกับวันที่ผ่านแต่ละวัน.";
							next;
							mapannounce "prt_church","มาฟังกันเถอะว่าเจ้าสาวคือนางสาว"+$@wed_bride$+", ได้กล่าว...",bc_map;
							mes "[Vomars]";
							mes "นางสาว "+$@wed_bride$+"...";
							mes "คุณสาบานที่จะอยู่";
							mes "เพื่อ "+$@wed_groom$+",";
							mes "ที่จะอยู่เคียงข้างเขาไม่ว่า";
							mes "สิ่งที่อันตรายที่อาจจะเกิดขึ้น?";
							next;
							if (select("^FF0000ไม่.^000000:ฉันสัญญา") == 1) {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","คู่ถัดไปโปรดดำเนินการต่อ...",bc_map;
								mes "[Vomars]";
								mes "เพราะ "+$@wed_groom$;
								mes "ไม่ใช่คุณคนเดียว";
								mes "ต้องการแต่งงาน";
								set $@wedding,0;
								close2;
								stopnpctimer;
								cutin "",255;
								end;
							}
							mes "[Vomars]";
							mes "คุณจะสาบานไหมว่าจะมี";
							mes "ความจงรักภักดีและความอดทน?";
							mes "คุณจะแต่งงานกับ "+$@wed_groom$+"?";
							next;
							if (select("ใช่,ฉันสัญญา:^FF0000ไม่,ฉันยังกังวลใจเล็กหน่อย.^000000") == 1) {
								if (isloggedin(getcharid(3,$@wed_groom$))) {
									if (marriage($@wed_groom$)) {
										//Call Wedding effect
										wedding;
										//Give ring to Bride, and change to wedding sprite.
										sc_start SC_Wedding,3600000,1;
										getitem 2635,1; //Bride_Ring
										//Give ring to Groom, and change to wedding sprite.
										attachrid(getcharid(3,$@wed_groom$));
										sc_start SC_Wedding,3600000,1;
										getitem 2634,1; //Bridegroom_Ring
										detachrid;
										//Switch Script progression back to Bride
										attachrid(getcharid(3,$@wed_bride$));
										cutin "wedding_bomars02",2;
										mapannounce "prt_church","ตอนนี้ฉันได้ประกาศว่า คุณ, "+$@wed_groom$+" และ "+$@wed_bride$+", ได้เป็นสามีและภรรยา.",bc_map;
										mes "[Vomars]";
										mes "ด้วยพลังอันศักดิ์สิทธิ์";
										mes "ฉันเป็นบาทหลวง";
										mes "ในราชอาณาจักร Rune-Midgarts ,";
										mes "ตอนนี้ผมคาดว่าคุณจะเป็นสามี";
										mes "และภรรยา. อนาคตของคุณอาจมี";
										mes "ความสุขกับความสุขที่ดีมาก.";
										next;
										mes "[Vomars]";
										mes "และสุดท้าย...";
										mes "จะมีความสุขทุกครั้ง,";
										mes "ที่รัก "+$@wed_bride$+"...";
										set $@wed_groom$,"";
										set $@wed_bride$,"";
										set $@wedding,0;
										close2;
										stopnpctimer;
										cutin "",255;
										detachrid;
										end;
									}
								}
								cutin "wedding_bomars03",2;
								mes "[Vomars]";
								mes "อืมม. ดูเหมือน่วา";
								mes "เจ้าบ่าวของคุณได้ไปก่อน";
								mes "พิธีแต่งงานจะเสร็จ";
								mes "โปรดพยายามใหม่อีกครั้ง";
								mes "ทำให้เขากลับมา.";
								goto L_End;
							}
							else {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","ไม่อยากเชื่อ คุณ "+$@wed_bride$+" ได้ปฏิเสธ "+$@wed_groom$+"'ข้อเสนอแต่งงาน!",bc_map;
								mes "[Vomars]";
								mes "อืมม. ดูเหมือนว่า";
								mes "คุณเปลี่ยนใจของ";
								mes "คุณแล้ว. แม้ว่าฉันรู้สึก";
								mes "ไม่ดีกับเจ้าบ่าว, แต่คุณ";
								mes "จะทำยังไงกับใจของคุณ";
								mes "บอกตัวคุณว่ามันถูกแล้ว.ตอนนี้เลย, เร็ว!";
							}
							set $@wed_groom$,"";
							set $@wed_bride$,"";
							set $@wedding,0;
							close2;
							stopnpctimer;
							cutin "",255;
							end;
						}
						callsub S_Busy;
					}
					callsub S_Busy;
				}
				callsub S_Busy;
			}
			if (strcharinfo(0) == $@wed_bride$) {
				mes "[Vomars]";
				mes "อืม.ดูเหมือนว่า";
				mes "คุณยังไม่ได้กรอกใบสมัคร";
				mes "การแต่งงานของคุณ";
				mes "กรุณาพูดกับ Happy Marry";
				mes "เพื่อให้เราสามารถเริ่มต้น";
				mes "ในพิธีงานแต่งงาน.";
				goto L_End;
			}
			callsub S_Busy;
		}
		callsub S_Busy;
	}
	mes "[Vomars]";
	mes "ฉันขอให้คุณมีความสุข";
	mes "ไปตลอดกาล. ไม่ว่าตอนนี้";
	mes "มันจะมืดมนไปสักแค่ไหน";
	mes "เราจะยืนอยู่ข้างคุณเสมอ";
	mes "จะมีความรักอยู่ข้างตัวคุณ";
	mes "และในอนาคตด้วย.";

L_End:
	close2;
	cutin  "",255;
	end;

S_Busy:
	if ($@wed_groom$ != "" && $@wed_bride$ != "") {
		mes "[Vomars]";
		mes "การแต่งงานของ";
		mes "นางสาว "+ $@wed_bride$ +" และ";
		mes "นาย "+ $@wed_groom$;
		mes "ขณะนี้กำลังดำเนินการอยู่.";
		mes "กรุณาเก็บเสียงของคุณลง.";
		goto L_End;
	}
	mes "[Vomars]";
	mes "ฉันกำลังดำเนินการจัดงานแต่งงาน";
	mes "สำหรับคู่อื่นในขณะนี้,ดังนั้น";
	mes "โปรดรอสักครู่";
	mes "แล้วค่อยกลับมาใหม่ ขอขอบคุณที่ให้ความเข้าใจ... ^FFFFFF ^000000";
	goto L_End;

OnStop:
	stopnpctimer;
	end;

OnReset:
	set $@wed_groom$,"";
	set $@wed_bride$,"";
	set $@wedding,0;
	end;

OnTimer180000:
	mapannounce "prt_church","คุณตอบช้าเกินไปแล้ว...คู่ถัดไปโปรดดำเนินการต่อ",bc_map;
	donpcevent "Bishop#w::OnReset";
	stopnpctimer;
	end;
}

prt_church,28,178,4	script	The King of Rune-Midgarts	108,{
	callfunc "F_GM_NPC";
	mes "[Vomars]";
	mes "คุณเป็นใคร?";
	mes "เจ้าต้องรู้จัก";
	mes "รหัสผ่านเพื่อเรียกใช้";
	mes "พลังอันน่าเกรงขามของข้า";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Vomars]";
		mes "นี่คือ";
		mes "ไม่มีที่สำหรับ";
		mes "พูดเล่นไปรอบ ";
		close2;
		warp "prt_church",101,102;
		end;
	}
	mes "[Wedding Switch]";
	mes "มีปัญหากับ";
	mes "พิธีแต่งงาน?";
	mes "ฉันสามารถรีเซ็ต Bishop ได้";
	mes "Vomars NPC ถ้าคุณต้องการ";
	next;
	switch(select("ไม่ , ขอบคุณ:รีเซ็ต")) {
	case 1:
		mes "[Wedding Switch]";
		mes "เอาล่ะ";
		mes "อย่างไรก็ตาม ถ้า";
		mes "บิชอปโวมาร์ส";
		mes "NPC ติดค้างอยู่ มันอาจจะ";
		mes "ดีที่สุดที่จะรีเซ็ตมัน";
		close;
	case 2:
		donpcevent "Bishop#w::OnStop";
		donpcevent "Bishop#w::OnReset";
		mapannounce "prt_church","คุณตอบกลับช้าเกินไป... คู่ต่อไป โปรดดำเนินการต่อ",bc_map;
		mes "[Wedding Switch]";
		mes "The Bishop Vomars NPC";
		mes "ได้ถูกเปิดใช้งานอีกครั้งแล้ว";
		mes "มันควรจะเป็นไปได้แล้ว";
		mes "เพื่อดำเนินการจัดงานแต่งงาน";
		close;
	}
}

prt_church,20,179,4	script	Divorce Staff	71,{
	callfunc "F_GM_NPC";
	mes "[Bad Ending]";
	mes "เอ่อโอ้...";
	mes "คุณก็รู้ว่าฉันทำไม่ได้";
	mes "ทำทุกอย่างเพื่อคุณ";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Bad Ending]";
		mes "อืม...";
		mes "คุณจริงๆ";
		mes "ไม่ควรจะเป็น";
		mes "ณ ที่แห่งนี้...";
		close;
	}
	mes "[Bad Ending]";
	mes "เยี่ยมมาก คุณรู้เรื่องนี้";
	mes "รหัสผ่าน! ตอนนี้คุณทำแล้ว";
	mes "ต้องการให้ฉันลบ";
	mes "แหวนแต่งงานในตัวคุณ";
	mes "สินค้าคงคลัง?";
	next;
	switch(select("ดรอปแหวนแต่งงาน 1 วง:เก็บไว้")) {
	case 1:
		set .@ring, (Sex == SEX_MALE)?2634:2635; //Bridegroom_Ring, Bride_Ring
		if (countitem(.@ring)) {
			delitem .@ring,1;
			mes "[Bad Ending]";
			mes "มันจบแล้ว!";
		} else {
			mes "[Bad Ending]";
			mes "ฉันหาไม่เจอ";
			mes "แหวนแต่งงาน...";
			mes "โปรดตรวจสอบให้แน่ใจ";
			mes "ว่ามันไม่ได้ติดตั้ง";
		}
		close;
	case 2:
		mes "[Bad Ending]";
		mes "คุณแน่ใจว่าคุณต้องการ";
		mes "เก็บแหวนนั่นไว้?";
		mes "เอาล่ะ แต่ถ้ามันกลายเป็น";
		mes "มีปัญหา เจ้ามาหาข้า";
		close;
	}
}

prt_church,22,179,4	script	Remarry Staff	71,{
	callfunc "F_GM_NPC";
	mes "[Wedding Again]";
	mes "หืม...?";
	mes "จริงๆ แล้วคืออะไร";
	mes "คุณมาอยู่ที่นี่เหรอ?";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Wedding Again]";
		mes "อ๊าก!";
		mes "นักผจญภัย";
		mes "เหมือนที่คุณไม่ควร";
		mes "จงอยู่ที่นี่!";
		close;
	}
	if (getpartnerid()) {
		mes "[Wedding Again]";
		mes "อืม...";
		mes "ฉันทำได้เพียงสร้าง";
		mes "แหวนแต่งงานถ้า";
		mes "ตัวละครแต่งงานแล้ว";
		close;
	}
	mes "[Wedding Again]";
	mes "อา ฉันเห็นนะว่าเธอ";
	mes "รู้รหัสผ่าน";
	mes "เอาล่ะ ถ้าคุณทำอย่างใด";
	mes "ทำแหวนแต่งงานของคุณหาย";
	mes "ฉันสามารถทำให้คุณเป็นคนใหม่ได้";
	next;
	switch(select("ทำแหวนแต่งงานใหม่:ยกเลิก.")) {
	case 1:
		set .@ring, (Sex == SEX_MALE)?2634:2635; //Bridegroom_Ring, Bride_Ring
		if (countitem(.@ring) || isequipped(.@ring)) {
			mes "[Wedding Again]";
			mes "เดี๋ยวก่อน, รอ...";
			mes "คุณกำลังสวมชุดของคุณ";
			mes "แหวนแต่งงาน ฉันดีกว่า";
			mes "อย่าทำให้คุณเป็นอีกคนตั้งแต่นั้นมา";
			mes "คุณไม่จำเป็นต้องมีมากกว่าหนึ่ง";
		} else {
			getitem .@ring,1;
			mes "[Wedding Again]";
			mes "เอาล่ะ~";
			mes "มันเป็นแบรนด์ของคุณ";
			mes "แหวนแต่งงานใหม่!";
		}
		close;
	case 2:
		mes "[Wedding Again]";
		mes "เอาล่ะ ถ้าคุณเคย";
		mes "ทำแหวนแต่งงานของคุณหาย";
		mes "มาหาฉันสิ ถ้าเธอเกิดขึ้น";
		mes "ต้องการอันใหม่ โอเคไหม?";
		close;
	}
}
