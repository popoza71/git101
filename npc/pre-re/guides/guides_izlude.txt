//===== rAthena Script =======================================
//= Izlude Guides
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Translations TH: =====================================
//= Athena Studio
//===== Current Version: =====================================
//= 1.5a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Guides for the City of Izlude
//===== Additional Comments: =================================
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Rescripted to official 10.3 standards. [L0ne_W0lf]
//= 1.5a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//============================================================

izlude,122,84,5	script	Guide#iz::IzludeGuide	105,{
	cutin "prt_soldier",2;
	mes "[Izlude Guide]";
	mes "ยินดีต้อนรับสู่เมื่อง Izlude,";
	mes "ซึ่งเป็นเมืองลูกของเมือง Prontera.";
	mes "ถ้าคุณต้องการคำแนะนำใดๆ";
	mes "รอบ Izlude,";
	mes "คุณสามารถที่จะสอบถามฉันได้ตลอดเวลา.";
	set .@loop1,1;
	while(.@loop1) {
		next;
		switch(select("~ สอบถามไกด์:~ ลบตำแหน่งทั้งหมดออกจาก Mini-Map:~ หมายเหตุ:~ ยกเลิกการสนทนา")) {
		case 1:
			mes "[Izlude Guide]";
			mes "กรุณาเลือก";
			mes "ตำแหน่งจาก";
			mes "เมนูต่อไปนี้.";
			if (.@compass_check == 0) {
				mes "คุณต้องการที่จะ";
				mes "ให้ฉันทำเครื่องหมายสถานที่";
				mes "บน Mini-Map ของคุณหรือ ?";
				next;
				if (select("ใช่:ไม่") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000อดีตสมาคม Swordman^000000:คฤหาสน์ Swordman:สังเวียน:ท่าเรือ Izlude:ร้านจำหน่ายอาวุธ:ร้านค้าทั่วไป:ยกเลิก")) {
				case 1:
					mes "[Izlude Guide]";
					mes "อดีตสมาคม Swordman Association";
					mes "ตั้งอยู่บนเกาะในเมือง";
					mes "Izlude. ทางตะวันตก ถ้าคุณคิดว่าต้องการที่จะเปลี่ยนอาชีพเป็น Swordman,";
					mes "คุณควรจะไปที่นั่นเพื่อรับการทดสอบ.";
					if (.@compass_check)
						viewpoint 1,52,140,0,0xFF0000;
					break;
				case 2:
					mes "[Izlude Guide]";
					mes "คฤหาสน์ Swordman";
					mes "ตั้งอยู่ในภาคตะวันออก";
					mes "ของเกาะเชื่อมต่อกับ Izlude.";
					if (.@compass_check)
						viewpoint 1,214,130,1,0x00FF00;
					break;
				case 3:
					mes "[Izlude Guide]";
					mes "สนามสังเวียนที่มีชื่อเสียงที่สุดใน Izlude";
					mes "จะตั้งอยู่ที่";
					mes "เหนือสุดของ Izlude.";
					if (.@compass_check)
						viewpoint 1,128,225,2,0x00FF00;
					break;
				case 4:
					mes "[Izlude Guide]";
					mes "คุณสามารถพบ";
					mes "กับท่าเรือในภาคตะวันออกเฉียงเหนือ";
					mes "ส่วนหนึ่งของ Izlude";
					mes "ซึ่งคุณสามารถนั่งเรือที่จะนำคุณไปยัง";
					mes "เมือง Alberta หรือ เกาะ Byalan Island.";
					if (.@compass_check)
						viewpoint 1,200,180,3,0xFF0000;
					break;
				case 5:
					mes "[Izlude Guide]";
					mes "คุณสามารถพบร้านขายอาวุธ";
					mes "ได้อย่างง่ายดาย";
					mes "ซึ่งมันจะอยู่ทางทิศตะวันตกเฉียงเหนือของ Izlude.";
					if (.@compass_check)
						viewpoint 1,111,149,4,0xFF00FF;
					break;
				case 6:
					mes "[Izlude Guide]";
					mes "ร้านค้าทั่วไป";
					mes "คงไม่ยากเกินไปที่จะหา";
					mes "มันจะอยู่ทางตะวันออกเฉียงเหนือของ Izlude.";
					if (.@compass_check)
						viewpoint 1,148,148,5,0xFF00FF;
					break;
				case 7:
					mes "[Izlude Guide]";
					mes "กรุณาถามฉันถ้าหากคุณต้องการ ''ลบ";
					mes "ตำแหน่งจาก Mini-Map'' ถ้าคุณ";
					mes "ไม่ต้องการที่จะมี";
					mes "เครื่องหมายแสดงสถานที่ที่ตั้ง";
					mes "บน Mini-Map ของคุณ.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,0,0x00FF00;
			viewpoint 2,237,41,1,0x0000FF;
			viewpoint 2,46,345,2,0x00FF00;
			viewpoint 2,175,220,3,0xFF0000;
			viewpoint 2,134,221,4,0xFF0000;
			viewpoint 2,204,214,5,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Izlude Guide]";
			mes "ด้วยความก้าวหน้าในการใช้เวทมนตร์";
			mes "และ เทคโนโลยีเราได้รับอนุญาติให้";
			mes "เราในการปรับปรุงข้อมูลของเรา";
			mes "ในระบบ, ที่จะช่วยให้การขึ้นเครื่องหมายขนาดเล็ก";
			mes "อยู่บนแผนที่ เพื่อสำหรับ";
			mes "การนำทางที่แสนจะง่ายดายขึ้น.";
			next;
			mes "[Izlude Guide]";
			mes "คุณมี Mini-Map ตั้งอยู่";
			mes "ที่มุมบนด้านขวาของจอ";
			mes "ถ้าคุณไม่สามารถ";
			mes "ที่จะมองเห็นแผนที่ของคุณ, โปรดกด Ctrl + Tab";
			mes "หรือ คลิกที่ปุ่ม ''Map'' ของคุณที่อยู่ในหน้าต่าง Basic Info.";
			next;
			mes "[Izlude Guide]";
			mes "บน Mini-Map ของคุณ,";
			mes "คลิกที่ ''+'' และ ''-''";
			mes "สัญลักษณ์เพื่อที่จะซูมเข้าและซูมออก";
			mes "ในแผนที่ขนาดเล็กของคุณ Mini-Map. เรา";
			mes "หวังว่าคุณจะเพลิดเพลินไปกับการเดินทางของคุณ";
			mes "ที่นี่ในเมืองของ Alberta.";
			break;
		case 4:
			mes "[Izlude Guide]";
			mes "คุณจะปลอดภัย";
			mes "เมื่อคุณเดินทาง และ ไม่ลังเล";
			mes "ที่จะถามฉันหากคุณมีคำถาม";
			mes "เกี่ยวกับเมือง Izlude.";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}

izlude,124,177,5	duplicate(IzludeGuide)	Guide#1izlude	105
izlude,131,177,3	duplicate(IzludeGuide)	Guide#2izlude	105