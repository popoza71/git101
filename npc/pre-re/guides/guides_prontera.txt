//===== rAthena Script =======================================
//= Prontera Guides
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Translations TH: =====================================
//= Athena Studio
//===== Current Version: =====================================
//= 1.6a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Guides for the City of Prontera
//===== Additional Comments: =================================
//= 1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Removed Duplicates [Silent]
//= 1.6 Rescripted to official 10.3 standards. [L0ne_W0lf]
//= 1.6a Fixed some errors with the loop. [Samuray22]
//	-Thanks to $ephiroth.
//= 1.6b Fixed a Problem with Unnecesary RGB Values. [Samuray22]
//	-Thanks to Brainstorm.
//============================================================

prontera,152,194,4	script	Guide#prt::PrtGuide	105,{
	cutin "prt_soldier",2;
	mes "[Prontera Guide]";
	mes "ยินดีต้อนรับสู่เมืองหลวง Prontera,";
	mes "ที่สวยงามของ";
	mes "ราชอาณาจักร Rune-Midgarts Kingdom.";
	mes "ถ้าคุณมีคำถามหรือต้องการความช่วยเหลือในการค้นหาสิ่งที่อยู่ในเมืองอย่างลังเลที่จะถาม.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("~ สอบถามไกด์:~ ลบตำแหน่งทั้งหมดออกจาก Mini-Map:~ หมายเหตุ:~ ยกเลิกการสนทนา")) {
		case 1:
			mes "[Prontera Guide]";
			mes "กรุณาเลือก";
			mes "ตำแหน่งจาก";
			mes "เมนูต่อไปนี้.";
			if (.@compass_check == 0) {
				mes "คุณต้องการที่จะ";
				mes "ให้ฉันทำเครื่องหมายสถานที่";
				mes "บน Mini-Map ของคุณหรือ ?";
				next;
				if (select("~ ใช่:~ ไม่") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("~ สมาคม Swordman:^0000FF~ โบสถ์^000000:~ Prontera Chivalry:~ ร้านจำหน่ายอาวุธ:~ ร้านค้าทั่วไป:~ โรงแรม:~ Trading Post:~ ผับ:~ หอสมุด:~ สำนักงานอาชีพ:~ ปราสาท พรอนเทล่า:~ City Hall:~ ยกเลิกการสนทนา")) {
				case 1:
					mes "[Prontera Guide]";
					mes "สมาคม Swordman,";
					mes "จะดูแลเกี่ยวกับการเปลี่ยนอาชีพของ";
					mes "Swordman ทั้งหมด, แต่";
					mes "มันได้ถูกย้ายไปประจำเมือง Izlude. สถานที่นี้";
					mes "เป็นเพียงแค่อาคารว่างเปล่าในขณะนี้.";
					if (.@compass_check)
						viewpoint 1,237,41,4,0x00FF00;
					break;
				case 2:
					mes "[Prontera Guide]";
					mes "โบสถ์ Prontera";
					mes "จะดูแลเกี่ยวกับเปลี่ยนอาชีพ";
					mes "ของ Acolyte ทั้งหมด, และ สามารถ";
					mes "พบได้ในภาคตะวันออกเฉียงเหนือ";
					mes "มุมเมืองของ Prontera.";
					if (.@compass_check)
						viewpoint 1,236,316,5,0xFF0000;
					break;
				case 3:
					mes "[Prontera Guide]";
					mes "ขุนนางแห่งเมือง Prontera,";
					mes "จะดูแลรับผิดชอบเกี่ยวกับความปลอดภัย";
					mes "ของเงินทุนของเรา";
					mes "จะตั้งอยู่ที่มุมตะวันตกเฉียงเหนือของเมือง Prontera.";
					if (.@compass_check)
						viewpoint 1,46,345,6,0x00FF00;
					break;
				case 4:
					mes "[Prontera Guide]";
					mes "ร้านจำหน่ายอาวุธ";
					mes "ตั้งอยู่ทางทิศตะวันออกเฉียงเหนือ";
					mes "ของน้ำพุใจกลางเมือง.";
					if (.@compass_check)
						viewpoint 1,175,220,7,0xFF00FF;
					break;
				case 5:
					mes "[Prontera Guide]";
					mes "ร้านค้าทั่วไป";
					mes "ตั้งอยู่ทางตะวันตกเฉียงเหนือ";
					mes "ของน้ำพุใจกลางเมือง.";
					if (.@compass_check)
						viewpoint 1,134,221,8,0xFF00FF;
					break;
				case 6:
					mes "[Prontera Guide]";
					mes "โรมแรมขนาดเล็กในเมือง Prontera";
					mes "ตั้งอยู่ทั้งสองที่ ไปทางทิศตะวันออก";
					mes "และ ทางตะวันตกของเมือง Prontera";
					mes "ของน้ำพุใจกลางเมือง.";
					if (.@compass_check) {
						viewpoint 1,204,191,9,0xFF00FF;
						viewpoint 1,107,192,10,0xFF00FF;
					}
					break;
				case 7:
					mes "[Prontera Guide]";
					mes "Trading Post";
					mes "สามารถพบทางตะวันออกเฉียงใต้";
					mes "ของน้ำพุใจกลางเมือง.";
					if (.@compass_check)
						viewpoint 1,179,184,11,0x00FF00;
					break;
				case 8:
					mes "[Prontera Guide]";
					mes "ผับตั้งอยู่";
					mes "ทางตะวันออกเฉียงใต้ของน้ำพุ,";
					mes "ที่อยู่เบื้องหลังของ Trading Post.";
					if (.@compass_check)
						viewpoint 1,208,154,12,0x00FF00;
					break;
				case 9:
					mes "[Prontera Guide]";
					mes "ถ้าคุณมุ่งหน้าไปทางเหนือจาก";
					mes "น้ำพุกลางใจเมือง คุณจะ";
					mes "คุณจะพบพื้นที่ว่างเปล่า";
					mes "คุณสามารถที่จะไปหอสมุดของ Prontera";
					mes "ได้ถ้าคุณหันหัวไปยังทางทิศตะวันออก";
					mes "หรือ ทิศตะวันตก.";
					if (.@compass_check) {
						viewpoint 1,120,267,13,0x00FF00;
						viewpoint 1,192,267,14,0x00FF00;
					}
					break;
				case 10:
					mes "[Prontera Guide]";
					mes "สำนักงานอาชีพ";
					mes "ตั้งอยู่ตะวันตกเฉียงใต้ของ";
					mes "น้ำพุกลางใจเมือง.";
					if (.@compass_check)
						viewpoint 1,133,183,15,0x00FF00;
					break;
				case 11:
					mes "[Prontera Guide]";
					mes "ปราสาท Prontera Castle";
					mes "ตั้งอยู่ที่ภาคเหนือ";
					mes "ของเพื้นที่นี้. คุณสามารถ";
					mes "ไปที่นอกเมืองของ Prontera";
					mes "โดยจะผ่านปราสาทออกทางด้านหลัง";
					if (.@compass_check)
						viewpoint 1,156,360,16,0x00FF00;
					break;
				case 12:
					mes "[Prontera Guide]";
					mes "City Hall";
					mes "ตั้งอยู่ใน";
					mes "มุมตะวันตกเฉียงใต้";
					mes "ของเมือง Prontera.";
					if (.@compass_check)
						viewpoint 1,75,91,17,0x01FF01;
					break;
				case 13:
					mes "[Prontera Guide]";
					mes "กรุณาถามฉันถ้าหากคุณต้องการ ''ลบ";
					mes "ตำแหน่งจาก Mini-Map'' ถ้าคุณ";
					mes "ไม่ต้องการที่จะมี";
					mes "เครื่องหมายแสดงสถานที่ที่ตั้ง";
					mes "บน Mini-Map ของคุณ.";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,4,0x00FF00;
			viewpoint 2,237,41,5,0x0000FF;
			viewpoint 2,46,345,6,0x00FF00;
			viewpoint 2,175,220,7,0xFF0000;
			viewpoint 2,134,221,8,0xFF0000;
			viewpoint 2,204,191,9,0xFF0000;
			viewpoint 2,107,192,10,0xFF0000;
			viewpoint 2,179,184,11,0x00FF00;
			viewpoint 2,208,154,12,0x00FF00;
			viewpoint 2,120,267,13,0x00FF00;
			viewpoint 2,192,267,14,0x00FF00;
			viewpoint 2,133,183,15,0x00FF00;
			viewpoint 2,156,360,16,0x00FF00;
			viewpoint 2,75,91,17,0x00FF00;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Prontera Guide]";
			mes "ด้วยความก้าวหน้าในการใช้เวทมนตร์";
			mes "และ เทคโนโลยีเราได้รับอนุญาติให้";
			mes "เราในการปรับปรุงข้อมูลของเรา";
			mes "ในระบบ, ที่จะช่วยให้การขึ้นเครื่องหมายขนาดเล็ก";
			mes "อยู่บนแผนที่ เพื่อสำหรับ";
			mes "การนำทางที่แสนจะง่ายดายขึ้น.";
			next;
			mes "[Prontera Guide]";
			mes "คุณมี Mini-Map ตั้งอยู่";
			mes "ที่มุมบนด้านขวาของจอ";
			mes "ถ้าคุณไม่สามารถ";
			mes "ที่จะมองเห็นแผนที่ของคุณ, โปรดกด Ctrl + Tab";
			mes "หรือ คลิกที่ปุ่ม ''Map'' ของคุณที่อยู่ในหน้าต่าง Basic Info.";
			next;
			mes "[Prontera Guide]";
			mes "บน Mini-Map ของคุณ,";
			mes "คลิกที่ ''+'' และ ''-''";
			mes "สัญลักษณ์เพื่อที่จะซูมเข้าและซูมออก";
			mes "ในแผนที่ขนาดเล็กของคุณ Mini-Map. เรา";
			mes "หวังว่าคุณจะเพลิดเพลินไปกับการเดินทางของคุณ";
			mes "ที่นี่ในเมืองของ Payon.";
			break;
		case 4:
			mes "[Prontera Guide]";
			mes "ดี, นักผจญภัย...";
			mes "ฉันหวังว่าการเดินทางของคุณผ่าน";
			mes "ราชอาณาจักร Rune-Midgard";
			mes "มีทั้งความสนุกสนานและความปลอดภัย.";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}

prontera,277,210,2	duplicate(PrtGuide)	East Gate-Guide	105

prontera,35,197,6	duplicate(PrtGuide)	West Gate-Guide	105

prontera,162,33,0	duplicate(PrtGuide)	South Gate-Guide	105

prontera,162,328,3	duplicate(PrtGuide)	North Gate-Guide	105
