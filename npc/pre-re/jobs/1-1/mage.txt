//===== rAthena Script ======================================= 
//= Mage Job Quest
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job quest for Mage classes
//===== Additional Comments: ================================= 
//= 1.0 Fully working.
//= 1.1 Fixed input ingridients bug [Lupus]
//= 1.2 Added instant job change for High Novice [Lupus]
//= 1.3 Added Baby Class support [Lupus]
//= 1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//= 1.5 Now saves/restores all quest skills [Lupus]
//= 1.6 Merged back JFunc, CHANGED NUMBERS TO CONSTANTS [Lupus]
//= 1.7 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=     No longer uses function "F_ToHigh"
//= 1.7a Fixed a "If" in the quest. (bugreport:489) [Samuray22]
//= 1.8 Added Quest Log commands. [L0ne_W0lf]
//= 1.9 Fixed an issue with completequest by adding checkquest. [Kisuka]
//= 2.0 Moved "Dollshoi" and "Ponka-Hontas" NPCs. [Euphy]
//============================================================ 

geffen_in,164,124,4	script	Mage Guildsman	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[Mage Guildsman]";
				mes "โย่! ไม่เจอนาน! แต่ไม่ใช่ว่าเจ้าตายไปหรือเปล่า?";
				next;
				mes "[Mage Guildsman]";
				mes "อ๊ะ เจ้าต้องได้รับการเกิดใหม่ใช่ไหม? ยินดีที่เจ้ากลับมา";
				next;
				if (!callfunc("F_CanChangeJob")) {
					mes "[Mage Guildsman]";
					mes "ขอโทษนะ แต่เราคิดว่าเจ้ายังไม่พร้อมที่จะเรียนเวทย์ได้. ทำไมเจ้าไม่ไปเสริมทักษะพื้นฐานก่อน?";
					next;
					mes "[Mage Guildsman]";
					mes "ใช้เวลานานหน่อยไป, ยิ่งเจ้าเรียนรู้มากขึ้น, เจ้าก็จะพร้อมที่จะเรียนเวทย์ได้มากขึ้น";
					close;
				}
				mes "[Mage Guildsman]";
				mes "ดีแล้ว, เนื่องจากเจ้าผ่านการทดสอบของเวทย์มาแล้วซักครั้ง, เราจะไม่สงสัยถึงเจ้าว่ามีความรู้สึกพร้อมหรือยัง เจ้าอยากได้ทักษะเวทย์กลับมาทันทีใช่ไหม?";
				next;
				skill "NV_TRICKDEAD",0,SKILL_PERM;
				jobchange Job_Mage_High;
				skill "MG_ENERGYCOAT",1,SKILL_PERM;
				mes "[Mage Guildsman]";
				mes "ว้าว, มีเหตุผลที่เจ้าดูดีมาก น่าจะดีกว่าเดิมแน่. ยุคนี้, เราเชื่อว่าเจ้าจะทำหน้าที่ของเวทย์ได้ดีขึ้นเช่นกัน";
				close;
			}
			else {
				mes "[Mage Guildsman]";
				mes "มีอะไรที่ข้าจะช่วยเหลือเจ้าได้อีกไหม? ถ้าไม่มี, ทำไมเจ้าไม่ไปทดสอบทักษะของเจ้า? โลกนี้กำลังรอเจ้าอยู่~!";
				close;
			}
		}
		else {
			mes "[Mage Guildsman]";
			mes "อะไรนะ, เจ้าสนใจสานต่อเวทย์มนตร์ใช่ไหม? ข้าไม่อยากบอกเจ้าว่านี้, แต่เจ้าไม่ได้อยู่ที่นี่";
			next;
			mes "[Mage Guildsman]";
			mes "ข้าไม่แน่ใจว่าทำไมเจ้ายังยืนอยู่หน้าข้า, แต่ข้าสามารถบอกได้ว่าเจ้าไม่เหมาะกับการเป็นนักเวทย์";
			close;
		}
	}
	mes "[Mage Guildsman]";
	mes "เย้! มีไรให้ช่วยไหม?";
	next;
switch(select("ข้าต้องการเป็นนักเวทย์.:ข้อมูลความต้องการ.:ไม่มีอะไร.")) {
	case 1:
		mes "[Mage Guildsman]";
		if (BaseJob == Job_Mage) {
			mes "เธอ, นายไม่ได้รับรู้หรือยัง? นายก็เป็นนักเวทย์แล้วนะ, น่าขบขัน!";
			next;
			mes "[Mage Guildsman]";
			mes "มีวันหนึ่งนายจะรู้สึกถึงพลังภายในนายเมื่อนายสามารถสร้างไฟด้วยจิตใจของนาย!";
			close;
		}
		if (BaseJob != Job_Novice) {
			mes "เออ, มาเถอะ. เลิกเล่นเกมส์. นายไม่สามารถเป็นนักเวทย์ได้เพราะว่านายมีงานอื่นอยู่แล้ว.";
			close;
		}
		if (job_magician_q == 0) {
			mes "อยากเป็นนักเวทย์หรือเปล่า...?";
			if (Sex == SEX_MALE)
				mes "เฮ้ย, มองนายหน่อย! นายน่ารักมาก~! แม้ไม่ใช่ประเภทของข้าหรอก...";
			else {
				mes "โอ้, นายสวยมาก~!";
				mes "ข้าชอบสาวเหล่านี้~";
			}
			next;
			mes "[Mage Guildsman]";
			mes "โอเค, นายบอกว่าต้องการเป็นนักเวทย์ใช่ไหม? โอเคนะ, ลงนามในใบสมัครเวทย์นะ.";
			next;
			if (select("ลงนาม.:ยกเลิก.") == 1) {
				mes "[Mage Guildsman]";
				mes "โอเค. ลงนามที่นี่เลย. โอ้, นายเก่งมากเลย. นายมีความสามารถในการสะกดคำที่ดีมาก. โอเค. นายชื่อว่า... " + strcharinfo(0) + ".";
				next;
				mes "[Mage Guildsman]";
				mes "ตอนนี้เวลาของ";
				mes "ข้าทดสอบนาย";
				switch(rand(0,3)) {
				case 1:
					mes "ทำให้ข้าเห็น";
					mes "^3355FFMixed Solution No. 1^000000";
					mes "แล้วนำมาให้ข้า";
					set job_magician_q,1;
					break;
				case 2:
					mes "ทำให้ข้าเห็น";
					mes "^3355FFMixed Solution No. 2^000000";
					mes "แล้วนำมาให้ข้า";
					set job_magician_q,2;
					break;
				case 3:
					mes "ทำให้ข้าเห็น";
					mes "^3355FFMixed Solution No. 3^000000";
					mes "แล้วนำมาให้ข้า";
					set job_magician_q,3;
					break;
				default:
					mes "ทำให้ข้าเห็น";
					mes "^3355FFMixed Solution No. 4^000000";
					mes "แล้วนำมาให้ข้า";
					set job_magician_q,4;
				}
				next;
				getitem 1092,1; //Empty_Cylinder
				mes "[Mage Guildsman]";
				mes "เจ้าสามารถหาส่วนประกอบที่จำเป็นในหนังสือแนะนำในห้องนี้. ดังนั้น, เจ้าควรดูว่าเจ้าต้องการอะไรก่อนที่จะไป.";
				next;
				mes "[Mage Guildsman]";
				mes "เมื่อเจ้าเก็บส่วนประกอบทั้งหมดมาแล้ว, ให้ใช้เครื่องทำน้ำตาลที่ตั้งอยู่ตรงกลางห้องเพื่อผสมน้ำตาล. โชคดีนะ!";
				close;
			}
			mes "[Mage Guildsman]";
			mes "ว้าย~?! เพิ่งบอกว่าจะเป็นนักเวทย์เสร็จ, ก็เปลี่ยนใจเลย?! เป็นคนตัดสินใจหน่อย!";
			close;
		}
		mes "โอ้, จริงหรือ?";
		if (!callfunc("F_CanChangeJob")) {
			mes "โอ้, โชคร้าย. นายยังไม่ได้ทำเจ้าสมบัติเลย.";
			next;
			mes "[Mage Guildsman]";
			mes "กลับไปเพื่อให้ Novice Job level 9 ก่อน. อย่าลืมว่านายต้องเรียนรู้ทักษะพื้นฐานทั้งหมดก่อนที่จะกลับมานะ.";
			close;
		}
		mes "" + strcharinfo(0) + " ทดสอบของเจ้า...";
		if (job_magician_q == 1)
			mes "ทำให้มี ^3355FFMixed Solution No. 1^000000.";
		else if (job_magician_q == 2)
			mes "ทำให้มี ^3355FFMixed Solution No. 2^000000.";
		else if (job_magician_q == 3)
			mes "ทำให้มี ^3355FFMixed Solution No. 3^000000.";
		else
			mes "ทำให้มี ^3355FFMixed Solution No. 4^000000.";
		next;
		mes "[Mage Guildsman]";
		mes "โอ้, ประตูของข้า";
		mes "จะตรวจสอบความถูกต้อง";
		mes "ของการทำของนาย...";
		next;
		mes "[Mage Guildsman]";
		if (countitem(1071) == 0 && countitem(1085) == 0 && countitem(1086) == 0 && countitem(1087) == 0 && countitem(1090) == 0) {
			mes "เฮ, ที่ Solution อยู่ไหน";
			mes "ที่ข้าขอ...? ข้าไม่สามารถตรวจสอบได้ถ้านายไม่นำมาให้ข้าเห็น, ถูกไหม?";
			close;
		}
		else {
			if ((job_magician_q == 1 && countitem(1071) == 0) || (job_magician_q == 2 && countitem(1085) == 0) || (job_magician_q == 3 && countitem(1086) == 0) || (job_magician_q == 4 && countitem(1087) == 0)) {
				mes "รอสักครู่.";
				mes "นี่ไม่ใช่";
				mes "Solution ที่ข้าขอ!";
				next;
				mes "[Mage Guildsman]";
				if (job_magician_q == 1)
					mes "นายควรทำ Mixed Solution No. 1 แล้วนำมาให้ข้า. ตอนนี้ไปลองอีกครั้ง.";
				else if (job_magician_q == 2)
					mes "นายควรทำ Mixed Solution No. 2 แล้วนำมาให้ข้า. ตอนนี้ไปลองอีกครั้ง.";
				else if (job_magician_q == 3)
					mes "นายควรทำ Mixed Solution No. 3 แล้วนำมาให้ข้า. ตอนนี้ไปลองอีกครั้ง.";
				else
					mes "นายควรทำ Mixed Solution No. 4 แล้วนำมาให้ข้า. ตอนนี้ไปลองอีกครั้ง.";

				if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
				else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
				else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
				else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
				else delitem 1090,1; //Mage_Test_Etc
				close;
			}
		}
		if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
		else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
		else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
		else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
		else delitem 1090,1; //Mage_Test_Etc
		mes "ฮืม ข้าเห็นว่าเจ้าพยายามหนักมาก เพื่อคนใหม่มีความพยายามนี้น่าชื่นชม";
		mes "งานดี!";
		next;
		mes "[Mage Guildsman]";
		mes "โอเค! ยินดีที่จะบอกว่าเจ้าผ่านการทดสอบของเราแล้ว ข้าจะเปลี่ยนเจ้าทันที!";
		next;
		mes "[Mage Guildsman]";
		mes "*เฮ่ม*";
		mes "ขอแสดงความยินดี!";
		mes "ตอนนี้เจ้าเป็นนักเวทย์แล้ว!";
		next;
		for ( .@quest_id = 1005; .@quest_id <= 1008; .@quest_id++ ) {
			if (isbegin_quest(.@quest_id) == 1)
				completequest .@quest_id;
		}
		callfunc "Job_Change",Job_Mage;
		callfunc "F_ClearJobVar";
		set Zeny,Zeny+50;
		mes "[Mage Guildsman]";
		mes "'ยินดีต้อนรับสู่โลกของข้า~'";
		mes "เฮฮ เฮฮ เฮฮ ข้าแค่ต้องการพูดว่า เจ้ารู้สึกได้ดี! นี่คือคำพูดจากหนังที่มีชื่อเสียงนะ~";
		next;
		mes "[Mage Guildsman]";
		mes "ตอนนี้ที่เจ้าเป็นนักเวทย์เหมือนกัน เรามาเป็นเพื่อนกันนะ?";
		close;
	case 2:
		mes "[Mage Guildsman]";
		mes "อยากเป็นนักเวทย์หรือยัง?";
		if (Sex == SEX_MALE)
			mes "สำหรับเด็กน่ารักแบบเจ้า ข้ายินดีที่จะอธิบายเกี่ยวกับข้อกำหนด!";
		else
			mes "ข้ายินดีที่จะอธิบายข้อกำหนดสำหรับสาวน้อยสวยๆ เหล่านี้!";
		next;
		mes "[Mage Guildsman]";
		mes "ก่อนอื่น ๆ เจ้าต้องJob Novice ไปถึงเลเวล 10 และเรียนรู้ทักษะพื้นฐานทั้งหมด จากนั้นเจ้าต้องผ่านการทดสอบของนักเวทย์";
		next;
		if (job_magician_q != 0) {
		mes "[Mage Guildsman]";
		mes "การทดสอบของเจ้าคือ";
		switch(job_magician_q) {
		case 1:
			mes "ปรุงยา";
			mes "^3355FFMixed Solution No. 1^000000";
			mes "แล้วนำมาให้ข้า";
			if (isbegin_quest(1005) == 0)
				setquest 1005;
			break;
		case 2:
			mes "ปรุงยา";
			mes "^3355FFMixed Solution No. 2^000000";
			mes "แล้วนำมาให้ข้า";
			if (isbegin_quest(1006) == 0)
				setquest 1006;
			break;
		case 3:
			mes "ปรุงยา";
			mes "^3355FFMixed Solution No. 3^000000";
			mes "แล้วนำมาให้ข้า";
			if (isbegin_quest(1007) == 0)
				setquest 1007;
			break;
		default:
			mes "ปรุงยา";
			mes "^3355FFMixed Solution No. 4^000000";
			mes "แล้วนำมาให้ข้า";
			if (isbegin_quest(1008) == 0)
				setquest 1008;
			break;
		}
		next;
		mes "[Mage Guildsman]";
		mes "เจ้าสามารถดูวัตถุดิบที่เจ้าจะต้องใช้ในการสร้าง Solution ภายใน Guide Book ในสำนักงานนี้ได้";
	}
	else {
		mes "[Mage Guildsman]";
		mes "เจ้าจะได้รับข้อมูลเกี่ยวกับ Mixed Solution ที่เจ้าต้องสร้างหลังจากลงนามใบสมัคร";
	}
	next;
	mes "[Mage Guildsman]";
	mes "แจ้งให้ข้าทราบเมื่อเจ้าพร้อมที่จะเป็นนักเวทย์ได้นะ?";
	close;
	case 3:
		mes "[Mage Guildsman]";
		mes "ไม่มีอะไรเหรอ...?";
		close;
	}
}

geffen_in,164,112,4	script	Mixing Machine	111,{
mes "[Mixing Machine]";
mes "เครื่องนี้เป็นทรัพย์สมบัติของสภานักเวทย์ Geffen และใช้สำหรับการผสมสารเพื่อวัตถุประสงค์ทางเวทย์";
next;
if (select("ใช้เครื่อง.:ยกเลิก.") == 1) {
	mes "[Mixing Machine]";
	mes "เลือก";
	mes "สารละลาย";
	mes "สำหรับ Solution";
	next;
	switch(select("Payon Solution.:Morocc Solution.:No Solvent.")) {
	case 1:
		if (countitem(1089) == 0) {
			mes "[Mixing Machine]";
			mes "ข้อผิดพลาด";
			mes "ไม่พบไอเท็ม";
			mes "กรุณาตรวจสอบอีกครั้ง";
			mes "กระบวนการถูกหยุด";
			close;
		}
		set .@mixitem2,1;
		break;
	case 2:
		if (countitem(1088) == 0) {
			mes "[Mixing Machine]";
			mes "ข้อผิดพลาด";
			mes "ไม่พบไอเท็ม";
			mes "กรุณาตรวจสอบอีกครั้ง";
			mes "กระบวนการถูกหยุด";
			close;
		}
		set .@mixitem2,2;
		break;
	case 3:
		set .@mixitem2,0;
		break;
	}
	while (1) {
		if (.@progress == 2) {
			mes "[Mixing Machine]";
			if (.@mixitem1_1 != 0) mes "Jellopy: " + .@mixitem1_1 + " ea.";
			if (.@mixitem1_2 != 0) mes "Fluff: " + .@mixitem1_2 + " ea.";
			if (.@mixitem1_3 != 0) mes "Milk: " + .@mixitem1_3 + " ea.";
			if (.@mixitem2 == 0) mes "Solvent: ไม่มี";
			if (.@mixitem2 == 1) mes "Solvent: Payon Solution";
			if (.@mixitem2 == 2) mes "Solvent: Morocc Solution";
			next;
			mes "[Mixing Machine]";
			mes "กรุณาเลือกว่าเจ้าต้องการเริ่มกระบวนการผสมหรือป้อนจำนวนไอเท็มใหม่";
			next;
			switch(select("เริ่มการผสม.:ป้อนจำนวนไอเท็ม.:รีเซ็ต.")) {
			case 1:
				mes "[Mixing Machine]";
				mes "กรุณาวางไอเท็มในถังผสมทันที โปรดรอ";
				next;
				mes "[Mixing Machine]";
				mes "เจ้าไม่สามารถปรับแต่งหรือคืนไอเท็มได้เมื่อไอเท็มถูกวางในถังผสม";
				next;
				mes "[Mixing Machine]";
				mes "หากทุกอย่างถูกต้อง กดปุ่ม 'Mix' เมื่อเจ้าพร้อม มิเช่นนั้น กดปุ่ม 'Cancel'";
				next;
				if (select("กดปุ่ม 'Mix'.:กดปุ่ม 'Cancel'.") == 1) {
					mes "[Mixing Machine]";
					mes "วางไอเท็มในถังผสมทันที กรุณารอ";
					next;
					mes "[Mixing Machine]";
					if (countitem(909) < .@mixitem1_1) {
						mes "Jellopy ไม่เพียงพอ";
						mes "กรุณาตรวจสอบอีกครั้ง";
						mes "กระบวนการถูกหยุด";
						close;
					}
					else if (countitem(914) < .@mixitem1_2) {
						mes "Fluff ไม่เพียงพอ";
						mes "กรุณาตรวจสอบอีกครั้ง";
						mes "กระบวนการถูกหยุด";
						close;
					}
					else if (countitem(519) < .@mixitem1_3) {
						mes "Milk ไม่เพียงพอ";
						mes "กรุณาตรวจสอบอีกครั้ง";
						mes "กระบวนการถูกหยุด";
						close;
					}
					else if (.@mixitem2 == 1 || .@mixitem2 == 2) {
						if (countitem(1089) == 0 && countitem(1088) == 0) {
							mes "ไม่พบ Solution";
							mes "กรุณาตรวจสอบอีกครั้ง";
							mes "กระบวนการถูกหยุด";
							close;
						}
					}
					if (.@mixitem1_1 != 0) delitem 909,.@mixitem1_1; //Jellopy
					if (.@mixitem1_2 != 0) delitem 914,.@mixitem1_2; //Fluff
					if (.@mixitem1_3 != 0) delitem 519,.@mixitem1_3; //Milk
					if (.@mixitem2 == 1) delitem 1089,1; //Payon_Potion
					if (.@mixitem2 == 2) delitem 1088,1; //Morocc_Potion
					mes "ไอเท็มพร้อมแล้ว";
					mes "ปิดฝาถุงผสม";
					set .@progress,3;
					next;
				}
				break;
				case 2:
					set .@continue,0;
					next;
					break;
				case 3:
					set .@mixitem1_1,0;
					set .@mixitem1_2,0;
					set .@mixitem1_3,0;
					set .@progress,0;
					set .@continue,0;
					mes "[Mixing Machine]";
					mes "รีเซ็ตสมบัติเสร็จสมบูรณ์";
					mes "เริ่มต้นใหม่อีกครั้งหรือไม่?";
					next;
					if (select("ใช่.:ไม่.") == 1)
						break;
					mes "[Mixing Machine]";
					mes "กระบวนการถูกหยุด";
					mes "ขอบเจ้าครับ";
					close;
				}
				if (.@progress == 3) break;
			}
			else if (.@progress == 1) {
				mes "[Mixing Machine]";
				mes "ไม่พบไอเท็ม";
				next;
			}
			mes "[Mixing Machine]";
			mes "เลือกไอเท็มที่ต้องการผสม";
			while(1) {
				switch(select("Jellopy.:Fluff.:Milk.:พร้อมที่จะผสม.")) {
				case 1:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Mixing Machine]";
							mes "ข้อผิดพลาด: การเกินขีดจำกัดของไอเท็ม กรุณาป้อนค่าน้อยกว่า 10,000 แล้วลองอีกครั้ง";
							next;
						}
						else {
							if (countitem(909) > 0) set .@mixitem1_1,.@mixitem1_1 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 2:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Mixing Machine]";
							mes "ข้อผิดพลาด: การเกินขีดจำกัดของไอเท็ม กรุณาป้อนค่าน้อยกว่า 10,000 แล้วลองอีกครั้ง";
							next;
						}
						else {
							if (countitem(914) > 0) set .@mixitem1_2,.@mixitem1_2 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 3:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[Mixing Machine]";
							mes "ข้อผิดพลาด: การเกินขีดจำกัดของไอเท็ม กรุณาป้อนค่าน้อยกว่า 10,000 แล้วลองอีกครั้ง";
							next;
						}
						else {
						if (countitem(519) > 0) set .@mixitem1_3,.@mixitem1_3 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;

				case 4:
					if (.@progress != 2)
						set .@progress,1;
					set .@continue,1;
					next;
				}
				if (.@continue) break;
			}
		}
		mes "[Mixing Machine]";
		mes "กรุณาป้อน ";
		mes "Serial Number ของ";
		mes "Magic Powder";
		next;
	while(1) {
				input .@input;
				if (.@input < 1000 || .@input > 9999) {
					mes "[Mixing Machine]";
					if (.@input == 0) {
						mes "เจ้าต้องการข้ามเมนูนี้หรือไม่?";
						next;
						if (select("ใช่.:ไม่.") == 1) {
							break;
						}
					}
					else {
						mes "หมายเลขผลิตภัณฑ์ไม่ถูกต้อง.";
						mes "กรุณาลองอีกครั้ง.";
						next;
					}
				}
				else {
					mes "[Mixing Machine]";
					mes "หมายเลขผลิตภัณฑ์คือ #" + .@input + ", ถูกต้องใช่หรือไม่?";
					next;
					if (select("ยืนยัน.:ยกเลิก.") == 1) {
						if (.@input == 8472)
							set .@magic_powder,1;
						else if (.@input == 3735)
							set .@magic_powder,2;
						else if (.@input == 2750)
							set .@magic_powder,3;
						else if (.@input == 5429)
							set .@magic_powder,4;
						else
							set .@magic_powder,5;
					}
					break;
				}
			}
			mes "[Mixing Machine]";
			mes "เลือก";
			mes "Catalyst Stone.";
			next;
			switch(select("Yellow Gemstone.:Red Gemstone.:Blue Gemstone.:1carat Diamond.:ข้าม.")) {
			case 1: set .@mixitem3,1; break;
			case 2: set .@mixitem3,2; break;
			case 3: set .@mixitem3,3; break;
			case 4: set .@mixitem3,4; break;
			case 5: break;
			}
			mes "[Mixing Machine]";
			mes "พร้อมแล้ว.";
			mes "เริ่มกระบวนการ";
			mes "ผสม.";
			mes "กรุณารอสักครู่.";
			next;
			mes "[Mixing Machine]";
			mes "- สุภาษิตของวันนี้ -";
			switch(rand(1,5)) {
			case 1:
				mes "An Eye for an Eye: เมื่อเจ้าเอาแล้วต้องให้กลับหรือชดเชยในทางใดทางหนึ่ง";
				break;
			case 2:
				mes "Credibility is a Man's Currency: ความน่าเชื่อถือคือสกุลเงินของคน มีค่ามาก";
				break;
			case 3:
				mes "What Goes Around Comes Around: ในที่สุดเจ้าจะถูกดูแลในทางที่เจ้าดูแลผู้อื่น";
				mes "หมายความว่า 'เมื่อเจ้าทำร้ายคนอื่น เจ้าจะถูกร้ายโดยเขาในสถานการณ์ที่ไม่สามารถหลีกเลี่ยงได้'";
				break;
			case 4:
				mes "A good neighbor is better than a distant brother: เมื่อเจ้าต้องการความช่วยเหลือ เจ้าสามารถนับได้ว่ามีใครอยู่ใกล้เจ้า";
				break;
			default:
				mes "Birds of a Feather Flock Together: เจ้าสามารถดูเพื่อนของคนเพื่อวัดบุคลิกภาพของเขา";
			}
			next;
			if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 1 && .@mixitem2 == 1 && .@mixitem3 == 1 && .@magic_powder == 1) {
				mes "[Mixing Machine]";
				mes "ผลิตภัณฑ์ทดสอบเมจ No. 1.";
				getitem 1071,1; //Mage_Test_1
				next;
			}
			else if (.@mixitem1_1 == 3 && .@mixitem1_2 == 1 && .@mixitem1_3 == 1 && .@mixitem2 == 0 && .@mixitem3 == 2 && .@magic_powder == 2) {
				mes "[Mixing Machine]";
				mes "ผลิตภัณฑ์ทดสอบเมจ No. 2.";
				getitem 1085,1; //Mage_Test_2
				next;
			}
			else if (.@mixitem1_1 == 6 && .@mixitem1_2 == 1 && .@mixitem1_3 == 0 && .@mixitem2 == 1 && .@mixitem3 == 3 && .@magic_powder == 3) {
				mes "[Mixing Machine]";
				mes "ผลิตภัณฑ์ทดสอบเมจ No. 3.";
				getitem 1086,1; //Mage_Test_3
				next;
			}
			else if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 0 && .@mixitem2 == 2 && .@mixitem3 == 4 && .@magic_powder == 4) {
				mes "[Mixing Machine]";
				mes "ผลิตภัณฑ์ทดสอบเมจ No. 4.";
				getitem 1087,1; //Mage_Test_4
				next;
			}
			else {
				mes "[Mixing Machine]";
				mes "เกิดข้อผิดพลาด";
				mes "ที่ไม่คาดคิด.";
				getitem 1090,1; //Mage_Test_Etc
				next;
			}
			mes "[Mixing Machine]";
			mes "กระบวนการผสมเสร็จสมบูรณ์.";
			mes "ขอบเจ้าครับ.";
			close;
		}
		close;
	}

// The Morocc Solution and Payon Solution traders
// are also used in the Sage skill quests, and have
// been moved to npc/quests/mage_solution.txt.
//
// pay_arche,122,100,0	script	Dollshoi	88,{}
// moc_ruins,91,150,0	script	Ponka-Hontas	93,{}

geffen_in,177,112,4	script	Bookshelf	111,{
	mes "[Guide Book]";
	mes "หนังสือนี้เป็นทรัพย์สมบัติของสมาคมผู้ใช้เวทย์มนตร์ Geffen กรุณารักษาด้วยความระมัดระวัง";
	next;
	switch(select("Solution No. 1.:Solution No. 2.:Solution No. 3.:Solution No. 4.:Close.")) {
	case 1:
		mes "[Mage Test Solution No. 1]";
		mes "* รายการส่วนผสม *";
		mes "2 Jellopy";
		mes "3 Fluff";
		mes "1 Milk";
		next;
		mes "[Mage Test Solution No. 1]";
		mes "* สารละลาย *";
		mes "Payon Solution";
		mes "ที่จะหา:";
		mes "บริเวณบ่อน้ำเล็กใน Payon, หมู่บ้านผู้ยิงธนู";
		next;
		mes "[Mage Test Solution No. 1]";
		mes "* รหัสซีเรียลของพลังมนตร์ *";
		mes "8472";
		next;
		mes "[Mage Test Solution No. 1]";
		mes "* สารเร่ง *";
		mes "Yellow Gemstone";
		mes "(ใส่ลงเครื่องผสม)";
		close;
	case 2:
		mes "[Mage Test Solution No. 2]";
		mes "* รายการส่วนผสม *";
		mes "3 Jellopy";
		mes "1 Fluff";
		mes "1 Milk";
		next;
		mes "[Mage Test Solution No. 2]";
		mes "* สารละลาย *";
		mes "ไม่มี";
		next;
		mes "[Mage Test Solution No. 2]";
		mes "* รหัสซีเรียลของพลังมนตร์ *";
		mes "3735";
		next;
		mes "[Mage Test Solution No. 2]";
		mes "* สารเร่ง *";
		mes "Red Gemstone";
		mes "(ใส่ลงเครื่องผสม)";
		close;
	case 3:
		mes "[Mage Test Solution No. 3]";
		mes "* รายการส่วนผสม *";
		mes "6 Jellopy";
		mes "1 Fluff";
		next;
		mes "[Mage Test Solution No. 3]";
		mes "* สารละลาย *";
		mes "Payon Solution";
		mes "ที่จะหา:";
		mes "บริเวณบ่อน้ำเล็กใน Payon, หมู่บ้านผู้ยิงธนู";
		next;
		mes "[Mage Test Solution No. 3]";
		mes "* รหัสซีเรียลของพลังมนตร์ *";
		mes "2750";
		next;
		mes "[Mage Test Solution No. 3]";
		mes "* สารเร่ง *";
		mes "Blue Gemstone";
		mes "(ใส่ลงเครื่องผสม)";
		close;
	case 4:
		mes "[Mage Test Solution No. 4]";
		mes "* รายการส่วนผสม *";
		mes "2 Jellopy";
		mes "3 Fluff";
		next;
		mes "[Mage Test Solution No. 4]";
		mes "* สารละลาย *";
		mes "Morocc Solution";
		mes "ที่จะหา:";
		mes "บริเวณบ่อน้ำเล็กใน Morocc";
		next;
		mes "[Mage Test Solution No. 4]";
		mes "* รหัสซีเรียลของพลังมนตร์ *";
		mes "5429";
		next;
		mes "[Mage Test Solution No. 4]";
		mes "* สารเร่ง *";
		mes "1 carat Diamond";
		mes "(ใส่ลงเครื่องผสม)";
		close;
	}
	close;
}
