//===== rAthena Script ======================================= 
//= Gym Pass Cash Item NPC
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Exchange Gym Passes to learn "Enlarge Weight Limit"
//= iRO NPC situated in Payon beside Kafra Shop.
//===== Additional Comments: ================================= 
//= 1.0 First version [L0ne_W0lf]
//= 1.1 Fixed the NPC saying you "00" [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [Samuray22]
//= 1.3 Updated script, and fixed potential errors. [L0ne_W0lf]
//============================================================ 

morocc,109,71,5	script	เพิ่มน้ำหนัก GYM#GymPass	10119,{
	cutin "ep16_kronecker_granz_heine.png",2;
	mes "[Ripped]";
	mes " ว่าไง ร่างกายของคนเรา";
	mes " ไม่แข็งแรงเหมือนแต่ก่อนว่าไหม";
	mes " แม้ว่จะต่อสู้เพิ่อ";
	mes " ความอยู่รอด ร่างกายเจ้าก็จะ";
	mes " อ่อนแอและหมดแรงลงได้";
	mes " เจ้ารู่ใช่ไหมว่าข้าหมายถึงอะไร?";
	next;
	mes "[Ripped]";
	mes " นี่ถ้าเจ้ามาฝึกกับข้านะ";
	mes " ข้ารับรองได้ว่าเจ้าจะสามารถ";
	mes " ถือของได้มากขึ้นกว่าเดิม";
	mes " เพียงแค่เจ้าเอา";
	mes "^FF0000Gym Pass^000000 มาด้วยทุกครั้ง";
	mes " และทุกอย่างจะเรียบร้อย";
	next;
	mes "[Ripped]";
	mes " แต่ไม่ต้องตื่นเต้นนะ";
	mes " ไม่ว่าข้าจะฝึกเจ้าหนัก";
	mes " แค่ไหน เจ้าก็จะผ่านไปได้";
	mes " เจ้าเคยได้ยินพวกที่";
	mes " อึกมากๆ มั่งไหม?";
	mes " รู้ไหมว่าพวกนั้นตายไปหมดแล้ว?";
	next;
	mes "[Ripped]";
	mes " ข้าเพียงจะบอกเจ้าว่า";
	mes " การฝึกกับข้าเจ้าจะปลอดภัย";
	mes " และเพิ่มความสามารถ";
	mes " ในการแบกของเพิ่มขึ้น ^FF000010 เท่า^000000";
	mes "เอาล่ะ...พร้อมที่จะเหนื่อยหรือยัง?";
	next;
	switch(select(" พร้อมแล้ว: ยัง: เอ่อ พลังของข้าสูญหายไป")) {
	case 1:
		if (gympassmemory < 10) {
			set .@add_carry,gympassmemory + 1;
			set .@remain_carry,10 - .@add_carry;
			if (countitem(7776) > 0) {
				mes "[Ripped]";
				mes " โอ้.. ข้ากลัวว่าข้าจะไม่ได้เห็นเจ้าอีก";
				mes " เจ้านำ Gym Pass กลับมาหาข้าอีก. ";
				mes " พยายามให้เต็มที่ ข้าจะฝึกฝนเจ้าอย่างสุดความสามารถ";
				next;
				specialeffect2 EF_EARTHSPIKE;
				next;
				specialeffect2 EF_DEVIL;
				next;
				specialeffect2 EF_COIN;
				next;
				specialeffect2 EF_SIGHTRASHER;
				next;
				mes "[Ripped]";
				mes " ดี.. การฝึกครั้งนี้จะเป็นการฝึกเพื่อเพิ่มความสามารถของการแบกของให้กับเจ้า";
				mes " ตอนนี้น้ำหนักของเจ้าคือ ^FF00000" + .@remain_carry + "^000000";
				mes " และมันยังเพิ่มต่อไปได้อีก หากเจ้าตั้งใจฝึกฝน";
				delitem 7776,1; //Max_Weight_Up_Scroll
				set gympassmemory,.@add_carry;
				skill "ALL_INCCARRY",.@add_carry,SKILL_PERM_GRANT;
				cutin "",255;
				close;
			}
			else {
				mes "[Ripped]";
				mes " เพื่อนรัก ข้าบอกเจ้าไปแล้วนี่?";
				mes " เจ้าจะต้องเอา";
				mes "^FF0000Gym Pass^000000 มาให้ข้าด้วย";
				mes " หากเจ้าต้องการที่จะ";
				mes " เพิ่มความแข็งแกร่งของกล้ามเนื้อเจ้า";
				cutin "",255;
				close;
			}
		}
		else {
			mes "[Ripped]";
			mes " เพื่อนรัก ข้าคิดว่าเจ้าคงจะเพิ่มความแข็วแกร่งของกล้ามเนื้อไปมากกว่านี้ไม่ได้แล้วล่ะ...";
			mes " มันอันตรายเกินไป!!";
			mes " ร่างกายของเจ้าก็มีขีดจำกัดของมันเหมือนกัน";
			cutin "",255;
			close;
		}
	case 2:
		mes "[Ripped]";
		mes " อ้า แย่จัง";
		mes " งั้นถ้าเจ้าเปลี่ยนใจ";
		mes " ก็กลับมาได้นะ";
		mes " บอกเพื่อนๆ เจ้าด้วย";
		mes " ถ้าพวกเขาไมีมีเรี่ยวแรง";
		mes " ข้าช่วยพวกเขาได้";
		cutin "",255;
		close;
	case 3:
		if (gympassmemory > 0) {
			mes "[Ripped]";
			mes " อะไร...??";
			mes " นี่เจ้าปล่อยให้กล้ามเนื่อของเจ้า แบกของหนักๆ เช่นนี้มานานเท่าไหร่กัน?";
			next;
			specialeffect2 EF_EARTHSPIKE;
			next;
			specialeffect2 EF_DEVIL;
			next;
			specialeffect2 EF_COIN;
			next;
			specialeffect2 EF_SIGHTRASHER;
			next;
			mes "[Ripped]";
			mes " ข้าจะบอกวิธีที่จำให้เจ้าสามารถแบกไอเท็มได้มากขึ้น..?";
			mes " แต่กล้ามเนื้อร่างกายของเจ้าจะขยายตัว และมันยังเพิ่มต่อไปได้อีกหากเจ้าตั้งใจฝึกฝน?";
			skill "ALL_INCCARRY",gympassmemory,SKILL_PERM_GRANT;
			cutin "",255;
			close;
		}
		else {
			mes "[Ripped]";
			mes " อ่า...";
			mes " พวกเราไม่เคยได้ฝึกฝน";
			mes " ด้วยกันมาก่อนนี่";
			mes " ข้าคิดว่าอย่างนั้นนะ";
			cutin "",255;
			close;
		}
	}
OnInit:
	waitingroom "[ เพิ่มแบกน้ำหนัก ]",0;
	end;
}

